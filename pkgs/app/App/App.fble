
@ Drawing@ = /App/Drawing%.Drawing@;

@ Int@ = /Core/Int%.Int@;

<@>@ Get@ = /Core/Process%.Get@;
<@>@ Put@ = /Core/Process%.Put@;

@ Unit@ = /Core/Unit%.Unit@;

@ Key@ = +(
  Unit@ a, Unit@ d, Unit@ h, Unit@ j, Unit@ k, Unit@ l,
  Unit@ q, Unit@ s, Unit@ w,
  Unit@ left, Unit@ right, Unit@ up, Unit@ down,
  Unit@ space, Unit@ lshift, Unit@ rshift
);

@ Resize@ = *(
  Int@ width,
  Int@ height
);

@ Button@ = +(Unit@ left, Unit@ right);
@ MouseButton@ = *(Button@ button, Int@ x, Int@ y);

@ MouseMotion@ = *(
  Int@ x,     # Final x position of mouse in screen coordinates.
  Int@ y,     # Final y position of mouse in screen coordinates.
  Int@ dx,    # Change in x position since previous mouse motion event.
  Int@ dy     # Change in y position since previous mouse motion event.
);

@ Event@ = +(
  Unit@ tick,      # A tick event has occured in response to a tick request
  Key@ key_down,   # A key was pressed down.
  Key@ key_up,     # A key was released.
  MouseButton@ mouse_down,  # A mouse button was pressed.
  MouseButton@ mouse_up,    # A mouse button was released.
  Resize@ resize,  # The window was resized.
  MouseMotion@ mouse_motion   # The mouse has moved.
);

@ Effect@ = +(
  # Request for a tick Event@ to occur after the given number of milliseconds.
  # Time is relative to the time of the previously delivered tick event. The
  # tick will be delivered immediately if the more time has passed since the
  # previous tick event then requested.
  Int@ tick,

  # Draw to the screen.
  #
  # Coordinate (0, 0) is the upper left of the screen, with the positive x
  # access going towards the right edge of the screen and the positive y
  # access going towards the bottom edge of the screen.
  # 
  # The drawing is clipped to fit in the screen area.
  #
  # It is undefined how pixels not specified in the drawing will be displayed.
  Drawing@ draw
);

# AppIO@ --
#   Interface for an App.
#
# Fields:
#   width - The initial width of the screen.
#   height - The initial height of the screen.
#   event - Get the next input event.
#   effect - Output an effect.
@ AppIO@ = *(
  Int@ width,
  Int@ height,
  Get@<Event@> event,
  Put@<Effect@> effect
);

# App@ --
#   User API for describing an App.
#
# Inputs:
#   io - The interface to the application.
#
# Results:
#   None.
@ App@ = (AppIO@) { Unit@!; };

@(App@, AppIO@, Key@, Resize@, Event@, Effect@);
