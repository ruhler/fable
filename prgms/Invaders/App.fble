
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Color@ = /Drawing%.Color@;
@ Point@ = /Drawing%.Point@;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Transform = /Drawing%.Transform;

@ Int@ = /Int/Int%.Int@;
% 0 = /Int/Int%.0;
% Add = /Int/Int%.Add;
% Mul = /Int/Int%.Mul;
% Neg = /Int/Int%.Neg;

% I = /Int/Int/Lit%.I;
% D = /Int/Int/Lit%.D;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

% Ship = /Invaders/Ship%;
@ Dir@ = Ship.Dir@;
@ Ship@ = Ship.Ship@;

@ Input@ = +(
  Unit@ tick,
  Unit@ quit,
  Dir@ dir
);

(Get@<Event@>) { Get@<Input@>; } GetInput = (Get@<Event@> in) {
  Event@ e := in;
  ?(e;
      tick: $(Input@(tick: Unit)),
      key_down: ?(e.key_down;
        q: $(Input@(quit: Unit)),
        left: $(Input@(dir: Dir@(left: Unit))),
        right: $(Input@(dir: Dir@(right: Unit))),
        : GetInput(in)),
      key_up: ?(e.key_up;
        left: $(Input@(dir: Dir@(stopped: Unit))),
        right: $(Input@(dir: Dir@(stopped: Unit))),
        : GetInput(in))
   );
};

(Get@<Input@>, Put@<Ship@>, Unit@!, Ship@) { Unit@!; }
Run = (Get@<Input@> in, Put@<Ship@> draw, Unit@! tick, Ship@ ship) {
  Input@ i := in;
  ?(i;
      tick: {
        Unit@ _ := tick;
        Ship@ nship = Ship.Tick(ship);
        Unit@ _ := draw(nship);
        Run(in, draw, tick, nship);
      },
      quit: $(Unit),
      dir: Run(in, draw, tick, Ship.Input(ship, i.dir)));
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  # Map game coordinates into screen coordinate.
  Int@ ax = I(D|1);
  Int@ bx = I(D|0);
  Int@ ay = Neg(I(D|1));
  Int@ by = I(D|480);

  (Point@) { Point@; } f = (Point@ p) {
    @(x: Add(Mul(ax, p.x), bx), 
      y: Add(Mul(ay, p.y), by));
  };

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = I(D|16);
  Unit@! tick = out(Effect@(tick: frame_period));

  (Ship@) { Unit@!; } draw = (Ship@ s) {
    out(Effect@(draw: Over(
      Rect(0, 0, width, height, Color@(black: Unit)),
      Transform(f, Ship.Draw(s)))));
  };

  Ship@ ship = Ship.Initial;
  Unit@ _ := out(Effect@(tick: I(D|0)));
  Run(GetInput(in), draw, tick, ship); 
};

Main;
