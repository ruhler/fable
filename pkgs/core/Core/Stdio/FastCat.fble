
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ Char@ = /Core/Char%.Char@;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

@ World@ = /Core/Monad/IO%.World@;
% m = /Core/Monad/IO%.Monad;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;

<@>@ R@ = <@ T@> { *(World@ s, T@ x); };
<@>@ IO@ = <@ T@> (World@) { R@<T@>; };
<<@>@>@ IStream@ = <<@>@ M@> { M@<Maybe@<Int@>>; };

# Implementation of fble-cat for the purposes of experimenting with
# fble performance.
(
  IO@<Maybe@<Int@>>,
  (Int@, World@) { R@<Unit@>; },
  (Int@, World@) { R@<Unit@>; },
  (String@, World@) { R@<Maybe@<IStream@<IO@>>>; },
  List@<String@>
) { IO@<Bool@>; }
Main = (
  IO@<Maybe@<Int@>> stdin,
  (Int@, World@) { R@<Unit@>; } stdout,
  (Int@, World@) { R@<Unit@>; } _stderr,
  (String@, World@) { R@<Maybe@<IStream@<IO@>>>; } _read,
  List@<String@> args) {

  (Int@) { IO@<Unit@>; } stdout_ = (Int@ s, World@ w) {
    stdout(s, w);
  };

  IO@<Bool@> MCat = {
    Maybe@<Int@> byte <- m.do(stdin);
    byte.?(nothing: m.return(True));
    Char@ char = /Core/Char/Ascii%.Chr(byte.just);
    Unit@ _ <- m.do(stdout_(/Core/Char/Ascii%.Ord(char)));
    MCat;
  };

  (World@) { R@<Bool@>; } Cat = (World@ w) {
    R@<Maybe@<Int@>> byte = stdin(w);
    byte.x.?(nothing: R@(w, True));

    Char@ char = /Core/Char/Ascii%.Chr(byte.x.just);
    R@<Unit@> out = stdout(/Core/Char/Ascii%.Ord(char), byte.s);
    Cat(out.s);
  };

  Bool@ monadic = {
    args.?(nil: False);
    /Core/String/Eq%.Eq(Str|'--monadic', args.cons.head);
  };

  monadic.?(true: MCat, false: Cat);
};

Main;
