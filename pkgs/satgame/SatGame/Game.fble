
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
% Not = /Core/Bool%.Not;

@ Int@ = /Core/Int%.Int@;
% Decr = /Core/Int%.Decr;
% IsZero = /Core/Int/Eq%.IsZero;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;
<@>% Contains = /Core/List/Eq%.Contains;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Literal@ = /SatGame/Puzzle%.Literal@;
@ Clause@ = /SatGame/Puzzle%.Clause@;
@ Puzzle@ = /SatGame/Puzzle%.Puzzle@;

@ Var@ = /SatGame/Var%.Var@;

@ TileState@ = +(
  Unit@ normal,     # The tile has been neither selected nor dismissed.
  Unit@ selected,   # The user has selected this tile.
  Unit@ dismissed   # The user has crossed off this tile.
);

@ Tile@ = *(Var@ var, Bool@ pos, TileState@ state);
@ Group@ = List@<Tile@>;
@ Game@ = List@<Group@>;

(Literal@) { Tile@; } Tile = (Literal@ l) {
  Tile@(l.var, l.pos, TileState@(normal: Unit));
};

(Clause@) { Group@; } Group = (Clause@ clause) {
  /Core/List%.Map(clause, Tile);
};

(Puzzle@) { Game@; } Game = (Puzzle@ puzzle) {
  /Core/List%.Map(puzzle, Group);
};

# Toggle the state of the tile in the given group of the puzzle.
#  @arg[Int@][group] The index of the group in the Game@.
#  @arg[Int@][tile] The index of the tile within the group.
(Int@, Int@, Game@) { Game@; } Toggle = {
  (TileState@) { TileState@; } State = (TileState@ s) {
    s.?(
      normal: TileState@(dismissed: Unit),
      selected: TileState@(normal: Unit),
      dismissed: TileState@(selected: Unit));
  };

  (Tile@) { Tile@; } Tile = (Tile@ t) {
    t.@(state: State(t.state));
  };

  (Int@, Group@) { Group@; } Group = (Int@ tile, Group@ group) {
    group.?(nil: group);
    IsZero(tile).?(true: Cons(Tile(group.cons.head), group.cons.tail));
    Cons(group.cons.head, Group(Decr(tile), group.cons.tail));
  };
  
  (Int@ group, Int@ tile, Game@ game) {
    game.?(nil: game);
    IsZero(group).?(true: Cons(Group(tile, game.cons.head), game.cons.tail));
    Cons(game.cons.head, Toggle(Decr(group), tile, game.cons.tail));
  };
};

(Game@) { Bool@; } Solved = {
  # Removes all dismissed tiles from a group.
  (Group@) { Group@; }
  Dismiss = (Group@ group) {
    group.?(nil: group);
    Group@ tail = Dismiss(group.cons.tail);
    group.cons.head.state.?(dismissed: tail);
    Cons(group.cons.head, tail);
  };

  # Checks if an individual group is 'solved'.
  # Returns the single selected literal if so, Nothing otherwise.
  (Group@) { Maybe@<Literal@>; }
  Group = (Group@ group) {
    Group@ remaining = Dismiss(group);
    remaining.?(nil: Nothing<Literal@>);
    remaining.cons.tail.?(cons: Nothing<Literal@>);
    Tile@ tile = remaining.cons.head;
    tile.state.?(normal: Nothing<Literal@>);
    Just(Literal@(tile.var, tile.pos));
  };

  # Checks if a set of groups is solved.
  #  @arg[lits] literals selected so far.
  #  @arg[groups] rest of groups to check.
  (List@<Literal@>, List@<Group@>) { Bool@; }
  Groups = (List@<Literal@> lits, List@<Group@> groups) {
    groups.?(nil: True);

    Maybe@<Literal@> mlit = Group(groups.cons.head);
    mlit.?(nothing: False);

    Literal@ lit = mlit.just;
    Literal@ complement = lit.@(pos: Not(lit.pos));
    Contains(/SatGame/Puzzle/Eq%.Literal, lits, complement).?(true: False);
    Groups(Cons(lit, lits), groups.cons.tail);
  };

  Groups(Nil<Literal@>);
};

@(Game@, Group@, Tile@, Game, Toggle, Solved);
