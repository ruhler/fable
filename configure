#!/bin/sh
# configure script for fable\
exec tclsh8.6 "$0" "$@"

# usage -- output usage info to the given channel.
proc usage { channel } {
  puts $channel {
Usage: ../configure [OPTION]...

Options:
  -h, --help          display this help and exit
  --prefix=PREFIX     install files in PREFIX [/usr/local]

  --enable-fble-app=[yes|no]
      enable/disable build fble-app based programs requiring SDL/GL.
  --disable-fble-app
      don't build fble-app based programs requiring SDL/GL.

  --build-non-src-dist
      If present, build non-source distribution files in the build directory
      instead of the source directory.
}
}

namespace eval "config" {
  # The top level source directory.
  # Inferred relative to the location of this configure script.
  set srcdir [file dirname [info script]]

  # The top level build directory.
  # Inferred from the current directory.
  set builddir "."

  set nonsrcdistdir $srcdir

  # The install prefix.
  set prefix "/usr/local"

  set enable_fble_app "yes"

  while {[llength $::argv] > 0} {
    set ::arg [lindex $::argv 0]
    set ::argv [lrange $::argv 1 end]
    switch -glob $::arg {
      -h -
      --help {
        usage stdout
        exit 0
      }
      --prefix {
        if {[llength $::argv] == 0} {
          puts stderr "configure: error: missing argument to --prefix"
          exit 1
        }

        set prefix [lindex $::argv 0]
        set ::argv [lrange $::argv 1 end]
      }
      --prefix=* {
        set prefix [string range $arg [string length "--prefix="] end]
      }
      --enable-fble-app {
        set enable_fble_app yes
      }
      --enable-fble-app=* {
        set enable_fble_app [string range $arg [string length "--enable-fble-app="] end]
      }
      --disable-fble-app {
        set enable_fble_app no
      }
      --build-non-src-dist {
        set nonsrcdistdir $builddir
      }
      default {
        puts stderr "configure: error: unrecognized option: `$arg'"
        usage stderr
        exit 1
      }
    }
  }

  # Install directories based on prefix.
  set includedir $::config::prefix/include
  set libdir $::config::prefix/lib
  set bindir $::config::prefix/bin
  set mandir $::config::prefix/man
  set datadir $::config::prefix/share

  # SDL and OpenGL specific configuration.
  if $enable_fble_app {
    set sdl_cflags [exec sdl2-config --cflags]
    set sdl_libs [exec sdl2-config --libs]
    set gl_cflags ""
    set gl_libs "-lGL"
  }
}

# Generate config.tcl and show the user the config settings.
puts "Configuration:"
set fout [open $::config::builddir/config.tcl "w"]
puts $fout "namespace eval \"config\" {"
foreach var [lsort [info var ::config::*]] {
  set name [string range $var [string length "::config::"] end]
  set value [subst $$var]
  puts "  $name=$value"
  puts $fout "  set $name \{$value\}"
}
puts $fout "}"
close $fout

# Generate the build.ninja file.
exec tclsh8.6 $::config::srcdir/build.tcl $::config::builddir/build.ninja
