@ Bool@ = /Bool%.Bool@;
% True = /Bool%.True;
% False = /Bool%.False;
% And = /Bool%.And;

@ Char@ = /Char%.Char@;
% Chars = /Char%.Chars;

@ IntP@ = /Int/IntP%.IntP@;
% EqIntP = /Int/IntP/Eq%.Eq;
% 1 = /Int/IntP%.1;

<@>% S = /List%.S;
<@>% Append = /List%.Append;
<@>% EqList = /List/Eq%.Eq;
<@>% PrintList = /List/Print%.Print;

<@>@ Maybe@ = /Maybe%.Maybe@;
<@>% Just = /Maybe%.Just;
<@>% Nothing = /Maybe%.Nothing;
<@>% EqMaybe = /Maybe/Eq%.Eq;
<@>% PrintMaybe = /Maybe/Print%.Print;

@ Str@ = /String%.Str@;
% Str = /String%.Str;

@ Test@ = /Test%.Test@;
@ TestResult@ = /Test%.TestResult@;
@ TestSuite@ = /Test%.TestSuite@;
% TestSuite = /Test%.TestSuite;
<@>% AssertEquals = /Test%.AssertEquals;

@ Formula@ = /Sat%.Formula@;
@ Clause@ = /Sat%.Clause@;
@ Var@ = /Sat%.Var@;
@ Assignment@ = /Sat%.Assignment@;
% Solve = /Sat%.Solve;

(IntP@) { Str@; } PrintId = (IntP@ x) {
  # TODO: Print variable ids properly.
  Str(x.?(1: Chars|1, : Chars|'?'));
};

(Var@) { Str@; } PrintVar = (Var@ v) {
  Str@ sign = Str(v.polarity.?(true: Chars|'+', false: Chars|'-'));
  Append<Char@>(sign, PrintId(v.id));
};

(Assignment@) { Str@; } PrintAssignment = (Assignment@ x) {
  PrintList<Var@>(PrintVar, x);
};

(Maybe@<Assignment@>) { Str@; } PrintMaybeAssignment = (Maybe@<Assignment@> m) {
  PrintMaybe<Assignment@>(PrintAssignment, m);
};

(Var@, Var@) { Bool@; } EqVar = (Var@ a, Var@ b) {
  And(EqIntP(a.id, b.id), /Bool/Eq%.Eq(a.polarity, b.polarity));
};

(Assignment@, Assignment@) { Bool@; }
EqAssignment = (Assignment@ a, Assignment@ b) {
  EqList<Var@>(EqVar, a, b);
};

(Maybe@<Assignment@>, Maybe@<Assignment@>) { Bool@; }
EqMaybeAssignment = (Maybe@<Assignment@> a, Maybe@<Assignment@> b) {
  EqMaybe<Assignment@>(EqAssignment, a, b);
};

(Maybe@<Assignment@>, Maybe@<Assignment@>) { TestResult@; }
AssertEq = (Maybe@<Assignment@> a, Maybe@<Assignment@> b) {
  AssertEquals<Maybe@<Assignment@>>(
    EqMaybeAssignment, PrintMaybeAssignment, a, b);
};

TestSuite@ Tests = TestSuite(Chars|Sat, [
  Test@(Str(Chars|Sat), !({
    Formula@ f = S<Clause@>([
      S<Var@>([Var@(1, True)])
    ]);
    Maybe@<Assignment@> want = Just<Assignment@>(S<Var@>([Var@(1, True)]));
    Maybe@<Assignment@> got = Solve(f);
    AssertEq(want, got);
  })),

  Test@(Str(Chars|Unsat), !({
    Formula@ f = S<Clause@>([
      S<Var@>([Var@(1, True)]),
      S<Var@>([Var@(1, False)])
    ]);
    Maybe@<Assignment@> want = Nothing<Assignment@>;
    Maybe@<Assignment@> got = Solve(f);
    AssertEq(want, got);
  }))
]);

@(Tests);
