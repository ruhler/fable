
@ State@ = /BenchmarkGame/PiDigits%.State@;
@ StreamResult@ = /BenchmarkGame/PiDigits%.StreamResult@;
% Stream = /BenchmarkGame/PiDigits%.Stream;
% Initial = /BenchmarkGame/PiDigits%.Initial;

@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;

% Chars = /Core/Char%.Chars;

@ Int@ = /Core/Int%.Int@;
% Inc = /Core/Int%.Inc;
% Div = /Core/Int/Div%.Div;
% Eq = /Core/Int/Eq%.Eq;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;

@ Stdio@ = /Core/Stdio%.Stdio@;
@ IO@ = /Core/Stdio%.IO@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

# Output the digits of pi, one digit output for each input string, starting
# from state 's'.
Stdio@ Main = (IO@ io, List@<String@> args) {
  Int@ n = /Core/Int/Show%.Read(args.cons.head);
  Int@ l = Int|10;
  (Int@, State@) { Bool@!; } PiDigits = (Int@ i, State@ s) {
    Eq(i, n).?(true: !(True));

    StreamResult@ r = Stream(s);
    Unit@ _ := io.stdout(/Core/Int/Show%.Show(r.output));

    Int@ ni = Inc(i);
    Unit@ _ := Eq(Int|0, Div(ni, l).r).?(true: {
      Unit@ _ := io.stdout[Chars.tab, Chars.':'];
      Unit@ _ := io.stdout(/Core/Int/Show%.Show(ni));
      io.stdout[Chars.nl];
    }, false: !(Unit));

    PiDigits(ni, r.state);
  };

  PiDigits(Int|0, Initial);
};

Main;
