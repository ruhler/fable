
@ Char@ = /Char%.Char@;
% Chars = /Char%.Chars;

<@>@ List@ = /List%.List@;
<@>% Append = /List%.Append;
<@>% Concat = /List%.Concat;

<@>@ Put@ = /Process%.Put@;

@ String@ = /String%.String@;
% Str = /String%.Str;

@ Unit@ = /Unit%.Unit@;

# Output@ -- 
#   Abstract data type for outputting a sequence of lines of text from
#   compilation.
@ Output@ = String@;

# Line --
#   Output the given line of text.
(String@) { Output@; } Line = (String@ line) {
  Append<Char@>(line, Str[Chars.nl]);
};

# Lines --
#   Output multiple lines of text in order.
(List@<Output@>) { Output@; } Lines = (List@<Output@> lines) {
  Concat<Char@>(lines);
};

# Output --
#   Output the output to the given port.
(Put@<String@>, Output@) { Unit@!; } Output = (Put@<String@> put, Output@ output) {
  put(output);
};

@(Output@, Line, Lines, Output);
