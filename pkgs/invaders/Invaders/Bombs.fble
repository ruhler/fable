
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ Drawing@ = /App/Drawing%.Drawing@;
% Blank = /App/Drawing%.Blank;
% Over = /App/Drawing%.Over;
% Translate = /App/Drawing%.Translate;

@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Sub = /Core/Int%.Sub;
% Lt = /Core/Int/Eq%.Lt;
% Gt = /Core/Int/Eq%.Gt;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;
<@>% ForEach = /Core/List%.ForEach;
<@>% Length = /Core/List/Length%.Length;

% Bases = /Invaders/Bases%;
@ Bases@ = Bases.Bases@;

% Graphics = /Invaders/Graphics%;

@ Unit@ = /Core/Unit%.Unit@;

@ Kind@ = +(Unit@ bomb, Unit@ squiggle);

Int@ BombVelocity = Int|1;
Int@ SquiggleVelocity = Int|3;
Int@ MinY = Int|2;
Int@ MaxBombs = Int|20;

@ Bomb@ = *(Int@ x, Int@ y, Kind@ kind);

@ Bombs@ = List@<Bomb@>;

Bombs@ Initial = Nil<Bomb@>;

(Int@, Int@, Kind@, Bombs@) { Bombs@; }
Fire = (Int@ x, Int@ y, Kind@ kind, Bombs@ bombs) {
  Lt(Length(bombs), MaxBombs).?(false: bombs);
  Cons(Bomb@(x, y, kind), bombs);
};

# HitResult@ --
#   A result from checking if a bomb hit the ship.
#
# Fields:
#   hit - true if hit, false otherwise.
#   bombs - the bombs after removing the bomb that hit the ship.
@ HitResult@ = *(Bool@ hit, Bombs@ bombs);

# HitShip -- 
#   Returns just if any of the bombs are currently hitting the ship, with the
#   bomb that hit the ship removed. Returns nothing if no bomb hits the ship.
(Bombs@, Int@) { HitResult@; } HitShip = (Bombs@ bombs, Int@ ship_x) {
  HitResult@ miss = @(hit: False, bombs: bombs);

  bombs.?(nil: miss);

  HitResult@ tail_hit = HitShip(bombs.cons.tail, ship_x);
  tail_hit.hit.?(true: {
    @(hit: True, bombs: Cons(bombs.cons.head, tail_hit.bombs));
  });

  Bomb@ bomb = bombs.cons.head;
  Lt(bomb.y, /Invaders/Ship%.ShipY).?(true: miss);
  Lt(bomb.x, ship_x).?(true: miss);
  Gt(bomb.y, Add(Int|8, /Invaders/Ship%.ShipY)).?(true: miss);
  Gt(bomb.x, Add(ship_x, Int|15)).?(true: miss);
  @(hit: True, bombs: bombs.cons.tail);
};

@ HitBasesResult@ = *(Bombs@ bombs, Bases@ bases);

# HitBases --
#   Update Bombs@ and Bases@ for any collisions between the two.
(Bombs@, Bases@) { HitBasesResult@; }
HitBases = (Bombs@ bombs, Bases@ bases) {
  bombs.?(nil: @(bombs, bases));

  HitBasesResult@ tail_hit = HitBases(bombs.cons.tail, bases);

  Bomb@ bomb = bombs.cons.head;
  Bases.HitResult@ hit = Bases.BombHit(bomb.x, bomb.y, tail_hit.bases);
  Bombs@ nbombs = hit.hit.?(
    true: tail_hit.bombs,
    false: Cons(bomb, tail_hit.bombs));
  @(bombs: nbombs, bases: hit.bases);
};

(Bombs@) { Bombs@; } Tick = (Bombs@ bombs) {
  ForEach(bombs, Nil<Bomb@>, (Bomb@ b, Bombs@ bs) {
    Int@ v = b.kind.?(bomb: BombVelocity, squiggle: SquiggleVelocity);
    Bomb@ nb = Bomb@(b.x, Sub(b.y, v), b.kind);
    Lt(nb.y, MinY).?(true: bs);
    Cons(nb, bs);
  });
};

(Bombs@) { Drawing@; } Draw = (Bombs@ bombs) {
  ForEach(bombs, Blank, (Bomb@ b, Drawing@ d) {
    Drawing@ sprite = b.kind.?(bomb: Graphics.Bomb, squiggle: Graphics.Squiggle);
    Over(d, Translate(b.x, b.y, sprite));
  });
};

@(Bombs@, Kind@, HitResult@, HitBasesResult@,
  Initial, Fire, HitShip, HitBases, Tick, Draw);

