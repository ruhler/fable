@ Bool@ = /Core/Bool%.Bool@;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<@>@ IO@ = /Core/Monad/IO%.IO@;
@ World@ = /Core/Monad/IO%.World@;
<@>@ R@ = /Core/Monad/IO%.R@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;


# Run --
#   A top-level IO implementation for running a Stdio Main@ program.
(/Core/Stdio%.Main@)(
  IO@<Maybe@<Int@>>,
  (Int@, World@) { R@<Unit@>; },
  (Int@, World@) { R@<Unit@>; },
  (String@, World@) { R@<Maybe@<IStream@<IO@>>>; },
  List@<String@>) { IO@<Bool@>; }
Run = (/Core/Stdio%.Main@ main)(
  IO@<Maybe@<Int@>> stdin,
  (Int@, World@) { R@<Unit@>; } stdout,
  (Int@, World@) { R@<Unit@>; } stderr,
  (String@, World@) { R@<Maybe@<IStream@<IO@>>>; } read,
  List@<String@> args) {
  # Note: We pass stdout and stderr as functions taking the string and the
  # world rather than a function from string to IO@ just so we don't have to
  # implement the currying on the native C side.
  main<IO@>(
    /Core/Monad/IO%.Monad,
    @(
      in: stdin,
      out: (Int@ s)(World@ w) { stdout(s, w); },
      err: (Int@ s)(World@ w) { stderr(s, w); },
      read: (String@ s)(World@ w) { read(s, w); }))(args);
};

@(Run);
