
% IntP = /Int/IntP%;
@ IntP@ = IntP.IntP@;
% AddP = IntP.Add;
% MulP = IntP.Mul;

% Sub1 = /Int/IntP/Sub%.Sub1;

@ IntS@ = /Int/IntS%.IntS@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ Int@ = +(
  IntP@ n,   # Negative integers
  Unit@ 0,
  IntP@ p    # Positive integers
);

Int@ 0 = Int@(0: Unit);

(IntP@) { Int@; } N = (IntP@ x) {
  Int@(n: x);
};

(IntP@) { Int@; } P = (IntP@ x) {
  Int@(p: x);
};

Int@ 1 = P(IntP.1);
Int@ 2 = P(IntP.2);
Int@ 3 = P(IntP.3);
Int@ 4 = P(IntP.4);
Int@ 5 = P(IntP.5);
Int@ 6 = P(IntP.6);
Int@ 7 = P(IntP.7);
Int@ 8 = P(IntP.8);
Int@ 9 = P(IntP.9);
Int@ 10 = P(IntP.10);

# a + 1
(Int@){ Int@; } Inc = (Int@ a) {
  a.?( 
      n: {
        IntS@ s = Sub1(a.n);
        s.?(0: 0, p: N(s.p));
      },
      0: P(IntP.1),
      p: P(IntP.Add1(a.p)));
};

# a - 1
(Int@){ Int@; } Dec = (Int@ a) {
  a.?(
      n: N(IntP.Add1(a.n)),
      0: N(IntP.1),
      p: {
        IntS@ s = Sub1(a.p);
        s.?(0: 0, p: P(s.p));
      });
};

# 2 * a
(Int@) { Int@; } Mul2 = (Int@ a) {
  a.?(n: N(IntP.2P0(a.n)), 0: a, p: P(IntP.2P0(a.p)));
};

(IntP@, IntP@) { Int@; } SubP = (IntP@ a, IntP@ b) {
  a.?(1: {
         IntS@ s = Sub1(b);
         s.?(0: 0, p: N(s.p));
       },
       2p0: b.?(1: P(Sub1(a).p),
                2p0: Mul2(SubP(a.2p0, b.2p0)),
                2p1: Dec(Mul2(SubP(a.2p0, b.2p1)))),
       2p1: b.?(1: P(Sub1(a).p),
                2p0: Inc(Mul2(SubP(a.2p1, b.2p0))),
                2p1: Mul2(SubP(a.2p1, b.2p1))));
};

# a + b
(Int@, Int@) { Int@; } Add = (Int@ a, Int@ b) {
  a.?(n: b.?(n: N(AddP(a.n, b.n)),
             0: a,
             p: SubP(b.p, a.n)),
       0: b,
       p: b.?(n: SubP(a.p, b.n),
              0: a,
              p: P(AddP(a.p, b.p))));
};

# -a 
(Int@){ Int@; } Neg = (Int@ a) {
  a.?(n: P(a.n), 0: a, p: N(a.p));
};

# a - b 
(Int@, Int@){ Int@; } Sub = (Int@ a, Int@ b) {
  # a - b = a + (-b)
  Add(a, Neg(b));
};

# a * b
(Int@, Int@){ Int@; } Mul = (Int@ a, Int@ b) {
  a.?(
    n: b.?(n: P(MulP(a.n, b.n)),
           0: 0,
           p: N(MulP(a.n, b.p))),
    0: 0,
    p: b.?(n: N(MulP(a.p, b.n)),
            0: 0,
            p: P(MulP(a.p, b.p))));
};

@(Int@, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10,
  Inc, Dec, Add, Sub, Neg, Mul);
