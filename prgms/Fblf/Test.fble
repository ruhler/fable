
@ Char@ = /Char%.Char@;

@ Bit@ = /Digits%.Bit@;

@ Int@ = /Int/Int%.Int@;
% Int = /Int/Int/Lit%.Int;

% Exec = /Fblf/Exec%.Exec;

@ Heap@ = /Fblf/Heap%.Heap@;
% Heap = /Fblf/Heap%.Heap;
% Read = /Fblf/Heap%.Read;

@ Var@ = /Fblf/Program%.Var@;

<@>@ Ran@ = /Fblf/Module%.Ran@;
<@>@ Module@ = /Fblf/Module%.Module@;
@ Method@ = /Fblf/Module%.Method@;
<@>% Run = /Fblf/Module%.Run;

<@>@ List@ = /List%.List@;
<@>% List = /List%.List;
<@,@>% Map = /List%.Map;

@ String@ = /String%.String@;

<@>% AssertEquals = /Test%.AssertEquals;


@ Bits@ = List@<Bit@>;
/Eq%.Eq@<Bits@> EqBits = /List/Eq%.Eq<Bit@>(/Digits/Eq%.EqBit);
/Show%.Show@<Bits@> ShowBits = /Digits/Show%.ShowBits;

# The test runs the program and asserts that the n bits of result match the
# expected bits, where n is the number of given expected bits.
@ TestInstance@ = *(Method@ method, Var@ result, Bits@ expected);
@ TestCase@ = *(String@ name, Module@<TestInstance@> test);
@ TestSuite@ = *(String@ name, List@<Test@> tests),
@ Test@ = +(TestCase@ case, TestSuite@ suite);

# Test --
#   Creates a test.
(List@<Char@>, Module@<TestInstance@>) { Test@; }
Test = (List@<Char@> name, Module@<TestInstance@> test) {
  Test@(case: TestCase@(name, test));
};

# TestSuite --
(String@, List@<Test@>) { Test@; }
TestSuite = (String@ name, List@<Test@> tests) {
  Test@(suite: TestSuite@(name, tests));
};

# Convert an Fblf Test into an Fble Test.
(Test@) { /Test%.Test@; } RunTests = (Test@ t) {
  t.?(
    case: {
      Module@<TestInstance@> m = t.case.test;
      /Test%.TestResult@! body = !({
        Ran@<TestInstance@> i = Run<TestInstance@>(m);
        TestInstance@ mI = i.x;
        Heap@ heap = Exec(i.program, List<Int@>[Int|0], mI.method, Heap(i.heap));

        Bits@ wnt = mI.expected;
        Bits@ got = Read(heap, mI.result.offset, /List/Length%.Length<Bit@>(wnt));
        AssertEquals<Bits@>(EqBits, ShowBits, wnt, got);
      });
      /Test%.Test@(case: /Test%.TestCase@(t.case.name, body));
    },
    suite: {
      /Test%.Test@(suite: /Test%.TestSuite@(t.suite.name,
          Map<Test@, /Test%.Test@>(t.suite.tests, RunTests)));
    });
};

@(Test@, TestInstance@, TestCase@, TestSuite@, Test, TestSuite, RunTests);
