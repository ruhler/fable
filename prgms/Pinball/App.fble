
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
% Blank = /Drawing%.Blank;
% Color = /Drawing%.Color;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;
% Drawings = /Drawing%.Drawings;
% Translate = /Drawing%.Translate;

% Circle = /Graphics/2D/Circle%.Circle;
% Triangle = /Graphics/2D/Triangle%.Triangle;

@ Int@ = /Int/Int%.Int@;

<@>% List = /List%.List;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Rat@ = /Rat%.Rat@;
% Neg = /Rat%.Neg;
% Div = /Rat%.Div;
% Int = /Rat/Lit%.Int;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ Ball@ = /Pinball/Physics%.Ball@;
@ Edge@ = /Pinball/Physics%.Edge@;
@ Vec@ = /Pinball/Physics%.Vec@;
@ World@ = /Pinball/Physics%.World@;
% AddV = /Pinball/Physics%.AddV;
% SubV = /Pinball/Physics%.SubV;
% MulVS = /Pinball/Physics%.MulVS;
% Tick = /Pinball/Physics%.Tick;

(Rat@) { Int@; } Floor = (Rat@ x) {
  /Int/Int/Div%.Div(x.n, x.d).q;
};

(Vec@) { Point@; } Point = (Vec@ a) {
  Point@(Floor(a.x), Floor(a.y));
};

# Radius --
#   The radius of the ball.
Rat@ Radius = Int|8;

World@ Initial = World@(
  Ball@(Vec@(Int|0, Int|100), Vec@(Int|1, Int|0), Radius),
  List<Edge@>[
    # Floor
    Edge@(Vec@(Int|0, Int|1),
          Vec@(Neg(Int|16), Neg(Int|104)),
          Int|32),

    # Slopes
    Edge@(Vec@(Neg(Div(Int|3, Int|5)), Div(Int|4, Int|5)),
          Vec@(Int|16, Neg(Int|104)),
          Int|160),
    Edge@(Vec@(Div(Int|3, Int|5), Div(Int|4, Int|5)),
          Vec@(Neg(Int|144), Neg(Int|8)),
          Int|160),

    # Side walls
    Edge@(Vec@(Int|1, Int|0),
          Vec@(Neg(Int|144), Int|104),
          Int|112),
    Edge@(Vec@(Neg(Int|1), Int|0),
          Vec@(Int|144, Neg(Int|8)),
          Int|112),

    # Ceiling
    Edge@(Vec@(Int|0, Neg(Int|1)),
          Vec@(Int|144, Int|104),
          Int|288)
  ]);

(Edge@) { Drawing@; } DrawEdge = (Edge@ edge) {
  Vec@ s = SubV(edge.start, MulVS(edge.normal, Int|2));

  Point@ a = Point(edge.start);
  Point@ b = Point(s);
  Vec@ tangent = Vec@(edge.normal.y, Neg(edge.normal.x));
  Point@ c = Point(AddV(edge.start, MulVS(tangent, edge.length)));
  Point@ d = Point(AddV(s, MulVS(tangent, edge.length)));
  Drawings[
    Triangle(a, b, c, Color.Green),
    Triangle(c, b, d, Color.Green)
  ];
};

Drawing@ Background = {
  /List%.ForEach<Edge@, Drawing@>(Initial.edges, Blank, (Edge@ e, Drawing@ d) {
    Over(d, DrawEdge(e));
  });
};

Drawing@ DrawnBall = Circle(Point@(Int|0.n, Int|0.n), Floor(Radius), Color.Cyan);

(World@) { Drawing@; } Draw = (World@ world) {
  Point@ p = Point(world.ball.pos);
  Over(Background, Translate(p.x, p.y, DrawnBall));
};

(Get@<Event@>, Put@<World@>, Unit@!, World@) { Unit@!; }
Run = (Get@<Event@> in, Put@<World@> draw, Unit@! tick, World@ world) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(world);
    Run(in, draw, tick, Tick(world));
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));
    Run(in, draw, tick, world);
  });
  Run(in, draw, tick, world);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  Point@ a = @(x: Int|1.n, y: Neg(Int|1).n);
  Point@ b = @(x: Int|320.n, y: Int|240.n);
  Int@ frame_period = Int|16.n;
  Unit@! tick = out(Effect@(tick: frame_period));

  (World@) { Unit@!; } draw = (World@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0.n, Int|0.n, width, height, Color.Black),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0.n));
  Run(in, draw, tick, Initial); 
};

Main;

