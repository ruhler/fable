
@ App@ = /App%.App@;
@ AppIO@ = /App%.AppIO@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
% Blank = /Drawing%.Blank;
% Color = /Drawing%.Color;
% Over = /Drawing%.Over;
% Triangle = /Drawing%.Triangle;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;
% Drawings = /Drawing%.Drawings;
% Translate = /Drawing%.Translate;

% Circle = /Graphics/2D/Circle%.Circle;

@ Int@ = /Int%.Int@;

<@>% List = /List%.List;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;


@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ Num@ = /Pinball/Num%.Num@;
% Floor = /Pinball/Num%.Floor;
% Neg = /Pinball/Num%.Neg;
% Div = /Pinball/Num%.Div;
% Int = /Pinball/Num%.Int;

@ Ball@ = /Pinball/Ball%.Ball@;

@ Edge@ = /Pinball/Edge%.Edge@;
% Edge = /Pinball/Edge%.Edge;
% Dir = /Pinball/Edge%.Dir;

@ Paddle@ = /Pinball/Paddle%.Paddle@;
% Paddle = /Pinball/Paddle%.Paddle;

% Rotate = /Pinball/Rotate%.Rotate;

@ World@ = /Pinball/World%.World@;
% Tick = /Pinball/World%.Tick;

@ Vec@ = /Pinball/Vec%.Vec@;
% AddV = /Pinball/Vec%.AddV;
% RotateFrom = /Pinball/Vec%.RotateFrom;

(Vec@) { Point@; } Point = (Vec@ a) {
  Point@(Floor(a.x), Floor(a.y));
};

# Radius --
#   The radius of the ball.
Num@ Radius = Int|8;

Num@ PaddleVelocity = Div(Int|1, Int|4);

Num@ R = Div(Int|4, Int|8);

(Num@, Num@) { Paddle@; } LeftPaddle = (Num@ vel, Num@ pos) {
  Paddle(
    Vec@(Neg(Int|80), Neg(Int|156)),
    Vec@(Neg(Int|80), Neg(Int|156)),
    Vec@(Neg(Int|10), Neg(Int|156)),
    vel, pos, Neg(Div(Int|1, Int|2)), Div(Int|1, Int|2), R);
};

(Num@, Num@) { Paddle@; } RightPaddle = (Num@ vel, Num@ pos) {
  Paddle(
    Vec@(Int|80, Neg(Int|156)),
    Vec@(Int|10, Neg(Int|156)),
    Vec@(Int|80, Neg(Int|156)),
    vel, pos, Neg(Div(Int|1, Int|2)), Div(Int|1, Int|2), R);
};

World@ Initial = World@(
  Vec@(Int|0, Neg(Div(Int|2, Int|8))),
  Ball@(Vec@(Int|0, Int|100), Vec@(Neg(Div(Int|1, Int|2)), Int|0), Radius),
  List<Edge@>[
    # Floor
    Edge(Vec@(Neg(Int|144), Neg(Int|224)), Vec@(Int|144, Neg(Int|224)), R),

    # Slopes
    Edge(Vec@(Int|80, Neg(Int|156)), Vec@(Int|144, Neg(Int|108)), R),
    Edge(Vec@(Neg(Int|144), Neg(Int|108)), Vec@(Neg(Int|80), Neg(Int|156)), R),

    # Side walls
    Edge(Vec@(Neg(Int|144), Int|204), Vec@(Neg(Int|144), Neg(Int|224)), R),
    Edge(Vec@(Int|144, Neg(Int|224)), Vec@(Int|144, Int|204), R),

    # Ceiling
    Edge(Vec@(Int|144, Int|204), Vec@(Neg(Int|144), Int|204), R)
  ],
  List<Paddle@>[
    LeftPaddle(Neg(PaddleVelocity), Int|0),
    RightPaddle(PaddleVelocity, Int|0)
  ]
);

(Edge@) { Drawing@; } DrawEdge = (Edge@ edge) {
  Vec@ e = RotateFrom(Dir(edge), Vec@(Int|0, Neg(Int|2)));
  Point@ a = Point(edge.start);
  Point@ b = Point(AddV(edge.start, e));
  Point@ c = Point(edge.end);
  Point@ d = Point(AddV(edge.end, e));
  Drawings[
    Triangle(a, b, c, Color.Green),
    Triangle(c, b, d, Color.Green)
  ];
};

(Paddle@) { Drawing@; } DrawPaddle = (Paddle@ paddle) {
  Vec@ start = AddV(paddle.center, Rotate(paddle.start, paddle.pos));
  Vec@ end = AddV(paddle.center, Rotate(paddle.end, paddle.pos));
  Vec@ e = Rotate(Vec@(Int|0, Neg(Int|2)), paddle.pos);
  Point@ a = Point(start);
  Point@ b = Point(AddV(start, e));
  Point@ c = Point(end);
  Point@ d = Point(AddV(end, e));
  Drawings[
    Triangle(a, b, c, Color.Green),
    Triangle(c, b, d, Color.Green)
  ];
};

Drawing@ Background = {
  /List%.ForEach<Edge@, Drawing@>(Initial.edges, Blank, (Edge@ e, Drawing@ d) {
    Over(d, DrawEdge(e));
  });
};

Drawing@ DrawnBall = Circle(Point@(Floor(Int|0), Floor(Int|0)), Floor(Radius), Color.Cyan);

(World@) { Drawing@; } Draw = (World@ world) {
  Point@ ball = Point(world.ball.pos);
  /List%.ForEach<Paddle@, Drawing@>(
    world.paddles,
    Over(Background, Translate(ball.x, ball.y, DrawnBall)),
    (Paddle@ paddle, Drawing@ d) {
      Over(d, DrawPaddle(paddle));
    });
};

(Num@, World@) { World@; } SetLeftPaddleVelocity = (Num@ v, World@ w) {
  Paddle@ l = w.paddles.cons.head;
  Paddle@ r = w.paddles.cons.tail.cons.head;
  World@(w.gravity, w.ball, w.edges, 
    List<Paddle@>[LeftPaddle(v, l.pos), r]);
};

(Num@, World@) { World@; } SetRightPaddleVelocity = (Num@ v, World@ w) {
  Paddle@ l = w.paddles.cons.head;
  Paddle@ r = w.paddles.cons.tail.cons.head;
  World@(w.gravity, w.ball, w.edges, 
    List<Paddle@>[l, RightPaddle(v, r.pos)]);
};

(Get@<Event@>, Put@<World@>, Unit@!, World@) { Unit@!; }
Run = (Get@<Event@> in, Put@<World@> draw, Unit@! tick, World@ world) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(world);
    Run(in, draw, tick, Tick(world));
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));
    e.key_down.?(s: {
      Run(in, draw, tick, Initial);
    });
    e.key_down.?(lshift: {
      Run(in, draw, tick, SetLeftPaddleVelocity(PaddleVelocity, world));
    });
    e.key_down.?(rshift: {
      Run(in, draw, tick, SetRightPaddleVelocity(Neg(PaddleVelocity), world));
    });
    Run(in, draw, tick, world);
  });

  e.?(key_up: {
    e.key_up.?(lshift: {
      Run(in, draw, tick, SetLeftPaddleVelocity(Neg(PaddleVelocity), world));
    });
    e.key_up.?(rshift: {
      Run(in, draw, tick, SetRightPaddleVelocity(PaddleVelocity, world));
    });
    Run(in, draw, tick, world);
  });

  Run(in, draw, tick, world);
};

App@ Main = (AppIO@ app) {
  Point@ a = @(x: Floor(Int|1), y: Floor(Neg(Int|1)));
  Point@ b = @(x: Floor(Int|320), y: Floor(Int|240));
  Int@ frame_period = Floor(Int|16);
  Unit@! tick = app.effect(Effect@(tick: frame_period));

  (World@) { Unit@!; } draw = (World@ s) {
    app.effect(Effect@(draw: Over(
      Rect(Floor(Int|0), Floor(Int|0), app.width, app.height, Color.Black),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := app.effect(Effect@(tick: Floor(Int|0)));
  Run(app.event, draw, tick, Initial); 
};

Main;

