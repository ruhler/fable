
@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

# List@ --
#   A singly linked list of elements.
#
# Type Parameters:
#   T@ - The type of element to put on the list.
<@>@ List@ = <@ T@> {
  @ L@ = +(*(T@ head, L@ tail) cons, Unit@ nil);
  L@;
};

# Cons -- 
#   Prepends an element to a list.
#
# Type Parameters:
#   T@ - The type of element on the list.
#
# Arguments:
#   a - The element to prepend to the list.
#   l - The list to prepend the element to.
#
# Returns:
#   A new list consisting of the element 'a' followed by the elements of 'l'.
<@ T@>(T@, List@<T@>) { List@<T@>; } Cons = <@ T@>(T@ a, List@<T@> l) {
  List@(cons: @(head: a, tail: l));
};

# Nil --
#   The empty list of elements of type T@.
#
# Example Use:
#   List@<Foo@> empty = Nil<Foo@>;
List@ Nil = <@ T@> {
  List@<T@>(nil: Unit);
};

# List --
#   A function to use for writing list literals.
#
# Type Parameters:
#   T@ - The type of element on the list.
#
# Example Use:
#   List@<ABC@> x = List<ABC@>[A, B, A, C];
<@ T@>(List@<T@>) { List@<T@>; } List = <@ T@>(List@<T@> l) { l; };

# Last --
#   Returns the last element in a list.
#
# Type Parameters:
#   T@ - The type of element in the list.
#
# Arguments:
#   list - the list to take the last element of.
#
# Returns:
#   The last element of the list.
#
# Side Effects:
#   Behavior is undefined if the list is empty.
<@ T@>(List@<T@>) { T@; } Last = <@ T@>(List@<T@> list) {
  list.cons.tail.?(nil: list.cons.head);
  Last(list.cons.tail);
};

# Init --
#   Returns all but the last element in a list.
#
# Type Parameters:
#   T@ - The type of element in the list.
#
# Arguments:
#   list - the list to take the initial elements of.
#
# Returns:
#   A new list with all the elements of 'list' except the last element of
#   list. Returns an empty list if the input list is empty.
<@ T@>(List@<T@>) { List@<T@>; } Init = <@ T@>(List@<T@> list) {
  list.?(nil: Nil<T@>);
  list.cons.tail.?(nil: Nil<T@>);
  Cons(list.cons.head, Init(list.cons.tail));
};

# ForEach --
#   Iterate over elements of a list.
#
# Type Parameters:
#   A@ - The type of element on the list.
#   B@ - The type of value updated and returned by the foreach loop.
#
# Arguments:
#   list - The list to iterate over.
#   base - The initial value to update and return from the loop.
#   body - The body of the loop.
#
# Returns:
#   The result of applying the body consecutively to the base value, once for
#   each element in the list in list order.
#
# Conceptually:
#   B@ b = base
#   foreach x in list:
#     b = body(x, b)
#   return b
<@ A@>(List@<A@>)<@ B@>(B@, (A@, B@) { B@; }) { B@; }
ForEach = <@ A@>(List@<A@> list)<@ B@>(B@ base, (A@, B@) { B@; } body) {
  list.?(nil: base);
  ForEach(list.cons.tail, body(list.cons.head, base), body);
};

# Reverses elements of a list.
<@ T@>(List@<T@>) { List@<T@>; } Reverse = <@ T@>(List@<T@> l) {
  ForEach(l, Nil<T@>, Cons<T@>);
};

# Map --
#   Applies a function to every element of a list.
#
# Type Parameters:
#   A@ - The type of element of the input list.
#   B@ - The type of element of the output list.
#
# Arguments:
#   a - The input list
#   f - the function to apply
#
# Result:
#   A list of elements f(a_i) for each a_i in the input list.
<@ A@>(List@<A@>)<@ B@>((A@) { B@; }) { List@<B@>; }
Map = <@ A@>(List@<A@> a)<@ B@>((A@) { B@; } f) {
  a.?(nil: Nil<B@>);
  Cons(f(a.cons.head), Map(a.cons.tail, f));
};

# Append --
#   Appends one list to another.
#
# Type Parameters:
#   T@ - The type of elements in the list.
#
# Arguments:
#   a - the first list
#   b - the second list
#
# Returns:
#   A list that contains the elements of 'a' followed by the elements of 'b'.
<@ T@>(List@<T@>, List@<T@>) { List@<T@>; }
Append = <@ T@>(List@<T@> a, List@<T@> b) {
  a.?(nil: b);
  Cons(a.cons.head, Append(a.cons.tail, b));
};

# Concat --
#   Flattens a list of lists into a single list of all the elements.
#
# Type Parameters:
#   T@ - The type of element in the resulting list.
#
# Arguments:
#   x - The list to flatten.
#
# Returns:
#   A list with all elements from the list of lists x, in order.
<@ T@>(List@<List@<T@>>) { List@<T@>; } Concat = <@ T@>(List@<List@<T@>> x) {
  x.?(nil: Nil<T@>);
  Append(x.cons.head, Concat(x.cons.tail));
};

# @func[Join] Joins a list of lists together.
#  @arg[@][T@] The type of element on the list.
#  @arg[List@<List@<T@>>][lists] The list of lists to join.
#  @arg[List@<T@>][separator] Separator to put between the joined lists.
#  @returns[List@<T@>] The joined list.
<@ T@>(List@<List@<T@>>, List@<T@>) { List@<T@>; }
Join = <@ T@>(List@<List@<T@>> lists, List@<T@> separator) {
  lists.?(nil: Nil<T@>);
  lists.cons.tail.?(nil: lists.cons.head);
  Concat[lists.cons.head, separator, Join(lists.cons.tail, separator)];
};

@(List@, List, Cons, Nil, Append, Last, Init, Concat, Map, ForEach, Reverse,
  Join);
