% And = /Core/Bool%.And;
% Or = /Core/Bool%.Or;

@ Int@ = /Core/Int%.Int@;

<@>@ Ord@ = /Core/Ord%.Ord@;
@ Ordering@ = /Core/Ord%.Ordering@;

% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

# Coord@ --
#   A coordinate identifying a location in the memory/design grid.
#
# By convention the origin is at the upper left:
# * Positive x goes to the right.
# * Positive y goes down.
@ Coord@ = *(Int@ x, Int@ y);

# Add --
#   Add two coordinates together.
(Coord@, Coord@) { Coord@; } Add = (Coord@ a, Coord@ b) {
  Coord@(/Core/Int%.Add(a.x, b.x), /Core/Int%.Add(a.y, b.y));
};

# Eq --
#   Equality operator for Coord@.
/Core/Eq%.Eq@<Coord@> Eq = {
  % IntEq = /Core/Int/Eq%.Eq;
  (Coord@ a, Coord@ b) {
    And(IntEq(a.x, b.x), IntEq(a.y, b.y));
  };
};

# Lt --
#   Less than operator for Coord@.
/Core/Eq%.Eq@<Coord@> Lt = {
  % IntEq = /Core/Int/Eq%.Eq;
  % IntLt = /Core/Int/Eq%.Lt;
  (Coord@ a, Coord@ b) {
    Or(IntLt(a.x, b.x), And(IntEq(a.x, b.x), IntLt(a.y, b.y)));
  };
};

Ord@<Coord@> Ord = {
  % OrdInt = /Core/Int/Ord%.Ord;
  (Coord@ a, Coord@ b) {
    Ordering@ r = OrdInt(a.x, b.x);
    r.?(eq: OrdInt(a.y, b.y), : r);
  };
};

/Core/Show%.Show@<Coord@> Show = (Coord@ x) {
  Strs[Str|'(', /Core/Int/Show%.Show(x.x), Str|',', /Core/Int/Show%.Show(x.y), Str|')'];
};

@(Coord@, Add, Eq, Lt, Ord, Show);
