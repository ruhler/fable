
<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<@>@ Get@ = /Core/IO/Process%.Get@;
<@>@ Put@ = /Core/IO/Process%.Put@;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ AI@ = /Games/TicTacToe/AI%.AI@;
@ AIR@ = /Games/TicTacToe/AI%.AIR@;
% AI = /Games/TicTacToe/AI%;

@ Board@ = /Games/TicTacToe/Board%.Board@;
@ Position@ = /Games/TicTacToe/Board%.Position@;
@ Player@ = /Games/TicTacToe/Board%.Player@;
% BoardStatus = /Games/TicTacToe/Board%.BoardStatus;
% EmptyBoard = /Games/TicTacToe/Board%.EmptyBoard;
% Move = /Games/TicTacToe/Board%.Move;


@ GameResult@ = +(Player@ win, Unit@ draw);
@ GameStatus@ = +(Player@ playing, GameResult@ ended); 
@ Output@ = *(Board@ board, GameStatus@ status);

# move: Make a move at the given position for the current player. If no
#       position is supplied, have the computer pick a reasonable move instead.
# reset: Reset the game.
# quit: Quit the game.
@ Input@ = +(Maybe@<Position@> move, Unit@ reset, Unit@ quit);

(Get@<Input@>, Put@<Output@>, AI@){Unit@!;}
NewGame = (Input@! input, Put@<Output@> output, AI@ ai) {
  PlayGame(input, output, ai, EmptyBoard, Player@(X: Unit));
},
(Input@!, Put@<Output@>, AI@, Board@, Player@){Unit@!;}
PlayGame = (Input@! input, Put@<Output@> output, AI@ ai, Board@ board, Player@ x) {
  GameStatus@ status := !(BoardStatus(board).?(
                          X: GameStatus@(ended: GameResult@(win: Player@(X: Unit))),
                          O: GameStatus@(ended: GameResult@(win: Player@(O: Unit))),
                          D: GameStatus@(ended: GameResult@(draw: Unit)),
                          E: GameStatus@(playing: x)));
  Unit@ _ := output(Output@(board, status));
  Input@ in := input;
  in.?(quit: !(Unit));
  in.?(reset: NewGame(input, output, ai));
  status.?(ended: PlayGame(input, output, ai, board, x));

  AIR@ air := in.move.?(
    just: {
      Position@ position := !(in.move.just);
      !(AIR@(ai, position));
    },
    nothing: !(AI.Move(ai, board, x)));

  Maybe@<Board@> nboard := !(Move(board, air.position, x));
  nboard.?(
      just: PlayGame(input, output, air.ai, nboard.just, x.?(X: Player@(O: Unit), O: Player@(X: Unit))),
      nothing: PlayGame(input, output, air.ai, board, x));
};

(Get@<Input@>, Put@<Output@>){Unit@!;} TicTacToe = (Get@<Input@> input, Put@<Output@> output) {
  NewGame(input, output, AI.Initial);
};

@(GameStatus@, Input@, Output@, TicTacToe);
