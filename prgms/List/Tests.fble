
@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ Bool@ = /Bool%.Bool@;
% True = /Bool%.True;
% False = /Bool%.False;

@ Char@ = /Char%.Char@;
% Chars = /Char%.Chars;

<@>@ Eq@ = /Eq%.Eq@;

@ Int@ = /Int/Int%.Int@;
% I = /Int/Int/Lit%.I;
% D = /Int/Int/Lit%.D;

<@>@ List@ = /List%.List@;
<@>% List = /List%.List;
<@>% Cons = /List%.Cons;
<@>% Nil = /List%.Nil;
<@>% Append = /List%.Append;
<@>% Init = /List%.Init;
<@>% Concat = /List%.Concat;
<@,@>% Map = /List%.Map;
<@,@>% ForEach = /List%.ForEach;
<@>% Length = /List/Length%.Length;
<@>% Sort = /List/Sort%.Sort;

<@>@ Show@ = /Show%.Show@;

@ String@ = /String%.String@;
% Str = /String%.Str;

@ Test@ = /Test%.Test@;
@ TestResult@ = /Test%.TestResult@;
@ TestSuite@ = /Test%.TestSuite@;
% TestSuite = /Test%.TestSuite;

@ Elem@ = +(Unit@ A, Unit@ B, Unit@ C, Unit@ D, Unit@ E);

% Elems = @(
  A: Elem@(A: Unit),
  B: Elem@(B: Unit),
  C: Elem@(C: Unit),
  D: Elem@(D: Unit),
  E: Elem@(E: Unit));

(Elem@) { String@; } ElemShow = (Elem@ e) {
  Str(e.?(A: Chars|A, B: Chars|B, C: Chars|C, D: Chars|D, E: Chars|E));
};

(Elem@, Elem@) { Bool@; } ElemLe = (Elem@ a, Elem@ b) {
  a.?(A: True,
      B: b.?(A: False, : True),
      C: b.?(A: False, B: False, : True),
      D: b.?(A: False, B: False, C: False, : True),
      E: b.?(A: False, B: False, C: False, D: False, E: True));
};

(Elem@, Elem@){Bool@;} ElemEq = (Elem@ a, Elem@ b) {
  a.?(A: b.?(A: True, : False),
      B: b.?(B: True, : False),
      C: b.?(C: True, : False),
      D: b.?(D: True, : False),
      E: b.?(E: True, : False));
};

Eq@<List@<Elem@>> Eq = /List/Eq%.Eq<Elem@>(ElemEq);
Show@<List@<Elem@>> Show = /List/Show%.Show<Elem@>(ElemShow);

(List@<Elem@>, List@<Elem@>) { TestResult@; }
AssertEquals = (List@<Elem@> a, List@<Elem@> b) {
  /Test%.AssertEquals<List@<Elem@>>(Eq, Show, a, b);
};

TestSuite@ Tests = TestSuite(Chars|List, [
  Test@(Str(Chars|'Eq_Same'), !({
    List@<Elem@> a = List<Elem@>(Elems|ABCDE);
    List@<Elem@> b = List<Elem@>(Elems|ABCDE);

    Eq(a, b).?(
      true: TestResult@(pass: Unit),
      false: TestResult@(fail: Concat<Char@>(List<String@>([
        Show(a), Str(Chars|' != '), Show(b)
      ]))));
  })),

  Test@(Str(Chars|'Eq_DiffElem'), !({
    List@<Elem@> a = List<Elem@>(Elems|ABCDE);
    List@<Elem@> b = List<Elem@>(Elems|ABDDE);

    Eq(a, b).?(
      true: TestResult@(fail: Concat<Char@>(List<String@>([
          Show(a), Str(Chars|' == '), Show(b)
        ]))),
      false: TestResult@(pass: Unit));
  })),

  Test@(Str(Chars|'Eq_DiffSize'), !({
    List@<Elem@> a = List<Elem@>(Elems|ABCDE);
    List@<Elem@> b = List<Elem@>(Elems|ABCD);

    Eq(a, b).?(
      true: TestResult@(fail: Concat<Char@>(List<String@>([
          Show(a), Str(Chars|' == '), Show(b)
        ]))),
      false: TestResult@(pass: Unit));
  })),

  Test@(Str(Chars|'Append'), !({
    List@<Elem@> a = List<Elem@>(Elems|ABC);
    List@<Elem@> b = List<Elem@>(Elems|DEE);
    List@<Elem@> wnt = List<Elem@>(Elems|ABCDEE);
    List@<Elem@> got = Append<Elem@>(a, b);
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'Init'), !({
    List@<Elem@> l = List<Elem@>(Elems|ABCDE);
    List@<Elem@> wnt = List<Elem@>(Elems|ABCD);
    List@<Elem@> got = Init<Elem@>(l);
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'Concat'), !({
    List@<List@<Elem@>> l = List<List@<Elem@>>([
      List<Elem@>(Elems|ABC),
      List<Elem@>(Elems|DEA),
      List<Elem@>(Elems|BB)
    ]);

    List@<Elem@> wnt = List<Elem@>(Elems|ABCDEABB);
    List@<Elem@> got = Concat<Elem@>(l);
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'Map'), !({
    List@<Elem@> l = List<Elem@>(Elems|ABCDE);
    (Elem@) { Elem@; } f = (Elem@ e) {
      e.?(A: Elems.B, B: Elems.C, C: Elems.D, D: Elems.E, E: Elems.A);
    };

    List@<Elem@> wnt = List<Elem@>(Elems|BCDEA);
    List@<Elem@> got = Map<Elem@, Elem@>(l, f);
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'ForEach'), !({
    List@<Elem@> l = List<Elem@>(Elems|ABCDE);
    List@<Elem@> wnt = List<Elem@>(Elems|EDCBA);
    List@<Elem@> got =
      ForEach<Elem@, List@<Elem@>>(l, Nil<Elem@>, (Elem@ e, List@<Elem@> b) {
        Cons<Elem@>(e, b);
      });
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'Sort'), !({
    List@<Elem@> unsorted = List<Elem@>(Elems|DABEC);
    List@<Elem@> wnt = List<Elem@>(Elems|ABCDE);
    List@<Elem@> got = Sort<Elem@>(ElemLe, unsorted);
    AssertEquals(wnt, got);
  })),

  Test@(Str(Chars|'Length'), !({
    Int@ wnt = I(D|5);
    Int@ got = Length<Elem@>(List<Elem@>(Elems|ABCDE));
    /Test%.AssertEquals<Int@>(/Int/Int/Eq%.Eq, /Int/Int/Show%.Show, wnt, got);
  }))
]);

@(Tests);
