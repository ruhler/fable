<@>@ List@ = /Core/List%.List@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ StringL@ = /Fbld/StringL%.StringL@;

<@>@ Result@ = /Fbld/Result%.Result@;
<@>% Ok = /Fbld/Result%.Ok;
<@>% Err = /Fbld/Result%.Err;

<<@>@>@ R@ = /Fbld/Types%.R@;
@ Command@ = /Fbld/Types%.Command@;
<<@>@>@ Invoke@ = /Fbld/Types%.Invoke@;
<<@>@>% DoResult = /Fbld/Types%.DoResult;

<<@>@ M@>(Monad@<M@> m) {

  <@>% DoR = DoResult(m);

  (Invoke@<M@>, Invoke@<M@>, StringL@) { R@<M@>; }
  Inline = (Invoke@<M@> inline, Invoke@<M@> block, StringL@ text) {
    Result@<List@<Command@>> parsed = /Fbld/Parse%.Inline(text);
    parsed.?(err: m.return(Err<Unit@>(parsed.err)));

    /Core/List%.ForEach(parsed.ok, m.return(Ok(Unit)),
      (Command@ cmd, R@<M@> mx) {
        Unit@ _ <- DoR<Unit@>(mx);
        inline(inline, block, cmd);
      });
  };

  (Invoke@<M@>, Invoke@<M@>, StringL@) { R@<M@>; }
  Block = (Invoke@<M@> inline, Invoke@<M@> block, StringL@ text) {
    Result@<List@<Command@>> parsed = /Fbld/Parse%.Block(text);
    parsed.?(err: m.return(Err<Unit@>(parsed.err)));

    /Core/List%.ForEach(parsed.ok, m.return(Ok(Unit)),
      (Command@ cmd, R@<M@> mx) {
        Unit@ _ <- DoR(mx);
        block(inline, block, cmd);
      });
  };

  @(Inline, Block);
};
