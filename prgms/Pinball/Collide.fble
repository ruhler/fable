
<@>@ List@ = /List%.List@;
<@>@ Maybe@ = /Maybe%.Maybe@;

@ Event@ = /Pinball/Event%.Event@;
% Events = /Pinball/Event%.Events;
% BallEvent = /Pinball/Event%.BallEvent;
% TimeEvent = /Pinball/Event%.TimeEvent;

@ Num@ = /Pinball/Num%.Num@;
% Add = /Pinball/Num%.Add;
% Sub = /Pinball/Num%.Sub;
% Neg = /Pinball/Num%.Neg;
% Mul = /Pinball/Num%.Mul;
% Div = /Pinball/Num%.Div;
% Eq = /Pinball/Num%.Eq;
% Gt = /Pinball/Num%.Gt;
% Ge = /Pinball/Num%.Ge;
% Lt = /Pinball/Num%.Lt;
% Le = /Pinball/Num%.Le;
% Int = /Pinball/Num%.Int;

@ Ball@ = /Pinball/Ball%.Ball@;

@ Edge@ = /Pinball/Edge%.Edge@;

@ Paddle@ = /Pinball/Paddle%.Paddle@;

% Rotate = /Pinball/Rotate%.Rotate;

@ Linear@ = /Pinball/Quadratic%.Linear@;
@ Quadratic@ = /Pinball/Quadratic%.Quadratic@;
@ Roots@ = /Pinball/Quadratic%.Roots@;
% SubQQ = /Pinball/Quadratic%.SubQQ;
% SubLL = /Pinball/Quadratic%.SubLL;
% MulLL = /Pinball/Quadratic%.MulLL;
% Roots = /Pinball/Quadratic%.Roots;

@ Vec@ = /Pinball/Vec%.Vec@;
% AddV = /Pinball/Vec%.AddV;
% SubV = /Pinball/Vec%.SubV;
% MulVS = /Pinball/Vec%.MulVS;
% RotateTo = /Pinball/Vec%.RotateTo;
% RotateFrom = /Pinball/Vec%.RotateFrom;
% Magnitude = /Pinball/Vec%.Magnitude;
% Normalize = /Pinball/Vec%.Normalize;

# BallCollider@ --
#   Check for and return collision results between a ball and an object.
#
# Returns a time event if there is no collision within the given time.
# If there is a collision, return the ball in the state just after the
# collision at the time of collision.
<@>@ BallCollider@ = <@ T@>(Ball@, T@, Num@) { Event@; };

# BallEdge --
#   Check for and return collision results between a ball and an edge.
#
# Returns Nothing if there is no collision within the given time. If there is a
# collision, return the ball in the state just after the collision at the
# time of collision.
BallCollider@<Edge@> BallEdge = (Ball@ ball, Edge@ edge, Num@ tmax) {
  Event@ NoCollision = TimeEvent(tmax);

  # Verify the ball starts above the edge.
  Vec@ s = RotateTo(edge.dir, SubV(ball.pos, edge.start));
  Lt(s.y, Int|0).?(true: NoCollision);

  # Verify the ball ends within radius units of the edge.
  Num@ radius = ball.radius;
  Vec@ end = AddV(ball.pos, MulVS(ball.vel, tmax));
  Vec@ e = RotateTo(edge.dir, SubV(end, edge.start));
  Gt(e.y, radius).?(true: NoCollision);

  # Verify the ball is moving towards the edge instead of away from it.
  Vec@ v = RotateTo(edge.dir, ball.vel);
  Ge(v.y, Int|0).?(true: NoCollision);

  # Potential collision detected.
  # r = y0 + vt ==>  t = (r-y0)/v
  Num@ t = Lt(s.y, radius).?(true: Int|0, false: Div(Sub(radius, s.y), v.y));

  # Verify the collision happens within the segment of the edge.
  Num@ x = Add(s.x, Mul(v.x, t));
  Lt(x, Int|0).?(true: NoCollision);
  Gt(x, edge.length).?(true: NoCollision);

  # Compute the new state just after the collision.
  Vec@ vc = Vec@(v.x, Mul(edge.restitution, Neg(v.y)));
  Vec@ pos = AddV(ball.pos, MulVS(ball.vel, t));
  Vec@ vel = RotateFrom(edge.dir, vc);
  BallEvent(t, @(pos, vel, radius));
};

BallCollider@<List@<Edge@>>
BallEdgeS = (Ball@ ball, List@<Edge@> edges, Num@ tmax) {
  edges.?(nil: TimeEvent(tmax));
  Events[
    BallEdge(ball, edges.cons.head, tmax),
    BallEdgeS(ball, edges.cons.tail, tmax)
  ];
};

# BallPaddle --
#   Check for and return collision results between a ball and a paddle.
#
# Returns Nothing if there is no collision within the given time. If there is a
# collision, return the ball in the state just after the collision at the
# time of collision.
BallCollider@<Paddle@> BallPaddle = (Ball@ ball, Paddle@ paddle, Num@ tm) {
  # Compute time for the ball to collide with the line of the paddle.
  # We approximate the motion of the line of the paddle as the line from a
  # start point that travels at constant velocity from the initial paddle's
  # start point to the end paddle's start point to an end point that travels
  # at constant velocity from the initial paddle's end point to the final
  # paddle's end point.
  Event@ NoCollision = TimeEvent(tm);
  Num@ radius = ball.radius;

  # Compute the position of the start point of the paddle as a linear function
  # of time, scaled by factor tm.
  Num@ p0 = paddle.pos;
  Num@ pf = Add(paddle.pos, Mul(paddle.vel, tm));
  Vec@ s0 = AddV(paddle.center, Rotate(paddle.start, p0));
  Vec@ sf = AddV(paddle.center, Rotate(paddle.start, pf));
  Linear@ sx_t = Linear@(Sub(sf.x, s0.x), Mul(tm, s0.x));
  Linear@ sy_t = Linear@(Sub(sf.y, s0.y), Mul(tm, s0.y));

  # Compute the position of the end point of the paddle as a linear function
  # of time, scaled by factor tm.
  Vec@ e0 = AddV(paddle.center, Rotate(paddle.end, p0));
  Vec@ ef = AddV(paddle.center, Rotate(paddle.end, pf));
  Linear@ ex_t = Linear@(Sub(ef.x, e0.x), Mul(tm, e0.x));
  Linear@ ey_t = Linear@(Sub(ef.y, e0.y), Mul(tm, e0.y));

  # Compute the position of the center of the ball as a linear function of
  # time.
  Linear@ bx_t = Linear@(ball.vel.x, ball.pos.x);
  Linear@ by_t = Linear@(ball.vel.y, ball.pos.y);

  # Compute the direction vector of the line of the paddle as a function of
  # time, scaled by factor tm.
  Linear@ dx_t = SubLL(ex_t, sx_t);
  Linear@ dy_t = SubLL(ey_t, sy_t);

  # Compute the distance of the center of the ball from the line of the paddle
  # as a quadratic function of time, scaled by factor tm * l, where l is the
  # length of the paddle.
  Quadratic@ d_t = SubQQ(MulLL(dx_t, SubLL(by_t, sy_t)), MulLL(dy_t, SubLL(bx_t, sx_t)));

  # Our goal is to find the time when (d - radius) = 0, taking proper scaling
  # into account.
  # Radius scaled by factor tm * l
  Num@ l = Magnitude(SubV(paddle.end, paddle.start));
  Quadratic@ r_t = Quadratic@(Int|0, Int|0, Mul(tm, Mul(l, radius)));
  Quadratic@ q_t = SubQQ(d_t, r_t);


  Maybe@<Roots@> roots = Roots(q_t);
  roots.?(nothing: NoCollision);
  Num@ t = {
    Lt(roots.just.first, Int|0).?(true: roots.just.second);
    roots.just.first;
  };
  Gt(t, tm).?(true: NoCollision);

  # Compute where everything is at the time of collision.
  Num@ pc = Add(paddle.pos, Mul(paddle.vel, t));
  Vec@ bc = AddV(ball.pos, MulVS(ball.vel, t));
  Vec@ sc = AddV(paddle.center, Rotate(paddle.start, pc));
  Vec@ ec = AddV(paddle.center, Rotate(paddle.end, pc));
  Vec@ dc = Normalize(SubV(ec, sc));

  # Compute where the ball is relative to the edge of the paddle at the time
  # of collision.
  Vec@ bcr = RotateTo(dc, SubV(bc, sc));
  Lt(bcr.x, Int|0).?(true: NoCollision);
  Gt(bcr.x, l).?(true: NoCollision);

  # Compute the velocity of ball and paddle at time of collision, relative to
  # the edge of the paddle at the time of collision.
  Vec@ bv = RotateTo(dc, ball.vel);
  Vec@ center = RotateTo(dc, SubV(paddle.center, sc));
  Vec@ dcenter = SubV(Vec@(bcr.x, Int|0), center);
  Num@ r = Magnitude(dcenter);
  Num@ vm = Mul(r, paddle.vel);
  Num@ vp = Div(Mul(vm, dcenter.x), r);

  # Verify the ball is moving towards the edge at the time of collision.
  Num@ vr = Sub(vp, bv.y);                 # negative relative velocity.
  Le(vr, Int|0).?(true: NoCollision);

  # Compute the new state just after the collision.
  Num@ nvr = Mul(paddle.res, vr);         # updated relative velocity.
  Vec@ nva = Vec@(bv.x, Add(nvr, vp));    # updated absolute velocity.
  Vec@ nbv = RotateFrom(dc, nva);         # Convert to absolute coordinates.
  BallEvent(t, Ball@(bc, nbv, radius));
};

BallCollider@<List@<Paddle@>>
BallPaddleS = (Ball@ ball, List@<Paddle@> paddles, Num@ tmax) {
  paddles.?(nil: TimeEvent(tmax));
  Events[
    BallPaddle(ball, paddles.cons.head, tmax),
    BallPaddleS(ball, paddles.cons.tail, tmax)
  ];
};

# PaddleRange --
#   Check for an event where a paddle reaches the end of its range.
(Paddle@, Num@) { Event@; } PaddleRange = (Paddle@ paddle, Num@ tmax) {
  Event@ NoCollision = TimeEvent(tmax);

  Eq(Int|0, paddle.vel).?(true: NoCollision);

  Num@ t = Lt(paddle.vel, Int|0).?(
    true: Div(Sub(paddle.min, paddle.pos), paddle.vel),
    false: Div(Sub(paddle.max, paddle.pos), paddle.vel));
  Events[NoCollision, TimeEvent(t)];
};

# PaddleRangeS --
#   Check for an event where a paddle reaches the end of its range.
(List@<Paddle@>, Num@) { Event@; }
PaddleRangeS = (List@<Paddle@> paddles, Num@ tmax) {
  paddles.?(nil: TimeEvent(tmax));
  Events[
    PaddleRange(paddles.cons.head, tmax),
    PaddleRangeS(paddles.cons.tail, tmax)
  ];
};

@(BallEdge, BallEdgeS, BallPaddle, BallPaddleS, PaddleRange, PaddleRangeS);
