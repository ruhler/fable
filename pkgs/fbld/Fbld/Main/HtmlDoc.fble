
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ Str@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;

<@>@ Result@ = /Fbld/Types%.Result@;
<<@>@>@ Invoke@ = /Fbld/Types%.Invoke@;
@ String@ = /Fbld/Types%.String@;
% Start = /Fbld/Types%.Start;



# Main --
#   Main function for fbld-html-doc program.
#
# Converts an fbld @doc into html format.
Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);
  % Markup = /Fbld/Markup%(m);
  @ Markup@ = Markup.Markup@;

  % Core = /Fbld/Core%(m);
  @ Core@ = Core.Core@;

  Core@ core = /Fbld/Html%(m)(stdio.out).Core;
  Markup@ inline_markup = Core.InlineMarkup(core);
  Markup@ block_markup = Core.BlockMarkup(core);
  Invoke@<M@> inline = Markup.Invoke(inline_markup);
  Invoke@<M@> block = Markup.Invoke(block_markup);
  % Invoke = /Fbld/Invoke%(m);

  (List@<Str@> args) {
    args.?(nil: {
      Str@ msg = Str|'missing input file';
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    Str@ filename = args.cons.head;
    
    Maybe@<IStream@<M@>> fin <- m.do(stdio.read(filename));
    fin.?(nothing: {
      Str@ msg = Strs[Str|'unable to open ', filename];
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    Str@ contents <- m.do(I.GetText(fin.just));
    Result@<Unit@> r <- m.do(Invoke.Block(inline, block, String@(contents, Start(filename))));
    r.?(err: {
      Str@ msg = Strs[r.err.loc.file, Str|':',
        /Core/Int/Show%.Show(r.err.loc.line), Str|':',
        /Core/Int/Show%.Show(r.err.loc.col), Str|': ',
        r.err.str];
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    m.return(True);
  };
};

/Core/Stdio/IO%.Run(Main);
