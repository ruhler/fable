
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
% And = /Core/Bool%.And;

<@>@ Eq@ = /Core/Eq%.Eq@;

@ Int@ = /Core/Int%.Int@;
% Incr = /Core/Int%.Incr;
% Decr = /Core/Int%.Decr;

% IsZero = /Core/Int/Eq%.IsZero;
% Eq = /Core/Int/Eq%.Eq;
% Ord = /Core/Int/Ord%.Ord;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% ForEach = /Core/List%.ForEach;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<@>@ Ord@ = /Core/Ord%.Ord@;
@ Ordering@ = /Core/Ord%.Ordering@;

<@>@ Show@ = /Core/Show%.Show@;

% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Coord@ = *(Int@ row, Int@ col);

Eq@<Coord@> EqCoord = (Coord@ a, Coord@ b) {
  And(Eq(a.row, b.row), Eq(a.col, b.col));
};

Ord@<Coord@> OrdCoord = (Coord@ a, Coord@ b) {
  Ordering@ o = Ord(a.row, b.row);
  o.?(eq: Ord(a.col, b.col), : o);
};

Show@<Coord@> ShowCoord = (Coord@ a) {
  Strs[
    Str|'(', /Core/Int/Show%.Show(a.row),
    Str|', ', /Core/Int/Show%.Show(a.col),
    Str|')'];
};

# The world is a set of Coord@ where there are live cells.
@ World@ = /Core/Set%.Set@<Coord@>;
% EqWorld = /Core/Set/Eq%.Eq(EqCoord);
% ShowWorld = /Core/Set/Show%.Show(ShowCoord);
% W = @(
  Empty: /Core/Set%.Empty<Coord@>,
  Insert: /Core/Set%.Insert(OrdCoord),
  ForEach: /Core/Set%.ForEach<Coord@>
);

# Create a world from a list of coordinates of live cells.
(List@<Coord@>){ World@; } World = (List@<Coord@> mkCells) {
  mkCells.?(nil: W.Empty);
  W.Insert(World(mkCells.cons.tail), mkCells.cons.head);
};

# A count of neighboring cells. 4p stands for anything greater than or equal
# to 4.
@ Count@ = +(Unit@ 0, Unit@ 1, Unit@ 2, Unit@ 3, Unit@ 4p);

(Count@) { Count@; } IncCount = (Count@ c) {
  c.?(0: Count@(1: Unit),
      1: Count@(2: Unit),
      2: Count@(3: Unit),
      3: Count@(4p: Unit),
      4p: Count@(4p: Unit));
};

@ Status@ = *(Bool@ alive, Count@ neighbors);

# Returns true if a cell with the given status should be a live cell in the
# next generation.
(Status@) { Bool@; } Lives = (Status@ status) {
  status.alive.?(
    true: status.neighbors.?(2: True, 3: True, : False),
    false: status.neighbors.?(3: True, : False));
};

# Map from Coord to the status of the cell.
@ Cand@ = /Core/Map/Tree%.Map@<Coord@, Status@>;
% C = @(
  Empty: /Core/Map/Tree%.Empty<Coord@, Status@>,
  InsertWith: /Core/Map/Tree%.InsertWith(OrdCoord)<Status@>,
  ForEach: /Core/Map/Tree%.ForEach<Coord@, Status@>
);

# Advance the world by one unit of time.
(World@) { World@; } Step = (World@ w) {
  # 1. Create the candidate map.
  #   For each cell in world,
  #     Set this cell alive in candidate
  #     Incr neighbors for each neighbor cell
  Cand@ candidates = W.ForEach(w, C.Empty,
    (Coord@ coord, Cand@ cand_in) {
      Cand@ cand_alive = C.InsertWith(cand_in, coord, (Maybe@<Status@> ms) {
        ms.?(just: Status@(True, ms.just.neighbors),
             nothing: Status@(True, Count@(0: Unit)));
      });

      List@<Coord@> neighbors = List[
        Coord@(Decr(coord.row), Decr(coord.col)),
        Coord@(Decr(coord.row),     coord.col ),
        Coord@(Decr(coord.row), Incr(coord.col)),
        Coord@(    coord.row , Decr(coord.col)),
        Coord@(    coord.row , Incr(coord.col)),
        Coord@(Incr(coord.row), Decr(coord.col)),
        Coord@(Incr(coord.row),     coord.col ),
        Coord@(Incr(coord.row), Incr(coord.col))];

      ForEach(neighbors, cand_alive, (Coord@ n, Cand@ c) {
        C.InsertWith(c, n, (Maybe@<Status@> ms) {
          ms.?(just: Status@(ms.just.alive, IncCount(ms.just.neighbors)),
               nothing: Status@(False, Count@(1: Unit)));
        });
      });
    }
  );

  # 2. Filter live cells from the map.
  C.ForEach(candidates, W.Empty,
    (Coord@ coord, Status@ status, World@ w_in) {
    Lives(status).?(false: w_in);
    W.Insert(w_in, coord);
  });
};

(Int@, World@) { World@; } StepN = (Int@ n, World@ w) {
  IsZero(n).?(true: w);
  StepN(Decr(n), Step(w));
};

@(Coord@, World@, World, ForWorld: W.ForEach, EqWorld, ShowWorld, Step, StepN);
