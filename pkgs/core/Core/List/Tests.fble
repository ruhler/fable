
@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ Char@ = /Core/Char%.Char@;

<@>@ Eq@ = /Core/Eq%.Eq@;

@ Int@ = /Core/Int%.Int@;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;
<@>% Append = /Core/List%.Append;
<@>% Init = /Core/List%.Init;
<@>% Concat = /Core/List%.Concat;
<@,@>% Map = /Core/List%.Map;
<@,@>% ForEach = /Core/List%.ForEach;
<@>% Reverse = /Core/List%.Reverse;

<@>% Length = /Core/List/Length%.Length;
<@>% Split = /Core/List/Eq%.Split;
<@>% Sort = /Core/List/Sort%.Sort;

<@>@ Show@ = /Core/Show%.Show@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Test@ = /Core/Test%.Test@;
@ TestResult@ = /Core/Test%.TestResult@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;

@ Elem@ = +(Unit@ A, Unit@ B, Unit@ C, Unit@ D, Unit@ E);

% Elems = @(
  A: Elem@(A: Unit),
  B: Elem@(B: Unit),
  C: Elem@(C: Unit),
  D: Elem@(D: Unit),
  E: Elem@(E: Unit));

(List@<Elem@>) { List@<Elem@>; } EL = List<Elem@>;

(Elem@) { String@; } ElemShow = (Elem@ e) {
  e.?(A: Str|A, B: Str|B, C: Str|C, D: Str|D, E: Str|E);
};

(Elem@, Elem@) { Bool@; } ElemLe = (Elem@ a, Elem@ b) {
  a.?(A: True,
      B: b.?(A: False, : True),
      C: b.?(A: False, B: False, : True),
      D: b.?(A: False, B: False, C: False, : True),
      E: b.?(A: False, B: False, C: False, D: False, E: True));
};

(Elem@, Elem@){Bool@;} ElemEq = (Elem@ a, Elem@ b) {
  a.?(A: b.?(A: True, : False),
      B: b.?(B: True, : False),
      C: b.?(C: True, : False),
      D: b.?(D: True, : False),
      E: b.?(E: True, : False));
};

Eq@<List@<Elem@>> Eq = /Core/List/Eq%.Eq<Elem@>(ElemEq);
Show@<List@<Elem@>> Show = /Core/List/Show%.Show<Elem@>(ElemShow);

(List@<Elem@>, List@<Elem@>) { TestResult@; }
AssertEquals = (List@<Elem@> a, List@<Elem@> b) {
  /Core/Test%.AssertEquals<List@<Elem@>>(Eq, Show, a, b);
};

Test@ Tests = TestSuite(Str|List, List<Test@>[
  Test(Str|'Eq_Same', (Unit@ _) {
    List@<Elem@> a = EL|ABCDE;
    List@<Elem@> b = EL|ABCDE;

    Eq(a, b).?(
      true: TestResult@(pass: Unit),
      false: TestResult@(fail: Concat<Char@>(List<String@>[
        Show(a), Str|' != ', Show(b)
      ])));
  }),

  Test(Str|'Eq_DiffElem', (Unit@ _) {
    List@<Elem@> a = EL|ABCDE;
    List@<Elem@> b = EL|ABDDE;

    Eq(a, b).?(
      true: TestResult@(fail: Concat<Char@>(List<String@>[
          Show(a), Str|' == ', Show(b)
        ])),
      false: TestResult@(pass: Unit));
  }),

  Test(Str|'Eq_DiffSize', (Unit@ _) {
    List@<Elem@> a = EL|ABCDE;
    List@<Elem@> b = EL|ABCD;

    Eq(a, b).?(
      true: TestResult@(fail: Concat<Char@>(List<String@>[
          Show(a), Str|' == ', Show(b)
        ])),
      false: TestResult@(pass: Unit));
  }),

  Test(Str|'Append', (Unit@ _) {
    List@<Elem@> a = EL|ABC;
    List@<Elem@> b = EL|DEE;
    List@<Elem@> wnt = EL|ABCDEE;
    List@<Elem@> got = Append<Elem@>(a, b);
    AssertEquals(wnt, got);
  }),

  Test(Str|'Init', (Unit@ _) {
    List@<Elem@> l = EL|ABCDE;
    List@<Elem@> wnt = EL|ABCD;
    List@<Elem@> got = Init<Elem@>(l);
    AssertEquals(wnt, got);
  }),

  Test(Str|'Concat', (Unit@ _) {
    List@<List@<Elem@>> l = List<List@<Elem@>>[
      EL|ABC,
      EL|DEA,
      EL|BB
    ];

    List@<Elem@> wnt = EL|ABCDEABB;
    List@<Elem@> got = Concat<Elem@>(l);
    AssertEquals(wnt, got);
  }),

  Test(Str|'Map', (Unit@ _) {
    List@<Elem@> l = EL|ABCDE;
    (Elem@) { Elem@; } f = (Elem@ e) {
      e.?(A: Elems.B, B: Elems.C, C: Elems.D, D: Elems.E, E: Elems.A);
    };

    List@<Elem@> wnt = EL|BCDEA;
    List@<Elem@> got = Map<Elem@, Elem@>(l, f);
    AssertEquals(wnt, got);
  }),

  Test(Str|'ForEach', (Unit@ _) {
    List@<Elem@> l = EL|ABCDE;
    List@<Elem@> wnt = EL|EDCBA;
    List@<Elem@> got =
      ForEach<Elem@, List@<Elem@>>(l, Nil<Elem@>, (Elem@ e, List@<Elem@> b) {
        Cons<Elem@>(e, b);
      });
    AssertEquals(wnt, got);
  }),

  Test(Str|'Reverse', (Unit@ _) {
    List@<Elem@> input = EL|ABCDE;
    List@<Elem@> wnt = EL|EDCBA;
    List@<Elem@> got = Reverse(input);
    AssertEquals(wnt, got);
  }),

  Test(Str|'Sort', (Unit@ _) {
    List@<Elem@> unsorted = EL|DABEC;
    List@<Elem@> wnt = EL|ABCDE;
    List@<Elem@> got = Sort<Elem@>(ElemLe, unsorted);
    AssertEquals(wnt, got);
  }),

  Test(Str|'Length', (Unit@ _) {
    Int@ wnt = Int|5;
    Int@ got = Length<Elem@>(EL|ABCDE);
    /Core/Test%.AssertEquals<Int@>(/Core/Int/Eq%.Eq, /Core/Int/Show%.Show, wnt, got);
  }),

  Test(Str|'Split', (Unit@ _) {
    @ T@ = +(Unit@ ' ', Unit@ X);
    Eq@<T@> eq = (T@ a, T@ b) {
      a.?(' ': b.?(' ': True, 'X': False), 'X': b.?(' ': False, 'X': True));
    };
    Show@<T@> show = (T@ x) { x.?(' ': Str|' ', 'X': Str|'X'); };

    (T@) { Bool@; } sep = (T@ x) { x.?(' ': True, 'X': False); };
    List@<T@> list = List<T@>|'  XXX X  XX ';
    List@<List@<T@>> want = List<List@<T@>>[List<T@>|XXX, List<T@>|X, List<T@>|XX];
    List@<List@<T@>> got = Split<T@>(list, sep);
    /Core/Test%.AssertEquals<List@<List@<T@>>>(
      /Core/List/Eq%.Eq<List@<T@>>(/Core/List/Eq%.Eq<T@>(eq)),
      /Core/List/Show%.Show<List@<T@>>(/Core/List/Show%.Show<T@>(show)),
      want, got);
  })
]);

@(Tests);
