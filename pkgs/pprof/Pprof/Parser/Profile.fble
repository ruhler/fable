
@ Int@ = /Core/Int%.Int@;
% Incr = /Core/Int%.Incr;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Nil = /Core/List%.Nil;
<@>% Cons = /Core/List%.Cons;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;

% Map = /Core/Int/Map%.Map;

@ Sample@ = /Pprof/Profile%.Sample@;

<<@>@>@ Proto@ = /Pprof/Parser/Proto%.Proto@;
@ FieldId@ = /Pprof/Parser/Proto%.FieldId@;
@ Payload@ = /Pprof/Parser/Proto%.Payload@;
@ Record@ = /Pprof/Parser/Proto%.Record@;

Int@ 0 = Int|0;

@ StringId@ = Int@;
@ LocationId@ = Int@;
@ FunctionId@ = Int@;

@ Location@ = *(LocationId@ id, FunctionId@ func_id, Int@ line, Int@ col);
@ Function@ = *(FunctionId@ id, StringId@ name, StringId@ file);

@ Profile@ = *(
  List@<StringId@> sample_types,          # reverse order
  List@<Sample@> samples,                 # reverse order
  Map.Map@<Location@> locations,
  Map.Map@<Function@> functions,
  Map.Map@<String@> strings,
  StringId@ next_string_id
);

Profile@ EmptyProfile = Profile@(
  Nil<StringId@>,
  Nil<Sample@>,
  Map.Empty<Location@>,
  Map.Empty<Function@>,
  Map.Empty<String@>,
  0
);

# @func[Profile] A profile proto parser.
<<@>@ M@>(Proto@<M@>, Monad@<M@>) { M@<Profile@>; }
Profile = <<@>@ M@>(Proto@<M@> p, Monad@<M@> m) {

  (Payload@, String@) { M@<Int@>; }
  LengthFor = (Payload@ payload, String@ field_name) {
    payload.?(length: m.return(payload.length));
    p.Fail<Int@>(Strs[field_name, Str|' field is not of wire_type LEN']);
  };

  (Payload@, String@) { M@<Int@>; }
  IntFor = (Payload@ payload, String@ field_name) {
    payload.?(int: m.return(payload.int));
    p.Fail<Int@>(Strs[field_name, Str|' field is not of wire_type VARINT']);
  };

  (Record@, StringId@) { M@<StringId@>; }
  SampleTypeFields = (Record@ record, StringId@ type) {
    /Core/Int/Eq%.Eq(record.field, Int|1).?(true: {
      IntFor(record.payload, Str|'ValueType.type');
    });

    Unit@ _ <- m.do(p.Skip(record.payload));
    m.return(type);
  };

  (Profile@, Payload@) { M@<Profile@>; }
  SampleTypeField = (Profile@ profile, Payload@ payload) {
    Int@ length <- m.do(LengthFor(payload, Str|'sample_type'));
    StringId@ type <- m.do(p.Message(0, SampleTypeFields, length));
    List@<StringId@> sample_types = Cons(type, profile.sample_types);
    m.return(profile.@(sample_types));
  };

  (Record@, Location@) { M@<Location@>; }
  LineFields = (Record@ record, Location@ location) {
    /Core/Int/Eq%.Eq(record.field, Int|1).?(true: {
      Int@ func_id <- m.do(IntFor(record.payload, Str|'Line.function_id'));
      m.return(location.@(func_id));
    });

    /Core/Int/Eq%.Eq(record.field, Int|2).?(true: {
      Int@ line <- m.do(IntFor(record.payload, Str|'Line.line'));
      m.return(location.@(line));
    });

    /Core/Int/Eq%.Eq(record.field, Int|3).?(true: {
      Int@ col <- m.do(IntFor(record.payload, Str|'Line.column'));
      m.return(location.@(col));
    });


    Unit@ _ <- m.do(p.Skip(record.payload));
    m.return(location);
  };

  (Record@, Location@) { M@<Location@>; }
  LocationFields = (Record@ record, Location@ location) {
    /Core/Int/Eq%.Eq(record.field, Int|1).?(true: {
      Int@ id <- m.do(IntFor(record.payload, Str|'Location.id'));
      m.return(location.@(id));
    });

    /Core/Int/Eq%.Eq(record.field, Int|4).?(true: {
      Int@ length <- m.do(LengthFor(record.payload, Str|'Location.line'));
      p.Message(location, LineFields, length);
    });

    Unit@ _ <- m.do(p.Skip(record.payload));
    m.return(location);
  };

  (Profile@, Payload@) { M@<Profile@>; }
  LocationField = (Profile@ profile, Payload@ payload) {
    Int@ length <- m.do(LengthFor(payload, Str|'location'));
    Location@ location <- m.do(p.Message(Location@(0, 0, 0, 0), LocationFields, length));
    Map.Map@<Location@> locations = Map.Insert(profile.locations, location.id, location);
    m.return(profile.@(locations));
  };

  (Record@, Function@) { M@<Function@>; }
  FunctionFields = (Record@ record, Function@ function) {
    /Core/Int/Eq%.Eq(record.field, Int|1).?(true: {
      Int@ id <- m.do(IntFor(record.payload, Str|'Function.id'));
      m.return(function.@(id));
    });

    /Core/Int/Eq%.Eq(record.field, Int|2).?(true: {
      Int@ name <- m.do(IntFor(record.payload, Str|'Function.name'));
      m.return(function.@(name));
    });

    /Core/Int/Eq%.Eq(record.field, Int|4).?(true: {
      Int@ file <- m.do(IntFor(record.payload, Str|'Function.filename'));
      m.return(function.@(file));
    });

    Unit@ _ <- m.do(p.Skip(record.payload));
    m.return(function);
  };

  (Profile@, Payload@) { M@<Profile@>; }
  FunctionField = (Profile@ profile, Payload@ payload) {
    Int@ length <- m.do(LengthFor(payload, Str|'function'));
    Function@ function <- m.do(p.Message(Function@(0, 0, 0), FunctionFields, length));
    Map.Map@<Function@> functions = Map.Insert(profile.functions, function.id, function);
    m.return(profile.@(functions));
  };

  (Profile@, Payload@) { M@<Profile@>; }
  StringField = (Profile@ profile, Payload@ payload) {
    Int@ length <- m.do(LengthFor(payload, Str|'string_table'));
    String@ str <- m.do(p.String(length));
    Map.Map@<String@> strings = Map.Insert(profile.strings,
      profile.next_string_id, str);
    Int@ next_string_id = Incr(profile.next_string_id);
    m.return(profile.@(strings, next_string_id));
  };

  (Record@, Profile@) { M@<Profile@>; }
  ProfileFields = (Record@ record, Profile@ profile) {
    FieldId@ field = record.field;
    Payload@ payload = record.payload;
    /Core/Int/Eq%.Eq(field, Int|1).?(true: SampleTypeField(profile, payload));
    #/Core/Int/Eq%.Eq(field, Int|2).?(true: SampleField(profile, payload));
    /Core/Int/Eq%.Eq(field, Int|4).?(true: LocationField(profile, payload));
    /Core/Int/Eq%.Eq(field, Int|5).?(true: FunctionField(profile, payload));
    /Core/Int/Eq%.Eq(field, Int|6).?(true: StringField(profile, payload));

    Unit@ _ <- m.do(p.Skip(payload));
    m.return(profile);
  };

  p.Proto(EmptyProfile, ProfileFields);
};

@(Profile@, StringId@, LocationId@, FunctionId@, Function@, Location@, Profile);
