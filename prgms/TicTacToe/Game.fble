
{ Bool%; @(Bool@, True, False); };
{ Maybe%; @(Maybe@); };
{ Process%; @(Get@, Put@); };
{ Unit%; @(Unit@, Unit); };

{ AI%; @(AI@, WithAI); };
{ Board%; @(Board@, Position@, Player@, BoardStatus, EmptyBoard, Move); };

@ GameStatus@ = +(Player@ Move, Player@ Win, Unit@ Draw); 
@ Output@ = *(Board@ board, GameStatus@ status);

# move: Make a move at the given position for the current player. If no
#       position is supplied, have the computer pick a reasonable move instead.
# reset: Reset the game.
# quit: Quit the game.
@ Input@ = +(Maybe@<Position@> move, Unit@ reset, Unit@ quit);

(Get@<Input@>, Put@<Output@>, AI@){Unit@!;}
NewGame = (Input@! input, Put@<Output@> output, AI@ ai) {
  PlayGame(input, output, ai, EmptyBoard, Player@(X: Unit));
},
(Input@!, Put@<Output@>, AI@, Board@, Player@){Unit@!;}
PlayGame = (Input@! input, Put@<Output@> output, AI@ ai, Board@ board, Player@ x) {
  GameStatus@ status := $(?(BoardStatus(board);
                          X: GameStatus@(Win: Player@(X: Unit)),
                          O: GameStatus@(Win: Player@(O: Unit)),
                          D: GameStatus@(Draw: Unit),
                          E: GameStatus@(Move: x)));
  Unit@ _ := output(Output@(board, status));
  Input@ in := input;
  ?(in;
     move:
       ?(status;
          Move: {
            Position@ position := ?(in.move;
              just: $(in.move.just),
              nothing: ai(board, x));

            Maybe@<Board@> nboard := $(Move(board, position, x));
            ?(nboard ;
                just: PlayGame(input, output, ai, nboard.just, ?(x; X: Player@(O: Unit), O: Player@(X: Unit))),
                nothing: PlayGame(input, output, ai, board, x));

          },
          : # It's a Win or a Draw, but the user tried to make a move.
            # Ignore the move and let the user pick again.
            PlayGame(input, output, ai, board, x)),
     reset: NewGame(input, output, ai),
     quit: $(Unit));
};

(Get@<Input@>, Put@<Output@>){Unit@!;} TicTacToe = (Get@<Input@> input, Put@<Output@> output) {
  AI@ ai <- WithAI<Unit@>;
  NewGame(input, output, ai);
};

@(GameStatus@, Input@, Output@, TicTacToe);
