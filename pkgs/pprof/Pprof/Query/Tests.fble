
<@>% AssertEquals = /Core/Eq/Assert%.AssertEquals;

@ Int@ = /Core/Int%.Int@;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Test@ = /Core/Test%.Test@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;

@ Unit@ = /Core/Unit%.Unit@;

@ BlockId@ = /Pprof/Profile%.BlockId@;
@ Block@ = /Pprof/Profile%.Block@;
@ Blocks@ = /Pprof/Profile%.Blocks@;
@ Profile@ = /Pprof/Profile%.Profile@;
% Blocks = /Pprof/Profile%.Blocks;

@ Table@ = /Pprof/Query%.Table@;

% Overall = /Pprof/Query/Overall%;
% Self = /Pprof/Query/Self%;
% Total = /Pprof/Query/Total%.Total;

@ Entry@ = *(BlockId@ id, Block@ block);

(BlockId@, String@, String@, Int@, Int@) { Entry@; }
B = (BlockId@ id, String@ name, String@ file, Int@ line, Int@ column) {
  Entry@(id, Block@(name, file, line, column));
};

(List@<Entry@>) { Blocks@; }
Inserts = (List@<Entry@> xs) {
  xs.?(nil: Blocks.Empty<Block@>);
  Entry@ entry = xs.cons.head;
  Blocks.Insert(Inserts(xs.cons.tail), entry.id, entry.block);
};

Profile@ Profile = @(
  metrics: List[Str|'calls', Str|'samples'],
  blocks: Inserts[
    B(Int|1, Str|'[root]', Str|'../lib/profile.c', Int|311, Int|0),
    B(Int|2, Str|'foo', Str|'Foo.fble', Int|10, Int|14),
    B(Int|3, Str|'bar', Str|'Bar.fble', Int|140, Int|2),
    B(Int|4, Str|'sludge', Str|'Sludge.fble', Int|1400, Int|3)],
  samples: List[
    @(sequence: List[Int|1], values: List[Int|1, Int|1]),
    @(sequence: List[Int|1, Int|2], values: List[Int|1, Int|15]),
    @(sequence: List[Int|1, Int|2, Int|3], values: List[Int|1, Int|7]),
    @(sequence: List[Int|1, Int|4, Int|3, Int|4], values: List[Int|1, Int|18]),
    @(sequence: List[Int|1, Int|2, Int|4], values: List[Int|1, Int|25])]);

Test@ Tests = TestSuite(Str|Query, List[
  Test(Str|Total, (Unit@ _) {
    Int@ got = Total(Profile);
    Int@ wnt = Int|66;
    AssertEquals(/Core/Int/Eq%.Eq, /Core/Int/Show%.Show, wnt, got);
  }),

  Test(Str|Self, (Unit@ _) {
    Table@ got = Self.Query(Profile);
    Table@ wnt = List[
      @(seq: List[Int|4], value: Int|43),
      @(seq: List[Int|2], value: Int|15),
      @(seq: List[Int|3], value: Int|7),
      @(seq: List[Int|1], value: Int|1)
    ];

    AssertEquals(
      /Pprof/Query/Eq%.Table,
      /Pprof/Query/Show%.Table,
      wnt, got);
  }),

  Test(Str|Overall, (Unit@ _) {
    Table@ got = Overall.Query(Profile);
    Table@ wnt = List[
      @(seq: List[Int|1], value: Int|66),
      @(seq: List[Int|2], value: Int|47),
      @(seq: List[Int|4], value: Int|43),
      @(seq: List[Int|3], value: Int|25)
    ];

    AssertEquals(
      /Pprof/Query/Eq%.Table,
      /Pprof/Query/Show%.Table,
      wnt, got);
  })
]);

@(Tests);
