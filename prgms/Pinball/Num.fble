
@ Bool@ = /Core/Bool%.Bool@;

@ Decimal@ = /Digits%.Decimal@;

@ Int@ = /Core/Int%.Int@;
% AddI = /Core/Int%.Add;
% SubI = /Core/Int%.Sub;
% NegI = /Core/Int%.Neg;
% AbsI = /Core/Int%.Abs;
% MulI = /Core/Int%.Mul;
% DivI = /Core/Int/Div%.Div;
% GtI = /Core/Int/Eq%.Gt;
% GeI = /Core/Int/Eq%.Ge;
% LtI = /Core/Int/Eq%.Lt;
% LeI = /Core/Int/Eq%.Le;
% EqI = /Core/Int/Eq%.Eq;
% IntI = /Core/Int/Lit%.Int;
% ShowI = /Core/Int/Show%.Show;
% SqrtI = /Core/Int/Sqrt%.Sqrt;

<@>@ List@ = /Core/List%.List@;

<@>@ Show@ = /Show%.Show@;

% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

# _@ -- Token type to prevent internal access to Num@ abstract type.
@? _@;

# Num@ -- 
#   A number with sufficient precision for the pinball game.
#
# The integer x is used to represent the number x/S.
@ Num@ = _@<Int@>;

# S -- 
#   Scale factor for converting Int@ to Num@.
Int@ S = IntI|128;

(Num@, Num@) { Num@; } Add = _@.<@<Add>>(AddI);
(Num@, Num@) { Num@; } Sub = _@.<@<Sub>>(SubI);
(Num@) { Num@; } Neg = _@.<@<Neg>>(NegI);
(Num@) { Num@; } Abs = _@.<@<Abs>>(AbsI);

(Num@, Num@) { Num@; } Mul = _@.<@<Mul>>((Int@ a, Int@ b) {
  DivI(MulI(a, b), S).q;
});

(Num@, Num@) { Num@; } Div = _@.<@<Div>>((Int@ a, Int@ b) {
  DivI(MulI(a, S), b).q;
});

(Num@, Num@) { Bool@; } Eq = _@.<@<Eq>>(EqI);
(Num@, Num@) { Bool@; } Gt = _@.<@<Gt>>(GtI);
(Num@, Num@) { Bool@; } Ge = _@.<@<Ge>>(GeI);
(Num@, Num@) { Bool@; } Lt = _@.<@<Lt>>(LtI);
(Num@, Num@) { Bool@; } Le = _@.<@<Le>>(LeI);

Show@<Num@> Show = _@.<@<Show>>((Int@ a) {
  Strs[ShowI(a), Str|'/', ShowI(S)];
});

# Int --
#   Integer literal.
(List@<Decimal@>) { Num@; } Int = (List@<Decimal@> ds) {
  _@.<Num@>(MulI(IntI(ds), S));
};

(Num@) { Int@; } Floor = _@.<@<Floor>>((Int@ a) {
  DivI(a, S).q;
});

(Num@) { Num@; } Sqrt = _@.<@<Sqrt>>((Int@ a) {
  SqrtI(MulI(a, S)).q;
});

@(Num@, Add, Sub, Neg, Abs, Mul, Div, Eq, Gt, Ge, Lt, Le, Show, Int, Floor, Sqrt);
