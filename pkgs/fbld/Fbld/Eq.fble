
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ Eq@ = /Core/Eq%.Eq@;

<@>@ List@ = /Core/List%.List@;

@ Command@ = /Fbld/Command%.Command@;
@ Loc@ = /Fbld/Loc%.Loc@;
<@>@ Result@ = /Fbld/Result%.Result@;

@ Text@ = /Fbld/Text%.Text@;
% IsEmpty = /Fbld/Text%.IsEmpty;
% Head = /Fbld/Text%.Head;
% Tail = /Fbld/Text%.Tail;
% LocOf = /Fbld/Text%.LocOf;

Eq@<Loc@> Loc = (Loc@ a, Loc@ b) {
  /Core/String/Eq%.Eq(a.file, b.file).?(false: False);
  /Core/Int/Eq%.Eq(a.line, b.line).?(false: False);
  /Core/Int/Eq%.Eq(a.col, b.col).?(false: False);
  True;
};

Eq@<Text@> Text = (Text@ a, Text@ b) {
  IsEmpty(a).?(true: IsEmpty(b));
  IsEmpty(b).?(true: IsEmpty(a));

  /Core/Char/Eq%.Eq(Head(a), Head(b)).?(false: False);
  Loc(LocOf(a), LocOf(b)).?(false: False);
  Text(Tail(a), Tail(b));
};

<@ T@>(Eq@<T@>) { Eq@<Result@<T@>>; }
Result = <@ T@>(Eq@<T@> eq)(Result@<T@> a, Result@<T@> b) {
  a.?(
    ok: b.?(ok: eq(a.ok, b.ok), err: False),
    err: b.?(ok: False, err: Text(a.err, b.err)));
};

Eq@<Command@> Command = (Command@ a, Command@ b) {
  Text(a.name, b.name).?(false: False);
  /Core/List/Eq%.Eq(Text)(a.args, b.args).?(false: False);
  True;
};

Eq@<List@<Command@>> Commands = /Core/List/Eq%.Eq(Command);

@(Loc, Text, Result, Command, Commands);
