
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ Char@ = /Core/Char%.Char@;

@ Int@ = /Core/Int%.Int@;
% Incr = /Core/Int%.Incr;
% Int = /Core/Int/Lit%.Int;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Loc@ = /Fbld/Loc%.Loc@;
% Advance = /Fbld/Loc%.Advance;
% Unknown = /Fbld/Loc%.Unknown;

<@>@ Result@ = /Fbld/Result%.Result@;
<@>% Ok = /Fbld/Result%.Ok;
<@>% Err = /Fbld/Result%.Err;

@ Text@ = /Fbld/Text%.Text@;
% Text = /Fbld/Text%.Text;

# S@ - State for parser monad.
@ S@ = *(List@<Text@> input, Int@ indent);

# State monad for parser monad.
<@>@ SM@ = <@ T@> { *(S@ s, T@ x); };

# M@ - A parser monad.
<@>@ M@ = <@ T@>(S@) { Result@<SM@<T@>>; };

# Return the given value.
<@ T@>(T@) { M@<T@>; }
Return = <@ T@>(T@ x)(S@ s) {
  Ok(SM@(s, x));
};

# Report a parse error.
<@ T@>(Loc@, String@) { M@<T@>; }
Error = <@ T@>(Loc@ l, String@ msg)(S@ _) {
  Err<SM@<T@>>(Text(l, msg));
};

# Monadic Do for the parser monad.
<@ A@>(M@<A@>)<@ B@>((A@) { M@<B@>; }) { M@<B@>; }
Do = <@ A@>(M@<A@> ma)<@ B@>((A@) { M@<B@>; } f)(S@ s) {
  Result@<SM@<A@>> ra = ma(s);
  ra.?(err: Err<SM@<B@>>(ra.err));
  f(ra.ok.x)(ra.ok.s);
};

# Returns the current location.
M@<Loc@> Loc = (S@ s) {
  Loc@ l = {
    s.input.?(nil: Unknown);
    s.input.cons.head.loc;
  };
  Ok(SM@(s, l));
};

# Gets the next character in the input stream, advancing the parser state as
# appropriate. Fails if there are no more characters in the input stream.
M@<Char@> Get = (S@ s) {
  s.input.?(nil: Err<SM@<Char@>>(Text(Unknown, Str|'unexpected end of input')));
  s.input.cons.head.text.?(nil: Get(s.@(input: s.input.cons.tail)));
  Char@ c = s.input.cons.head.text.cons.head;
  Text@ head = Text(Advance(s.input.cons.head.loc, c), s.input.cons.head.text.cons.tail);
  Ok(SM@(s.@(input: Cons(head, s.input.cons.tail)), c));
};

# Parse an indented block of text.
# Single space indent is used. The indent characters will be skipped over and
# end of input will be returned when the indented text ends.
<@ T@>(M@<T@>) { M@<T@>; } Indent = <@ T@>(M@<T@> mx)(S@ s) {
  mx(s.@(indent: Incr(s.indent)));
};
  
# Try running a parser. If it succeeds, return the parsed value. If it fails,
# return Nothing.
<@ T@>(M@<T@>) { M@<Maybe@<T@>>; }
Try = <@ T@>(M@<T@> mx)(S@ s) {
  Result@<SM@<T@>> r = mx(s);
  r.?(err: Ok(SM@(s, Nothing<T@>)));
  Ok(SM@(r.ok.s, Just(r.ok.x)));
};

# Test if a parser would match at the current position, without consuming any
# input.
<@ T@>(M@<T@>) { M@<Maybe@<T@>>; }
Test = <@ T@>(M@<T@> mx)(S@ s) {
  Result@<SM@<T@>> r = mx(s);
  r.?(err: Ok(SM@(s, Nothing<T@>)));
  Ok(SM@(s, Just(r.ok.x)));
};

# Runs a parser monad on the given input string.
<@ T@>(M@<T@>, List@<Text@>) { Result@<T@>; }
Run = <@ T@>(M@<T@> m, List@<Text@> input) {
  Result@<SM@<T@>> r = m(S@(input, Int|0));
  r.?(err: Err<T@>(r.err));
  Ok(r.ok.x);
};


# Parses either of the given arguments, returning the result of the first to
# pass. Fails if both parsers fails.
<@ T@>(M@<T@>, M@<T@>) { M@<T@>; }
Or = <@ T@>(M@<T@> ma, M@<T@> mb) {
  Maybe@<T@> a <- Do(Try(ma));
  a.?(just: Return(a.just));
  mb;
};

# Same as Try, but discards the result of a successful match.
<@ T@>(M@<T@>) { M@<Bool@>; }
Try_ = <@ T@>(M@<T@> mx) {
  Maybe@<T@> r <- Do(Try(mx));
  Return(r.?(just: True, nothing: False));
};

# Test if a parser would match at the current position, without consuming any
# input, and ignoring the result of parsing.
<@ T@>(M@<T@>) { M@<Bool@>; }
Test_ = <@ T@>(M@<T@> mx) {
  Maybe@<T@> r <- Do(Test(mx));
  Return(r.?(just: True, nothing: False));
};

# Parse end of input. Fails if we have not reached the end of the input.
M@<Unit@> EndOfInput = {
  Loc@ l <- Do(Loc);
  Maybe@<Char@> mc <- Do(Test(Get));
  mc.?(nothing: Return(Unit));
  Error<Unit@>(l, Str|'expected end of input');
};


# Parse a specific string from the input stream. Fails if the input string
# does not match that string.
(String@) { M@<Unit@>; }
String = (String@ s) {
  s.?(nil: Return(Unit));
  Loc@ loc <- Do(Loc);
  Char@ c <- Do(Get);
  /Core/Char/Eq%.Eq(s.cons.head, c).?(
    true: String(s.cons.tail),
    false: Error<Unit@>(loc, Strs[Str|'Expected ', Str[s.cons.head]]));
};

@(M@, Return, Error, Do, Loc, Get, EndOfInput, Indent,
  Try, Try_, Test, Test_, Or, String,
  Run);
