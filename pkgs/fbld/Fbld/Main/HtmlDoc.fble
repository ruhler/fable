
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;

% Report = /Fbld/Loc%.Report;
% Start = /Fbld/Loc%.Start;

<@>@ Result@ = /Fbld/Result%.Result@;

@ StringL@ = /Fbld/StringL%.StringL@;

@ Command@ = /Fbld/Types%.Command@;
<<@>@>@ Invoke@ = /Fbld/Types%.Invoke@;


# Main --
#   Main function for fbld-html-doc program.
#
# Converts an fbld @doc into html format.
Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);
  % Markup = /Fbld/Markup%(m);
  @ Markup@ = Markup.Markup@;

  % Core = /Fbld/Core%(m);
  @ Core@ = Core.Core@;

  Core@ core = /Fbld/Html%(m, stdio.out).Core;

  # TODO: Properly implement @FbleVersion, @Buildstamp
  Markup@ inline_markup =
    Markup.Insert(
      Markup.Insert(Core.InlineMarkup(core),
        Str|'FbleVersion', (Invoke@<M@> i, Invoke@<M@> b, Command@ c) {
          core.text(StringL@(Str|'??FbleVersion??', c.name.loc));
        }),
      Str|'BuildStamp', (Invoke@<M@> i, Invoke@<M@> b, Command@ c) {
        core.text(StringL@(Str|'??BuildStamp??', c.name.loc));
      });

  Markup@ block_markup = Core.BlockMarkup(core);

  Invoke@<M@> inline = Markup.Invoke(inline_markup);
  Invoke@<M@> block = Markup.Invoke(block_markup);
  % Invoke = /Fbld/Invoke%(m);

  (List@<String@> args) {
    args.?(nil: {
      String@ msg = Str|'missing input file';
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    String@ filename = args.cons.head;
    
    Maybe@<IStream@<M@>> fin <- m.do(stdio.read(filename));
    fin.?(nothing: {
      String@ msg = Strs[Str|'unable to open ', filename];
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    String@ contents <- m.do(I.GetText(fin.just));
    Result@<Unit@> r <- m.do(Invoke.Block(inline, block, StringL@(contents, Start(filename))));
    r.?(err: {
      String@ msg = Report(r.err.loc, r.err.str);
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    m.return(True);
  };
};

/Core/Stdio/IO%.Run(Main);
