
% BitStr = /Digits/String%.BitStr;

% AddM = /Fblf/Lib/Add%.AddM;
% IncM = /Fblf/Lib/Add%.IncM;
% DecM = /Fblf/Lib/Add%.DecM;

<@>% AllocM = /Fblf/Module%.AllocM;
<@>% PureM = /Fblf/Module%.PureM;
<@,@>% InstM = /Fblf/Module%.InstM;

@ Addr@ = /Fblf/Program%.Addr@;
@ Program@ = /Fblf/Program%.Program@;
% Assign = /Fblf/Program%.Assign;
% Const = /Fblf/Program%.Const;
% Seq = /Fblf/Program%.Seq;

@ Test@ = /Fblf/Test%.Test@;
@ TestInstance@ = /Fblf/Test%.TestInstance@;
% Test = /Fblf/Test%.Test;
% TestSuite = /Fblf/Test%.TestSuite;

% Int = /Int/Int/Lit%.Int;

<@>% List = /List%.List;

% Str = /String%.Str;

Test@ Tests = TestSuite(Str|'Add', List<Test@>[
  Test(Str|AddM, {
    Addr@ a <- AllocM<TestInstance@>(Int|5);
    Addr@ b <- AllocM<TestInstance@>(Int|5);
    Addr@ z <- AllocM<TestInstance@>(Int|5);

    Program@ add <- InstM<Program@, TestInstance@>(AddM(Int|4, a, b, z));

    # Note: we put an extra '1' bit after each value as a regression test for
    # a bug we had where it was reading one bit off the end of the values.
    Program@ test = Seq(List<Program@>[
      Assign(a, Const(BitStr|00101), Int|5),
      Assign(b, Const(BitStr|10111), Int|5),
      Assign(z, Const(BitStr|11111), Int|5),
      add
    ]);

    PureM<TestInstance@>(TestInstance@(test, a, BitStr|001011011111011));
  }),

  Test(Str|IncM, {
    Addr@ a <- AllocM<TestInstance@>(Int|5);
    Program@ inc <- InstM<Program@, TestInstance@>(IncM(Int|5, a));
    Program@ test = Seq(List<Program@>[Assign(a, Const(BitStr|10011), Int|5), inc]);
    PureM<TestInstance@>(TestInstance@(test, a, BitStr|10100));
  }),

  Test(Str|'IncM Overflow', {
    Addr@ a <- AllocM<TestInstance@>(Int|5);
    Program@ inc <- InstM<Program@, TestInstance@>(IncM(Int|5, a));
    Program@ test = Seq(List<Program@>[Assign(a, Const(BitStr|11111), Int|5), inc]);
    PureM<TestInstance@>(TestInstance@(test, a, BitStr|00000));
  }),

  Test(Str|DecM, {
    Addr@ a <- AllocM<TestInstance@>(Int|5);
    Program@ dec <- InstM<Program@, TestInstance@>(DecM(Int|5, a));
    Program@ test = Seq(List<Program@>[Assign(a, Const(BitStr|10100), Int|5), dec]);
    PureM<TestInstance@>(TestInstance@(test, a, BitStr|10011));
  })
]);

@(Tests);
