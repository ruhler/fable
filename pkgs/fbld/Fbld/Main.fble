
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

% Report = /Fbld/Loc%.Report;
% Start = /Fbld/Loc%.Start;

<@>@ Result@ = /Fbld/Result%.Result@;
@ StringL@ = /Fbld/StringL%.StringL@;


# Main --
#   Main function for fbld program.
Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);

  @ Env@ = /Fbld/Invoke%<M@>.Env@;

  % Markup = /Fbld/Markup%(m);
  @ Markup@ = Markup.Markup@;
  % Markups = Markup.Markups;

  % Core = /Fbld/Core%(m);
  @ Core@ = Core.Core@;

  % Usage = /Fbld/Usage%(m);

  % Run = /Fbld/Run%(m);

  @ Backend@ = +(Unit@ html, Unit@ markdown, Unit@ man, Unit@ text);

  @ Args@ = *(
    String@ buildstamp,
    String@ version,
    Backend@ backend,
    Bool@ fble,
    Bool@ tutorial,
    Bool@ usage_help,
    Bool@ usage_man,
    Bool@ usage_lib,
    Maybe@<String@> file
  );

  (List@<String@>) { Args@; } ParseArgs = (List@<String@> args) {
    (List@<String@>, Args@) { Args@; }
    parse = (List@<String@> args, Args@ result) {
      args.?(nil: result);
      String@ arg = args.cons.head;
      /Core/String/Eq%.Eq(Str|'--buildstamp', arg).?(true: {
        String@ buildstamp = args.cons.tail.cons.head;
        List@<String@> nargs = args.cons.tail.cons.tail;
        parse(nargs, result.@(buildstamp));
      });
      /Core/String/Eq%.Eq(Str|'--version', arg).?(true: {
        String@ version = args.cons.tail.cons.head;
        List@<String@> nargs = args.cons.tail.cons.tail;
        parse(nargs, result.@(version));
      });
      /Core/String/Eq%.Eq(Str|'--html', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(html: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--markdown', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(markdown: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--man', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(man: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--text', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(text: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--fble', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(fble: True));
      });
      /Core/String/Eq%.Eq(Str|'--tutorial', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(tutorial: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.help', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_help: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.man', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_man: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.lib', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_lib: True));
      });
      parse(args.cons.tail, result.@(file: Just(arg)));
    };
    parse(args, Args@(Str|'??', Str|'??',
        Backend@(html: Unit), False, False, False, False, False, Nothing<String@>));
  };

  (List@<String@> args) {
    Args@ parsed = ParseArgs(args);

    % Man = /Fbld/Man%(m, stdio.out);
    Core@ core = parsed.backend.?(
      html: /Fbld/Html%(m, stdio.out).Core,
      markdown: /Fbld/Markdown%(m, stdio.out).Core,
      man: Man.Core,
      text: /Fbld/Text%(m, stdio.out).Core);

    parsed.file.?(nothing: {
      String@ msg = Str|'missing input file';
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    String@ filename = parsed.file.just;
    
    Maybe@<IStream@<M@>> fin <- m.do(stdio.read(filename));
    fin.?(nothing: {
      String@ msg = Strs[Str|'unable to open ', filename];
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    % Build = /Fbld/Fble/Build%(m)(core, parsed.buildstamp);
    % Version = /Fbld/Fble/Version%(m)(core, parsed.version);

    Markup@ markup = Markups[
      Core.Markup(core),
      Build.Markup,
      Version.Markup,
      parsed.fble.?(
        true: /Fbld/Fble/Fble%(m)(core).Markup,
        false: Markup.Empty),
      parsed.tutorial.?(
        true: /Fbld/Fble/Tutorial%(m)(core, Build.BuildStamp, Version.Version).Markup,
        false: Markup.Empty),
      parsed.usage_help.?(
        true: Usage.Markup(/Fbld/Usage/Help%(m)(core).Usage),
        false: Markup.Empty),
      parsed.usage_lib.?(
        true: /Fbld/Usage/Lib%(m).Markup,
        false: Markup.Empty)
    ];

    Env@ env = Markup.Env(markup);

    String@ contents <- m.do(I.GetText(fin.just));
    Result@<Unit@> r <- m.do(Run.Block(env, StringL@(contents, Start(filename))));
    r.?(err: {
      String@ msg = Report(r.err.loc, r.err.str);
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    m.return(True);
  };
};

/Core/Stdio/IO%.Run(Main);
