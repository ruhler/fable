
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
% Not = /Core/Bool%.Not;

% Incr = /Core/Int%.Incr;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Nil = /Core/List%.Nil;
<@>% Cons = /Core/List%.Cons;
<@>% Append = /Core/List%.Append;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<@>@ Show@ = /Core/Show%.Show@;

<<@>@>@ OStream@ = /Core/Stream%.OStream@;

@ String@ = /Core/String%.String@;
% Strs = /Core/String%.Strs;
% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

<<@>@>@ Debug@ = /Sat/Debug%.Debug@;

@ Id@ = /Sat/Id%.Id@;
% EqId = /Sat/Id%.EqId;

@ VarId@ = Id@;
@ ClauseId@ = Id@;

@ Var@ = *(VarId@ id, Bool@ pos);
@ Clause@ = List@<Var@>;

# Imagine two clauses:
#   (a1 + a2 + ... + v)
#   (b1 + b2 + ... + v')
#
# These conflict in the case where:
#   (a1 + a2 + ... + b1 + b2 + ...)
#
# Conflict@ describes a pair of conflicting clauses and the variable they
# conflicted on.
@ Conflict@ = *(ClauseId@ a, ClauseId@ b, VarId@ var);

% ClauseIds = /Sat/Id%.Set;
@ ClauseIds@ = ClauseIds.Set@;

# pos - the set of clauses this variable appears positive in.
# neg - the set of clauses this variable appears negative in.
@ VarS@ = *(ClauseIds@ pos, ClauseIds@ neg);

% Vars = /Sat/Id%.Map<VarS@>;
@ Vars@ = Vars.Map@;

% Clauses = /Sat/Id%.Map<Clause@>;
@ Clauses@ = Clauses.Map@; 

% Conflicts = /Sat/Id%.Map<Conflict@>;
@ Conflicts@ = Conflicts.Map@;


# Functions for working with clauses.
# TODO: Move to a separate module?
% Clause = {
  # Adds a variable to a clause, preserving the invariant that each variable
  # appears at most once in a clause.
  #
  # Returns Nothing if the clause contains both x and x' after the insertion.
  (Var@, Clause@) { Maybe@<Clause@>; } AddVar = (Var@ v, Clause@ c) {
    c.?(nil: Just(List[v]));
    Var@ head = c.cons.head;
    EqId(v.id, head.id).?(true: {
      /Core/Bool/Eq%.Eq(v.pos, head.pos).?(true: Just(c));
      Nothing<Clause@>;
    });
    Maybe@<Clause@> tail = AddVar(v, c.cons.tail);
    tail.?(nothing: Nothing<Clause@>);
    Just(Cons(head, tail.just));
  };

  # Adds a list of variables to a clause, preserving the invariant that each
  # variable appears at most once in a clause.
  #
  # Returns Nothing if the clause contains both x and x' after the insertion.
  (List@<Var@>, Clause@) { Maybe@<Clause@>; }
  AddVars = (List@<Var@> vs, Clause@ c) {
    vs.?(nil: Just(c));
    Maybe@<Clause@> mc = AddVar(vs.cons.head, c);
    mc.?(nothing: Nothing<Clause@>);
    AddVars(vs.cons.tail, mc.just);
  };

  # Removes a variable from a clause.
  (VarId@, Clause@) { Clause@; } RemoveVar = (VarId@ vid, Clause@ c) {
    /Core/List%.ForEach(c, Nil<Var@>, (Var@ v, List@<Var@> vars) {
      EqId(vid, v.id).?(true: vars);
      Cons(v, vars);
    });
  };

  @(AddVar, AddVars, RemoveVar);
};

# The reasoning behind a variable assignment.
@ Why@ = +(
  Unit@ pure,         # The variable is a pure literal, so no harm setting it.
  ClauseId@ inferred  # The value was inferred from the given clause.
);

Why@ Pure = Why@(pure: Unit);
(ClauseId@) { Why@; } Inferred = (ClauseId@ id) { Why@(inferred: id); };

@ Assign@ = *(Var@ var, Why@ why);

@ Solver@ = *(
  Bool@ unsat,              # if true, the formula is trivially unsatisfiable.
  ClauseId@ clause_id,
  Vars@ vars,
  Clauses@ clauses,
  Conflicts@ conflicts,
  List@<Var@> assigns,        # Assignments made so far.
  List@<Assign@> todo         # Assignments we should make.
);

Solver@ NewSolver = @(
  unsat: False,
  clause_id: Int|0,
  vars: Vars.Empty,
  clauses: Clauses.Empty,
  conflicts: Conflicts.Empty,
  assigns: Nil<Var@>,
  todo: Nil<Assign@>
);

# Adds a new entry to the list of todos.
(Var@, Why@, List@<Assign@>) { List@<Assign@>; }
Todo = (Var@ v, Why@ w, List@<Assign@> s) {
  Cons(Assign@(v, w), s);
};

# Add a new clause with the given clause id.
(Solver@, ClauseId@, List@<Var@>) { Solver@; }
AddClauseAt = (Solver@ s, ClauseId@ id, List@<Var@> vars) {
  Maybe@<Clause@> mc = Clause.AddVars(vars, Nil<Var@>);

  # Ignore trivial clauses.
  mc.?(nothing: s);
  Clause@ clause = mc.just;

  # Check for empty clause.
  clause.?(nil: {
    s.@(unsat: True, clauses: Clauses.Insert(s.clauses, id, Nil<Var@>));
  });

  List@<Assign@> todo = {
    # Check for unit clause.
    clause.cons.tail.?(nil: Todo(clause.cons.head, Inferred(id), s.todo));
    s.todo;
  };

  # Update Vars@ data structure.
  Vars@ vars = /Core/List%.ForEach(clause, s.vars,
    (Var@ v, Vars@ vs) {
      Vars.InsertWith(vs, v.id, (Maybe@<VarS@> mx) {
          VarS@ x = mx.?(
            just: mx.just,
            nothing: VarS@(ClauseIds.Empty, ClauseIds.Empty));
          v.pos.?(
            true: VarS@(ClauseIds.Insert(x.pos, id), x.neg),
            false: VarS@(x.pos, ClauseIds.Insert(x.neg, id)));
        });
    });

  Clauses@ clauses = Clauses.Insert(s.clauses, id, clause);
  s.@(vars, clauses, todo);
};

# @func[AddClause] Adds a clause to cnf state Solver@.
#  This should be done in the initial phase before starting to solve the
#  formula.
#
#  @arg[s] The cnf state to add the clause to.
#  @arg[clause] The clause to add.
#  @returns
#   An updated cnf state with clause added.
(Solver@, List@<Var@>) { Solver@; } AddClause = (Solver@ s, List@<Var@> vars) {
  ClauseId@ id = s.clause_id;
  AddClauseAt(s.@(clause_id: Incr(id)), id, vars);
};

(Solver@, List@<Clause@>) { Solver@; }
AddClauses = (Solver@ solver, List@<Clause@> clauses) {
  /Core/List%.ForEach(clauses, solver, (Clause@ c, Solver@ s) {
    AddClause(s, c);
  });
};

# Adds a clause to the solver state for the given conflict.
(ClauseId@, Conflict@, Solver@) { Solver@; }
AddConflictClause = (ClauseId@ id, Conflict@ conflict, Solver@ s) {
  Maybe@<Clause@> ma = Clauses.Lookup(s.clauses, conflict.a);
  Clause@ a = Clause.RemoveVar(conflict.var, {
    ma.?(nothing: Nil<Var@>);
    ma.just;
  });

  Maybe@<Clause@> mb = Clauses.Lookup(s.clauses, conflict.b);
  Clause@ b = Clause.RemoveVar(conflict.var, {
    mb.?(nothing: Nil<Var@>);
    mb.just;
  });

  AddClauseAt(s, id, Append(a, b));
};

# Removes a clause from a Vars@ var entry.
(ClauseId@)(Var@, Solver@) { Solver@; }
RemoveClauseFromVar = (ClauseId@ clause)(Var@ var, Solver@ s) {
  Maybe@<VarS@> mvs = Vars.Lookup(s.vars, var.id);
  mvs.?(nothing: s);
  VarS@ vs = mvs.just;

  ClauseIds@ match = var.pos.?(true: vs.pos, false: vs.neg);
  ClauseIds@ ncls = ClauseIds.Delete(match, clause);

  List@<Assign@> todo = {
    ClauseIds.IsEmpty(ncls).?(false: s.todo);
    Todo(Var@(var.id, Not(var.pos)), Pure, s.todo);
  };

  VarS@ nvs = var.pos.?(
    true: @(pos: ncls, neg: vs.neg),
    false: @(pos: vs.pos, neg: ncls));

  s.@(vars: Vars.Insert(s.vars, var.id, nvs), todo);
};

# Removes a clause from the solver.
(ClauseId@, Solver@) { Solver@; }
RemoveClause = (ClauseId@ id, Solver@ s) {
  Maybe@<Clause@> mcs = Clauses.Lookup(s.clauses, id);
  mcs.?(nothing: s);

  Clause@ cs = mcs.just;
  Solver@ ns = /Core/List%.ForEach(cs, s, RemoveClauseFromVar(id));
  ns.@(clauses: Clauses.Delete(ns.clauses, id));
};

# Caller is responsible for ensuring no conflict arises as a result of
# removing the variable.
(VarId@)(ClauseId@, Solver@) { Solver@; }
RemoveVarFromClause = (VarId@ var)(ClauseId@ clause, Solver@ s) {
  Maybe@<Clause@> mcs = Clauses.Lookup(s.clauses, clause);
  mcs.?(nothing: s);

  List@<Var@> nvars = Clause.RemoveVar(var, mcs.just);

  # Assertion: nvars must not be empty, because caller ensures we won't
  # encounter a conflict.
  List@<Assign@> todo = {
    nvars.cons.tail.?(cons: s.todo);
    Todo(nvars.cons.head, Inferred(clause), s.todo);
  };

  s.@(clauses: Clauses.Insert(s.clauses, clause, nvars), todo);
};

# Assign a value to a variable.
# It is the callers responsibility to ensure no conflict is encountered during
# assignment.
(Var@, Solver@) { Solver@; } Assign = (Var@ var, Solver@ s) {
  Maybe@<VarS@> mvs = Vars.Lookup(s.vars, var.id);

  # If the variable doesn't exist, we must have already made an assignment for
  # it. Ignore this assignment attempt.
  mvs.?(nothing: s);
  VarS@ vs = mvs.just;

  Solver@ ns = s.@(
    vars: Vars.Delete(s.vars, var.id),
    assigns: Cons(var, s.assigns));

  ClauseIds@ match = var.pos.?(true: vs.pos, false: vs.neg);
  Solver@ match_s = ClauseIds.ForEach(match, ns, RemoveClause);

  ClauseIds@ unmatch = var.pos.?(true: vs.neg, false: vs.pos);
  ClauseIds.ForEach(unmatch, match_s, RemoveVarFromClause(var.id));
};

@ Assignment@ = List@<Var@>;

@ SearchResult@ = +(
  Assignment@ sat,
  *(ClauseId@ clause_id, Conflicts@ conflicts) unsat
);

# Result@ --
#   The result of trying to solve a sat problem.
@ Result@ = +(Assignment@ sat, Unit@ unsat);

% Show = {
  Show@<Id@> Id = /Core/Int/Show%.Show;

  Show@<Var@> Var = (Var@ v) {
    String@ sign = v.pos.?(true: Str|'+', false: Str|'-');
    Append(sign, Id(v.id));
  };

  Show@<Clause@> Clause = /Core/List/Show%.Show(Var);

  Show@<Why@> Why = (Why@ w) {
    w.?(pure: Str|'pure', inferred: Strs[Str|'inferred from ', Id(w.inferred)]);
  };

  Show@<Assign@> Assign = (Assign@ op) {
    Strs[Str|'Assign ', Var(op.var), Str|' (', Why(op.why), Str|')'];
  };

  Show@<List@<Assign@>> Todo = /Core/List/Show%.Show(Assign);

  Show@<Conflict@> Conflict = (Conflict@ c) {
    Strs[Str|'Conflict ', Id(c.a), Str|', ', Id(c.b), Str|' on ', Id(c.var)];
  };


  @(Id, Var, Clause, Why, Assign, Todo, Conflict,
    Clauses: Clauses.Show(Clause));
};

(Solver@, Solver@) { Solver@; }
ApplyLearnedClauses = (Solver@ to, Solver@ from) {
  Solver@ s = to.@(clause_id: from.clause_id, conflicts: from.conflicts);
  /Core/Iter%.For(
      /Core/Int/Iter%.Range(to.clause_id, from.clause_id), s,
      (Id@ i, Solver@ s) {
        Maybe@<Conflict@> conflict = Conflicts.Lookup(s.conflicts, i);
        AddConflictClause(i, conflict.just, s);
      });
};

(Assign@, Assign@) { Bool@; }
AssignsAreConflicting = (Assign@ a, Assign@ b) {
  EqId(a.var.id, b.var.id).?(false: False);
  /Core/Bool/Eq%.Eq(a.var.pos, b.var.pos).?(true: False);

  # Don't consider 'pure' literals as conflicting to work around the case
  # where a variable is entirely eliminated, becoming simultaneously pure
  # positive and pure negative.
  a.why.?(pure: False);

  True;
};

# Find and return a conflicting assignment in a todo list sorted by variable
# id.
(List@<Assign@>) { Maybe@<Conflict@>; }
GetConflictFromSorted = (List@<Assign@> todo)
{
  todo.?(nil: Nothing<Conflict@>);
  todo.cons.tail.?(nil: Nothing<Conflict@>);
  Assign@ a = todo.cons.head;
  Assign@ b = todo.cons.tail.cons.head;
  AssignsAreConflicting(a, b).?(true: {
    Just(Conflict@(a.why.inferred, b.why.inferred, a.var.id));
  });

  GetConflictFromSorted(todo.cons.tail);
};

# Find and return a conflicting assignment in the todo list, if any.
(List@<Assign@>) { Maybe@<Conflict@>; }
GetConflict = (List@<Assign@> todo)
{
  GetConflictFromSorted(/Core/List/Sort%.Sort(
      (Assign@ a, Assign@ b) { /Core/Int/Eq%.Le(a.var.id, b.var.id); },
      todo));
};

<<@>@ M@>(Monad@<M@>, Debug@<M@>)(Solver@) { M@<Result@>; }
Solve = <<@>@ M@>(Monad@<M@> m, Debug@<M@> debug) {
  % O = /Core/Stream/OStream%(m);

  # Process todos until no more remain or unsat.
  (Solver@) { M@<Solver@>; } Simplify = (Solver@ s) {
    Unit@ _ <- m.do(debug(Int|4, (OStream@<M@> dbg) {
      O.PutLine(dbg, Strs[Str|'Simplify: ', Show.Clauses(s.clauses)]);
    }));

    s.unsat.?(true: m.return(s));
    s.todo.?(nil: m.return(s));

    # Check for conflicts in the todo list.
    Unit@ _ <- m.do(debug(Int|1, (OStream@<M@> dbg) {
      O.PutLine(dbg, Strs[Str|'Todo: ', Show.Todo(s.todo)]);
    }));

    Maybe@<Conflict@> conflict = GetConflict(s.todo);
    conflict.?(just: {
      # Add the conflict and note that we are done here.
      ClauseId@ id = s.clause_id;
      Unit@ _ <- m.do(debug(Int|3, (OStream@<M@> dbg) {
        O.PutLine(dbg, Strs[Show.Id(id), Str|': ', Show.Conflict(conflict.just)]);
      }));
      Solver@ rs = s.@(
        unsat: True,
        clause_id: Incr(id),
        conflicts: Conflicts.Insert(s.conflicts, id, conflict.just));
      m.return(rs);
    });

    # Apply the next assignment in the todo list.
    Assign@ assign = s.todo.cons.head;
    Unit@ _ <- m.do(debug(Int|3, (OStream@<M@> dbg) {
      O.PutLine(dbg, Show.Assign(assign));
    }));
    Solver@ ns = s.@(todo: s.todo.cons.tail);
    Solver@ result = Assign(assign.var, ns);

    # If the assignment succeeded, but further simplification failed, apply
    # what we learned to before the assignment and simplify again.
    Solver@ ss <- m.do(Simplify(result));
    ss.unsat.?(false: m.return(ss));
    Simplify(ApplyLearnedClauses(s, ss));
  };

  # Pick some variable that exists in the given vars map.
  (Vars@) { Maybe@<VarId@>; } PickSomeVar = (Vars@ vars) {
    # TODO: Come up with a more efficient way to pick any single existing
    # variable.
    Vars.ForEach(vars, Nothing<VarId@>,
      (VarId@ v, VarS@ _, Maybe@<VarId@> _mv) {
        Just(v);
      });
  };

  (Solver@) { SearchResult@; } Unsat = (Solver@ s) {
    SearchResult@(unsat: @(clause_id: s.clause_id, conflicts: s.conflicts));
  };

  # Sat solver with conflict driven clause learning.
  (Solver@) { M@<SearchResult@>; } Search = (Solver@ s) {
    Unit@ _ <- m.do(debug(Int|4, (OStream@<M@> dbg) {
      O.PutLine(dbg, Strs[Str|'Search: ', Show.Clauses(s.clauses)]);
    }));

    Solver@ ss <- m.do(Simplify(s));
    Solver@ s = ss;
    s.unsat.?(true: m.return(Unsat(s)));

    Maybe@<VarId@> mvar = PickSomeVar(s.vars);
    mvar.?(nothing: m.return(SearchResult@(sat: s.assigns)));
    VarId@ var = mvar.just;

    Var@ vattempt = Var@(var, True);
    Unit@ _ <- m.do(debug(Int|4, (OStream@<M@> dbg) {
      O.PutLine(dbg, Strs[Str|'Try: ', Show.Var(vattempt)]);
    }));
    Solver@ attempt = Assign(vattempt, s);
      
    SearchResult@ attempted <- m.do(Search(attempt));
    attempted.?(unsat: {
      # Add conflict clauses for all conflicts encountered in our attempted
      # search
      Solver@ ns = s.@(
        clause_id: attempted.unsat.clause_id,
        conflicts: attempted.unsat.conflicts);
      Search(ApplyLearnedClauses(s, ns));
    });

    m.return(attempted);
  };

  (Solver@ s) {
    SearchResult@ result <- m.do(Search(s));
    m.return(result.?(
      sat: Result@(sat: result.sat),
      unsat: Result@(unsat: Unit)));
  };
};

@(VarId@, Var@, Clause@,
  Solver@, Assignment@, Result@,
  EqId, NewSolver, AddClause, AddClauses, Solve);
