
<<@>@>@ Monad@ = /Core/Monad%.Monad@;

@ Int@ = /Core/Int%.Int@;
% Int = /Core/Int/Lit%.Int;

<@>% Length = /Core/List/Length%.Length;

% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;

@ Command@ = /Fbld/Command%.Command@;
@ Loc@ = /Fbld/Loc%.Loc@;
<@>% Err = /Fbld/Result%.Err;
@ Text@ = /Fbld/Text%.Text@;

(Text@) { Text@; } Unescape = (Text@ s) {
  Text@(/Fbld/Unescape%.Unescape(s.str), s.loc);
};

<<@>@ M@>(Monad@<M@> m) {
  % Run = /Fbld/Run%(m);

  @ MRU@ = M@</Fbld/Result%.Result@<Unit@>>;
  @ Invoke@ = /Fbld/Invoke%<M@>.Invoke@;
  @ Env@ = /Fbld/Invoke%<M@>.Env@;

  # @<tag>
  # Creates an Invoke@ function expecting zero arguments.
  ((Env@, Loc@) { MRU@; }) { Invoke@; }
  A0 = ((Env@, Loc@) { MRU@; } f) {
    (Env@ env, Command@ command) {
      Int@ num_args = Length(command.args);
      /Core/Int/Eq%.Eq(Int|0, num_args ).?(false: {
        m.return(Err<Unit@>(Text@(Strs[
              Str|'expected 0 args, but got ',
              /Core/Int/Show%.Show(num_args)], command.name.loc)));
      });

      f(env, command.name.loc);
    };
  };

  # @<tag>[*]
  # Creates an Invoke@ function expecting one argument.
  ((Env@, Loc@, Text@) { MRU@; }) { Invoke@; }
  A1 = ((Env@, Loc@, Text@) { MRU@; } f) {
    (Env@ env, Command@ command) {
      Int@ num_args = Length(command.args);
      /Core/Int/Eq%.Eq(Int|1, num_args ).?(false: {
        m.return(Err<Unit@>(Text@(Strs[
              Str|'expected 1 arg, but got ',
              /Core/Int/Show%.Show(num_args)], command.name.loc)));
      });

      Text@ arg = command.args.cons.head;
      f(env, command.name.loc, arg);
    };
  };

  # @<tag>[*][*]
  # Creates an Invoke@ function expecting two arguments.
  ((Env@, Loc@, Text@, Text@) { MRU@; }) { Invoke@; }
  A2 = ((Env@, Loc@, Text@, Text@) { MRU@; } f) {
    (Env@ env, Command@ command) {
      Int@ num_args = Length(command.args);
      /Core/Int/Eq%.Eq(Int|2, num_args ).?(false: {
        m.return(Err<Unit@>(Text@(Strs[
              Str|'expected 2 args, but got ',
              /Core/Int/Show%.Show(num_args)], command.name.loc)));
      });

      Text@ a1 = command.args.cons.head;
      Text@ a2 = command.args.cons.tail.cons.head;
      f(env, command.name.loc, a1, a2);
    };
  };

  # @<tag>[*][*][*]
  # Creates an Invoke@ function expecting three arguments.
  ((Env@, Loc@, Text@, Text@, Text@) { MRU@; }) { Invoke@; }
  A3 = ((Env@, Loc@, Text@, Text@, Text@) { MRU@; } f) {
    (Env@ env, Command@ command) {
      Int@ num_args = Length(command.args);
      /Core/Int/Eq%.Eq(Int|3, num_args ).?(false: {
        m.return(Err<Unit@>(Text@(Strs[
              Str|'expected 3 args, but got ',
              /Core/Int/Show%.Show(num_args)], command.name.loc)));
      });

      Text@ a1 = command.args.cons.head;
      Text@ a2 = command.args.cons.tail.cons.head;
      Text@ a3 = command.args.cons.tail.cons.tail.cons.head;
      f(env, command.name.loc, a1, a2, a3);
    };
  };

  # @<tag>[ESCAPED]
  # Creates an Invoke@ function expecting one ESCAPED argument.
  ((Text@) { MRU@; }) { Invoke@; } E = ((Text@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ arg <- A1;
    f(Unescape(arg));
  };

  # @<tag>[INLINE]
  # Creates an Invoke@ function expecting one INLINE argument.
  ((Loc@, MRU@) { MRU@; }) { Invoke@; } LI = ((Loc@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ arg <- A1;
    f(loc, Run.Inline(env, arg));
  };

  # @<tag>[INLINE]
  # Creates an Invoke@ function expecting one INLINE argument.
  ((MRU@) { MRU@; }) { Invoke@; } I = ((MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ arg <- A1;
    f(Run.Inline(env, arg));
  };

  # @<tag>[BLOCK] helper function
  # Creates an Invoke@ function expecting one BLOCK argument.
  ((MRU@) { MRU@; }) { Invoke@; } B = ((MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ arg <- A1;
    f(Run.Block(env, arg));
  };

  # @<tag>[BLOCK] helper function
  # Creates an Invoke@ function expecting one BLOCK argument.
  ((Loc@, MRU@) { MRU@; }) { Invoke@; } LB = ((Loc@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ arg <- A1;
    f(loc, Run.Block(env, arg));
  };

  # @<tag>[ESCAPED][ESCAPED] helper function
  # Creates an Invoke@ function expecting two ESCAPED arguments.
  ((Text@, Text@) { MRU@; }) { Invoke@; } EE = ((Text@, Text@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(Unescape(a1), Unescape(a2));
  };

  # @<tag>[ESCAPED][BLOCK] helper function
  ((Loc@, Text@, MRU@) { MRU@; }) { Invoke@; }
  LEB = ((Loc@, Text@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(loc, Unescape(a1), Run.Block(env, a2));
  };

  # @<tag>[ESCAPED][INLINE] helper function
  ((Text@, MRU@) { MRU@; }) { Invoke@; } EI = ((Text@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(Unescape(a1), Run.Inline(env, a2));
  };

  # @<tag>[INLINE][INLINE] helper function
  ((MRU@, MRU@) { MRU@; }) { Invoke@; } II = ((MRU@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(Run.Inline(env, a1), Run.Inline(env, a2));
  };

  # @<tag>[INLINE][INLINE] helper function
  ((Loc@, MRU@, MRU@) { MRU@; }) { Invoke@; }
  LII = ((Loc@, MRU@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(loc, Run.Inline(env, a1), Run.Inline(env, a2));
  };

  # @<tag>[INLINE][BLOCK] helper function
  ((MRU@, MRU@) { MRU@; }) { Invoke@; } IB = ((MRU@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(Run.Inline(env, a1), Run.Block(env, a2));
  };

  # @<tag>[INLINE][BLOCK] helper function
  ((Loc@, MRU@, MRU@) { MRU@; }) { Invoke@; } LIB = ((Loc@, MRU@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2 <- A2;
    f(loc, Run.Inline(env, a1), Run.Block(env, a2));
  };

  # @<tag>[INLINE][INLINE][BLOCK] helper function
  ((Loc@, MRU@, MRU@, MRU@) { MRU@; }) { Invoke@; }
  LIIB = ((Loc@, MRU@, MRU@, MRU@) { MRU@; } f) {
    Env@ env, Loc@ loc, Text@ a1, Text@ a2, Text@ a3 <- A3;
    f(loc, Run.Inline(env, a1), Run.Inline(env, a2), Run.Block(env, a3));
  };

  # @<tag>[ESCAPED][? INLINE] helper function, where the optional argument
  # defaults to the first argument value.
  ((Text@, MRU@) { MRU@; }) { Invoke@; }
  EmI = ((Text@, MRU@) { MRU@; } f) {
    (Env@ env, Command@ command) {
      Int@ num_args = Length(command.args);
      /Core/Int/Eq%.Eq(Int|1, num_args).?(true: {
        Text@ a1 = command.args.cons.head;
        f(Unescape(a1), Run.Inline(env, a1));
      });

      /Core/Int/Eq%.Eq(Int|2, num_args).?(true: {
        Text@ a1 = command.args.cons.head;
        Text@ a2 = command.args.cons.tail.cons.head;
        f(Unescape(a1), Run.Inline(env, a2));
      });

      m.return(Err<Unit@>(Text@(Strs[
              Str|'expected 1 or 2 args, but got ',
              /Core/Int/Show%.Show(num_args)], command.name.loc)));
    };
  };


  @(A0, A1, A2, A3,
    E, I, LI, B, LB,
    LEB, EE, EI, II, LII, IB, LIB, EmI,
    LIIB);
};

