
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
@ Color@ = /Drawing%.Color@;
% Color = /Drawing%.Color;
% Drawings = /Drawing%.Drawings;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

@ Vec@ = /Graphics/3D%.Vec@;

@ Camera@ = /Graphics/Camera%.Camera@;
@ Rect@ = /Graphics/Camera%.Rect@;
% Camera = /Graphics/Camera%.Camera;
% Translate = /Graphics/Camera%.Translate;
% RotateY = /Graphics/Camera%.RotateY;
% Focus = /Graphics/Camera%.Focus;
% Vertex = /Graphics/Camera%.Vertex;

% Triangle = /Graphics/Triangle%.Triangle;

@ Int@ = /Int/Int%.Int@;
% Neg = /Int/Int%.Neg;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ State@ = Camera@;

State@ Initial = Camera(Int|20, Rect@(Neg(Int|32), Neg(Int|24), Int|64, Int|48));

% Colors = @(
  Wall: Color@(Int|175, Int|175, Int|175),
  Ground: Color@(Int|134, Int|48, Int|22),
  Sky: Color@(Int|87, Int|184, Int|230)
);

% Vertices = {
  Int@ xh = Int|20;
  Int@ xl = Neg(xh);
  Int@ yh = Int|6;
  Int@ yl = Neg(yh);
  Int@ zh = Int|30;
  Int@ zl = Neg(zh);
  @(000: Vec@(xl, yl, zl),
    001: Vec@(xl, yl, zh),
    010: Vec@(xl, yh, zl),
    011: Vec@(xl, yh, zh),
    100: Vec@(xh, yl, zl),
    101: Vec@(xh, yl, zh),
    110: Vec@(xh, yh, zl),
    111: Vec@(xh, yh, zh));
};

(State@) { Drawing@; } Draw = (Camera@ camera) {
  % vs = @(
    000: Vertex(camera, Vertices.000),
    001: Vertex(camera, Vertices.001),
    010: Vertex(camera, Vertices.010),
    011: Vertex(camera, Vertices.011),
    100: Vertex(camera, Vertices.100),
    101: Vertex(camera, Vertices.101),
    110: Vertex(camera, Vertices.110),
    111: Vertex(camera, Vertices.111));

  Drawings[
    # Sky
    Rect(camera.clip.x, camera.clip.y, camera.clip.w, camera.clip.h, Colors.Sky),

    # Ground
    Triangle(camera, vs.000, vs.001, vs.100, Colors.Ground),
    Triangle(camera, vs.100, vs.001, vs.101, Colors.Ground),

    # Walls
    Triangle(camera, vs.000, vs.001, vs.011, Colors.Wall),
    Triangle(camera, vs.000, vs.011, vs.010, Colors.Wall),
    Triangle(camera, vs.100, vs.101, vs.111, Colors.Wall),
    Triangle(camera, vs.100, vs.111, vs.110, Colors.Wall),
    Triangle(camera, vs.000, vs.100, vs.110, Colors.Wall),
    Triangle(camera, vs.000, vs.110, vs.010, Colors.Wall),
    Triangle(camera, vs.001, vs.101, vs.111, Colors.Wall),
    Triangle(camera, vs.001, vs.111, vs.011, Colors.Wall)
  ];
};

(Get@<Event@>, Put@<State@>, Unit@!, State@) { Unit@!; }
Run = (Get@<Event@> in, Put@<State@> draw, Unit@! tick, State@ state) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(state);
    Run(in, draw, tick, state);
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));

    State@ new_state = e.key_down.?(
      a: Translate(Neg(Int|1), Int|0, Int|0, state),
      d: Translate(Int|1, Int|0, Int|0, state),
      j: Focus(Neg(Int|1), state),
      k: Focus(Int|1, state),
      s: Translate(Int|0, Int|0, Neg(Int|1), state),
      w: Translate(Int|0, Int|0, Int|1, state),

      left: RotateY(Int|4, state),
      right: RotateY(Neg(Int|4), state),
      : state);
    Run(in, draw, tick, new_state);
  });
  Run(in, draw, tick, state);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  # Set the window coordinates to (-32, -24) lower left corner, (32, 24) upper
  # right corner.
  Point@ a = @(x: Int|10, y: Neg(Int|10));
  Point@ b = @(x: Int|320, y: Int|240);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = out(Effect@(tick: frame_period));

  (State@) { Unit@!; } draw = (State@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0, Int|0, width, height, Color.Black),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0));
  Run(in, draw, tick, Initial); 
};

Main;
