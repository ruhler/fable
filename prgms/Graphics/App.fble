
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Color@ = /Drawing%.Color@;
@ Point@ = /Drawing%.Point@;
% Drawings = /Drawing%.Drawings;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

% Triangle = /Graphics/2D%.Triangle;

@ Camera@ = /Graphics/3D%.Camera@;
@ Vec@ = /Graphics/3D%.Vec@;
@ Mat@ = /Graphics/3D%.Mat@;
% View = /Graphics/3D%.View;

@ Int@ = /Int/Int%.Int@;
% Neg = /Int/Int%.Neg;
% Add = /Int/Int%.Add;
% Sub = /Int/Int%.Sub;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ State@ = Vec@;

(State@) { Drawing@; } Draw = (State@ s) {
  Camera@ camera = Camera@(
    s,
    Mat@(
      Vec@(Int|1, Int|0, Int|0),
      Vec@(Int|0, Int|1, Int|0),
      Vec@(Int|0, Int|0, Int|1)),
    Int|100);

  Point@ 000 = View(camera, Vec@(Int|0, Int|0, Int|0));
  Point@ 001 = View(camera, Vec@(Int|0, Int|0, Int|50));
  Point@ 010 = View(camera, Vec@(Int|0, Int|50, Int|0));
  Point@ 011 = View(camera, Vec@(Int|0, Int|50, Int|50));
  Point@ 100 = View(camera, Vec@(Int|50, Int|0, Int|0));
  Point@ 101 = View(camera, Vec@(Int|50, Int|0, Int|50));
  Point@ 110 = View(camera, Vec@(Int|50, Int|50, Int|0));
  Point@ 111 = View(camera, Vec@(Int|50, Int|50, Int|50));

  Color@ green = Color@(green: Unit);
  Color@ blue = Color@(blue: Unit);
  Color@ red = Color@(red: Unit);

  Drawings[
    Triangle(010, 011, 110, red), Triangle(110, 011, 111, red),
    Triangle(000, 100, 001, red), Triangle(100, 101, 001, red),

    Triangle(100, 110, 111, blue), Triangle(100, 111, 101, blue),
    Triangle(000, 011, 010, blue), Triangle(000, 001, 011, blue),

    Triangle(000, 010, 110, green), Triangle(000, 110, 100, green),
    Triangle(000, 110, 010, green), Triangle(000, 100, 110, green)
  ];
};

(Get@<Event@>, Put@<State@>, Unit@!, State@) { Unit@!; }
Run = (Get@<Event@> in, Put@<State@> draw, Unit@! tick, State@ state) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(state);
    Run(in, draw, tick, state);
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));

    State@ new_state = e.key_down.?(
      h: State@(Sub(state.x, Int|10), state.y, state.z),
      j: State@(state.x, Sub(state.y, Int|10), state.z),
      k: State@(state.x, Add(state.y, Int|10), state.z),
      l: State@(Add(state.x, Int|10), state.y, state.z),
      left: State@(state.x, state.y, Sub(state.z, Int|10)),
      right: State@(state.x, state.y, Add(state.z, Int|10)),
      : state);
    Run(in, draw, tick, new_state);
  });
  Run(in, draw, tick, state);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  Point@ a = @(x: Int|5, y: Neg(Int|5));
  Point@ b = @(x: Int|320, y: Int|240);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = out(Effect@(tick: frame_period));

  (State@) { Unit@!; } draw = (State@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0, Int|0, width, height, Color@(black: Unit)),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0));
  State@ state = State@(Int|0, Int|0, Neg(Int|10));
  Run(in, draw, tick, state); 
};

Main;
