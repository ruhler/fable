
@ Drawing@ = /Drawing%.Drawing@;
% Blank = /Drawing%.Blank;
% Drawings = /Drawing%.Drawings;
% Translate = /Drawing%.Translate;

@ Int@ = /Int/Int%.Int@;

% I = /Int/Int/Lit%.I;
% D = /Int/Int/Lit%.D;

% Graphics = /Invaders/Graphics%;

% Ship = /Invaders/Ship%;
@ Dir@ = Ship.Dir@;
@ Ship@ = Ship.Ship@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;


@ Lives@ = +(Unit@ 0, Unit@ 1, Unit@ 2, Unit@ 3);

@ Game@ = *(
  Int@ score,
  Lives@ lives,
  Ship@ ship
);

Game@ Initial = @(
  score: I(D|0),
  lives: Lives@(3: Unit),
  ship: Ship.Initial
);

(Game@) { Game@; } Tick = (Game@ g) {
  Game@(g.score, g.lives, Ship.Tick(g.ship));
};

(Game@, Dir@) { Game@; } Input = (Game@ g, Dir@ d) {
  Game@(g.score, g.lives, Ship.Input(g.ship, d));
};

(Lives@) { Drawing@; } DrawLives = {
  Drawing@ l1 = Translate(I(D|320), I(D|440), Graphics.Ship);
  Drawing@ l2 = Translate(I(D|360), I(D|440), Graphics.Ship);
  Drawing@ l3 = Translate(I(D|400), I(D|440), Graphics.Ship);
  (Lives@ l) {
    ?(l;
      0: Blank,
      1: Drawings([l1]),
      2: Drawings([l1, l2]),
      3: Drawings([l1, l2, l3]));
  };
};

(Game@) { Drawing@; } Draw = (Game@ g) {
  Drawings([
    # TODO: Print the score.
    DrawLives(g.lives),
    Graphics.Background,
    Ship.Draw(g.ship)
  ]);
};

@(Game@, Initial, Input, Tick, Draw);

