
@ Int@ = /Int%.Int@;
% 0 = /Int%.0;
% Inc = /Int%.Inc;
% Dec = /Int%.Dec;
% Le = /Int/Eq%.Le;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;

# Drop --
#   Remove the first n elements from the list.
#
# Returns an empty list if there are less than n elements on the list.
<@ T@>(Int@, List@<T@>) { List@<T@>; } Drop = <@ T@>(Int@ n, List@<T@> l) {
  Le(n, 0).?(true: l, false: l.?(cons: Drop<T@>(Dec(n), l.cons.tail), nil: l));
};

# Take --
#   Returns the first n elements on the list.
#
# Returns fewer than n elements if there are less than n elements on the list.
<@ T@>(Int@, List@<T@>) { List@<T@>; } Take = <@ T@>(Int@ n, List@<T@> l) {
  Le(n, 0).?(
    true: Nil<T@>,
    false: l.?(
      cons: Cons<T@>(l.cons.head, Take<T@>(Dec(n), l.cons.tail)),
      nil: Nil<T@>));
};

# Length --
#   Returns the length of a list.
#
# Type Parameters:
#   T@ - The type of element on the list.
#
# Arguments:
#   l - the list to get the length of.
#
# Returns:
#   The length of the list.
<@ T@>(List@<T@>) { Int@; } Length = <@ T@>(List@<T@> l) {
  # TODO: This is non tail recursive? Should we implement a more memory
  # efficient version?
  l.?(cons: Inc(Length<T@>(l.cons.tail)), nil: 0);
};

@(Take, Drop, Length);
