
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Color@ = /Drawing%.Color@;
@ Point@ = /Drawing%.Point@;
% Drawings = /Drawing%.Drawings;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

% Line = /Graphics/2D%.Line;

@ Camera@ = /Graphics/3D%.Camera@;
@ Vec@ = /Graphics/3D%.Vec@;
@ Mat@ = /Graphics/3D%.Mat@;
% View = /Graphics/3D%.View;

@ Int@ = /Int/Int%.Int@;
% Neg = /Int/Int%.Neg;
% Add = /Int/Int%.Add;
% Sub = /Int/Int%.Sub;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ State@ = Vec@;

(State@) { Drawing@; } Draw = (State@ s) {
  Camera@ camera = Camera@(
    s,
    Mat@(
      Vec@(Int|1, Int|0, Int|0),
      Vec@(Int|0, Int|1, Int|0),
      Vec@(Int|0, Int|0, Int|1)),
    Int|50);
  Color@ green = Color@(green: Unit);

  # Draw a 10x10 cube centered at (0, 0, 10)
  Drawings[
    Line(View(camera, Vec@(Int|5, Int|5, Int|5)),
         View(camera, Vec@(Neg(Int|5), Int|5, Int|5)), green),
    Line(View(camera, Vec@(Int|5, Int|5, Int|15)),
         View(camera, Vec@(Neg(Int|5), Int|5, Int|15)), green),
    Line(View(camera, Vec@(Int|5, Neg(Int|5), Int|15)),
         View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|15)), green),
    Line(View(camera, Vec@(Int|5, Neg(Int|5), Int|5)),
         View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|5)), green),

    Line(View(camera, Vec@(Int|5, Int|5, Int|5)),
         View(camera, Vec@(Int|5, Neg(Int|5), Int|5)), green),
    Line(View(camera, Vec@(Neg(Int|5), Int|5, Int|5)),
         View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|5)), green),
    Line(View(camera, Vec@(Int|5, Int|5, Int|15)),
         View(camera, Vec@(Int|5, Neg(Int|5), Int|15)), green),
    Line(View(camera, Vec@(Neg(Int|5), Int|5, Int|15)),
         View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|15)), green),

    Line(View(camera, Vec@(Int|5, Int|5, Int|5)),
         View(camera, Vec@(Int|5, Int|5, Int|15)), green),
    Line(View(camera, Vec@(Neg(Int|5), Int|5, Int|5)),
         View(camera, Vec@(Neg(Int|5), Int|5, Int|15)), green),
    Line(View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|5)),
         View(camera, Vec@(Neg(Int|5), Neg(Int|5), Int|15)), green),
    Line(View(camera, Vec@(Int|5, Neg(Int|5), Int|5)),
         View(camera, Vec@(Int|5, Neg(Int|5), Int|15)), green)
  ];
};

(Get@<Event@>, Put@<State@>, Unit@!, State@) { Unit@!; }
Run = (Get@<Event@> in, Put@<State@> draw, Unit@! tick, State@ state) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(state);
    Run(in, draw, tick, state);
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));

    State@ new_state = e.key_down.?(
      h: State@(Sub(state.x, Int|1), state.y, state.z),
      j: State@(state.x, Sub(state.y, Int|1), state.z),
      k: State@(state.x, Add(state.y, Int|1), state.z),
      l: State@(Add(state.x, Int|1), state.y, state.z),
      left: State@(state.x, state.y, Sub(state.z, Int|1)),
      right: State@(state.x, state.y, Add(state.z, Int|1)),
      : state);
    Run(in, draw, tick, new_state);
  });
  Run(in, draw, tick, state);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  Point@ a = @(x: Int|10, y: Neg(Int|10));
  Point@ b = @(x: Int|320, y: Int|240);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = out(Effect@(tick: frame_period));

  (State@) { Unit@!; } draw = (State@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0, Int|0, width, height, Color@(black: Unit)),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0));
  State@ state = State@(Int|0, Int|0, Neg(Int|10));
  Run(in, draw, tick, state); 
};

Main;
