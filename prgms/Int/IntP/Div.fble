
@ IntP@ = /Int/IntP%.IntP@;

@ IntS@ = /Int/IntS%.IntS@;
% 0 = /Int/IntS%.0;
% IntP = /Int/IntS%.IntP;
% 2P0 = /Int/IntS%.2P0;
% 2P1 = /Int/IntS%.2P1;

@ Int@ = /Int/Int%.Int@;
% Sub = /Int/Int%.Sub;
% I0 = /Int/Int%.0;

# Promote an IntS@ to Int@.
(IntS@) { Int@; } Int = (IntS@ s) {
  s.?(0: I0, p: Int@(p: s.p));
};

# QR@ --
#   Quotient and remainder.
@ QR@ = *(IntS@ q, IntS@ r);

# a = b*q + r, r < b, where q = floor(a/b)
(IntP@, IntP@) { QR@; } Div = (IntP@ a, IntP@ b) {
  b.?(1: @(q: IntP(a), r: 0),
      2p0: a.?(1: @(q: 0, r: IntP(a)),
               2p0: {
                 # (2a) = (2b)*q + (2r)
                 QR@ qr = Div(a.2p0, b.2p0);
                 @(q: qr.q, r: 2P0(qr.r));
               },
               2p1: {
                 # (2a+1) = (2b)*q + (2r+1)
                 QR@ qr = Div(a.2p1, b.2p0);
                 @(q: qr.q, r: 2P1(qr.r));
               }),
      2p1: a.?(1: @(q: 0, r: IntP(a)),
               2p0: {
                 QR@ qr = Div(a.2p0, b);
                 IntS@ 2r = 2P0(qr.r);
                 Int@ 2r_minus_b = Sub(Int(2r), Int@(p: b));
                 2r_minus_b.?(
                    n: @(q: 2P0(qr.q), r: 2r),
                    0: @(q: 2P1(qr.q), r: 0),
                    p: @(q: 2P1(qr.q), r: IntP(2r_minus_b.p)));
               },
               2p1: {
                 QR@ qr = Div(a.2p1, b);
                 IntS@ 2rp1 = 2P1(qr.r);
                 Int@ 2rp1_minus_b = Sub(Int(2rp1), Int@(p: b));
                 2rp1_minus_b.?(
                    n: @(q: 2P0(qr.q), r: 2rp1),
                    0: @(q: 2P1(qr.q), r: 0),
                    p: @(q: 2P1(qr.q), r: IntP(2rp1_minus_b.p)));
               }));
};

@(QR@, Div);
