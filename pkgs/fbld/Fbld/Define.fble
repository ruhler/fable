
@ Bool@ = /Core/Bool%.Bool@;
% False = /Core/Bool%.False;

@ Char@ = /Core/Char%.Char@;
% Chars = /Core/Char%.Chars;
% IsNewline = /Core/Char/Type%.IsNewline;

@ Int@ = /Core/Int%.Int@;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Append = /Core/List%.Append;
<@>% Reverse = /Core/List%.Reverse;
<@>% Init = /Core/List%.Init;
<@>% Last = /Core/List%.Last;
<@>% Drop = /Core/List/Length%.Drop;
<@>% Length = /Core/List/Length%.Length;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;
% StartsWith = /Core/String/Eq%.StartsWith;

@ Unit@ = /Core/Unit%.Unit@;

@ Command@ = /Fbld/Command%.Command@;

<@>@ Fbld@ = /Fbld/Fbld%.Fbld@;
@ Invoke@ = /Fbld/Fbld%.Invoke@;
@ Env@ = /Fbld/Fbld%.Env@;
<@>% Do = /Fbld/Fbld%.Do;
<@>% Return = /Fbld/Fbld%.Return;
<@>% Error = /Fbld/Fbld%.Error;
% GetEnv = /Fbld/Fbld%.GetEnv;
% SetEnv = /Fbld/Fbld%.SetEnv;

@ Loc@ = /Fbld/Loc%.Loc@;
% Start = /Fbld/Loc%.Start;

@ Markup@ = /Fbld/Markup%.Markup@;
% Block = /Fbld/Markup%.Block;
% Entry = /Fbld/Markup%.Entry;

% Run = /Fbld/Run%;

@ Text@ = /Fbld/Text%.Text@;
% Text = /Fbld/Text%.Text;
% StringOf = /Fbld/Text%.StringOf;
% LocOf = /Fbld/Text%.LocOf;

@ Map@ = /Core/Map%.Map@<String@, String@>;
% Empty = /Core/Map%.Empty<String@, String@>;
% Insert = /Core/Map%.Insert<String@>(/Core/String/Ord%.Ord)<String@>;
% Lookup = /Core/Map%.Lookup<String@>(/Core/String/Ord%.Ord)<String@>;

@ Args@ = *(String@ name, List@<String@> params, String@ body);

(List@<Text@>) { Args@; } GetArgs = (List@<Text@> args) {
  List@<String@> strs = /Core/List%.Map(args, StringOf);
  String@ name = strs.cons.head;
  List@<String@> params = Init(strs.cons.tail);
  String@ body = Last(strs.cons.tail);
  @(name, params, body);
};

# Indent all but first line with prefix.
(String@, String@) { String@; } Indent = (String@ prefix, String@ text) {
  (Bool@, String@, String@) { String@; }
  indent = (Bool@ nl, String@ str, String@ rchrs) {
    str.?(nil: Reverse(rchrs));
    nl.?(true: indent(False, str, Append(Reverse(prefix), rchrs)));
    Char@ c = str.cons.head;
    indent(IsNewline(c), str.cons.tail, Cons(c, rchrs));
  };

  indent(False, text, Str|'');
};

(Map@, String@) { String@; }
Subst = (Map@ map, String@ body) {
  (String@, String@, String@) { String@; } subst = (String@ indent, String@ body, String@ r) {
    body.?(nil: r);

    String@ tail = body.cons.tail;
    IsNewline(body.cons.head).?(true: {
      subst(Str|'', tail, Cons(body.cons.head, r));
    });
    String@ nindent = Cons(Chars.' ', indent);

    body.cons.head.?('$': {
      Maybe@<String@> mv = /Core/Map%.ForEach(map, Nothing<String@>,
        (String@ key, String@ _, Maybe@<String@> mv) {
          mv.?(just: mv);
          StartsWith(tail, key).?(true: Just(key));
          Nothing<String@>;
        });
      mv.?(nothing: subst(nindent, tail, Cons(body.cons.head, r)));
      String@ ntail = Drop(Length(mv.just), tail);
      String@ value = Lookup(map, mv.just).just;
      String@ indented = Indent(indent, value);
      subst(nindent, ntail, Append(Reverse(indented), r));
    });
    subst(nindent, tail, Cons(body.cons.head, r));
  };

  Reverse(subst(Str|'', body, Str|''));
};

(List@<String@>, List@<Text@>, Map@) { Map@; }
MakeMap = (List@<String@> keys, List@<Text@> values, Map@ m) {
  keys.?(nil: m);
  MakeMap(keys.cons.tail, values.cons.tail,
    Insert(m, keys.cons.head, StringOf(values.cons.head)));
};

(List@<String@>, String@, Command@) { Fbld@<Text@>; }
Define = (List@<String@> argnames, String@ body, Command@ command) {
  Int@ wnt_args = Length(argnames);
  Int@ got_args = Length(command.args);
  /Core/Int/Eq%.Eq(wnt_args, got_args ).?(false: {
    Error<Text@>(LocOf(command.name), Strs[
          Str|'expected ', /Core/Int/Show%.Show(wnt_args),
          Str|' args, but got ', /Core/Int/Show%.Show(got_args)]);
    });

  String@ nbody = Subst(MakeMap(argnames, command.args, Empty), body);
  Loc@ unknown = Start|'???';
  Return(Text(nbody, unknown));
};

(List@<String@>)(String@) { Invoke@; }
DefB = (List@<String@> argnames)(String@ body) {
  (Command@ command) {
    Text@ text <- Do(Define(argnames, body, command));
    Run.Block(text);
  };
};

(List@<String@>)(String@) { Invoke@; }
DefI = (List@<String@> argnames)(String@ body) {
  (Command@ command) {
    Text@ text <- Do(Define(argnames, body, command));
    Run.Inline(text);
  };
};

Markup@ Markup = Block[
  Entry(Str|'defi', (Command@ cmd) {
    Int@ num_args = Length(cmd.args);
    /Core/Int/Eq%.Lt(num_args, Int|2).?(true: {
      Error<Text@>(LocOf(cmd.name), Str|'expected at least two args');
    });

    Args@ args = GetArgs(cmd.args);
    Invoke@ invoke = DefI(args.params)(args.body);
    Env@ env <- Do(GetEnv);
    Invoke@ inline = (Command@ cmd) {
      /Core/String/Eq%.Eq(StringOf(cmd.name), args.name).?(
        true: invoke(cmd),
        false: env.inline(cmd));
    };
    Unit@ _ <- Do(SetEnv(env.@(inline: inline)));
    Return(/Fbld/Text%.Empty(LocOf(cmd.name)));
  }),
  Entry(Str|'defb', (Command@ cmd) {
    Int@ num_args = Length(cmd.args);
    /Core/Int/Eq%.Lt(num_args, Int|2).?(true: {
      Error<Text@>(LocOf(cmd.name), Str|'expected at least two args');
    });

    Args@ args = GetArgs(cmd.args);
    Invoke@ invoke = DefB(args.params)(args.body);
    Env@ env <- Do(GetEnv);
    Invoke@ block = (Command@ cmd) {
      /Core/String/Eq%.Eq(StringOf(cmd.name), args.name).?(
        true: invoke(cmd),
        false: env.block(cmd));
    };
    Unit@ _ <- Do(SetEnv(env.@(block: block)));
    Return(/Fbld/Text%.Empty(LocOf(cmd.name)));
  })
];

@(Markup);
