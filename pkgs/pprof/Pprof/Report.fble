
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

% Chars = /Core/Char%.Chars;

@ Int@ = /Core/Int%.Int@;
% Incr = /Core/Int%.Incr;
% Decr = /Core/Int%.Decr;
% Add = /Core/Int%.Add;
% Sub = /Core/Int%.Sub;
% Mul = /Core/Int%.Mul;
% Div = /Core/Int/Div%.Div;
% IsZero = /Core/Int/Eq%.IsZero;
% Gt = /Core/Int/Eq%.Gt;
% Le = /Core/Int/Eq%.Le;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;
<@>% Search = /Core/List/Eq%.Search;
<@>% Length = /Core/List/Length%.Length;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;
% Lines = /Core/String%.Lines;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Profile@ = /Pprof/Profile%.Profile@;
@ Location@ = /Pprof/Profile%.Location@;
@ LocId@ = /Pprof/Profile%.LocId@;

@ Count@ = /Pprof/Profile/Count%.Count@;
% Counts = /Pprof/Profile/Count%.Counts;
% LM = /Pprof/Profile/LocationMap%.Map;

<<@>@>% Parse = /Pprof/Perf%.Parse;

# Add n spaces to the left of the string.
(Int@, String@) { String@; }
Pad = (Int@ n, String@ str) {
  Le(n, Int|0).?(true: str);
  Pad(Decr(n), Cons(Chars.' ', str));
};

# Add spaces on the left to meet the target width.
(Int@, String@) { String@; }
MinWidth = (Int@ width, String@ str) {
  Pad(Sub(width, Length(str)), str);
};

# Prints a header line for a table show metrics and block.
(List@<String@>) { String@; }
MetricsHeader = (List@<String@> metrics) {
  # Format is "   %8s ... %8s  %s" on: "m1", ..., "mN", "block"
  Strs[
    Str|'   ',
    Strs(/Core/List%.Map(metrics, (String@ metric) {
      Cons(Chars.' ', MinWidth(Int|8, metric));
      })),
    Str|'  block'];
};

@ Row@ = *(Location@ loc, List@<Int@> value);

(Row@, Row@) { Bool@; }
RowAscending = (Row@ a, Row@ b) {
  a.value.?(nil: True);
  b.value.?(nil: False);
  Gt(b.value.cons.head, a.value.cons.head);
};

(Row@, Row@) { Bool@; }
RowDescending = (Row@ a, Row@ b) {
  b.value.?(nil: True);
  a.value.?(nil: False);
  Le(b.value.cons.head, a.value.cons.head);
};

# Prints a header line for a table show metrics and block.
(Bool@, Row@) { String@; }
PrintRow = (Bool@ highlight, Row@ row) {
  Strs[
    highlight.?(true: Str|'** ', false: Str|'   '),
    Strs(/Core/List%.Map(row.value, (Int@ value) {
      Cons(Chars.' ', MinWidth(Int|8, /Core/Int/Show%.Show(value)));
      })),
    Str|'  ',
    row.loc.name, Str|'[', /Pprof/Profile/Show%.LocId(row.loc.id), Str|'X]',
    highlight.?(true: Str|' **', false: Str|'')];
};

Main@ Main = <<@>@ M@>(Stdio@<M@> stdio, Monad@<M@> m, List@<String@> _args) {
  % O = /Core/Stream/OStream%(m);

  Maybe@<Profile@> mprofile <- m.do(Parse(m, stdio.in));
  mprofile.?(nothing: m.return(False));

  # Exit out early while we work on improving performance.
  Unit@ _ <- m.do(O.PutLine(stdio.out, Str|'Done parsing profile. Bailing out for now.'));
  mprofile.?(just: m.return(True));

  Profile@ profile = mprofile.just;

  LM.Map@<Count@> flat = Counts(Nil<LocId@>, Nil<LocId@>, profile);

  (LocId@, Count@, List@<Row@>) { List@<Row@>; }
  FoldOverallRows = (LocId@ id, Count@ count, List@<Row@> es) {
    Int@ sum = /Core/List%.ForEach(count.total, Int|0, Add);
    IsZero(sum).?(true: es);

    Location@ loc = Search(profile.locations, (Location@ l) {
      /Pprof/Profile/Eq%.LocId(l.id, id);
      }).just;
    Cons(@(loc, value: count.total), es);
  };

  List@<Row@> by_overall = {
    List@<Row@> entries = LM.ForEach(flat, Nil<Row@>, FoldOverallRows);
    /Core/List/Sort%.Sort(RowDescending, entries);
  };

  # Header
  Unit@ _ <- m.do(O.PutLine(stdio.out, Lines[
    Str|'Profile Report',
    Str|'==============']));

  # Blocks Executed
  Unit@ _ <- m.do({
    Int@ num_locs = /Core/List/Length%.Length(profile.locations);
    Int@ num_exec = LM.ForEach(flat, Int|0, (LocId@ _k, Count@ c, Int@ sum) {
        Int@ total = /Core/List%.ForEach(c.total, Int|0, Add);
        Gt(total, Int|0).?(true: Incr(sum), false: sum);
      });
    Int@ percent = Div(Mul(Int|100, num_exec), num_locs).q;

    O.PutLine(stdio.out, Lines[
      Strs[
        Str|'blocks executed: ', /Core/Int/Show%.Show(percent),
        Str|'% of ', /Core/Int/Show%.Show(num_locs)]]);
  });

  # Flat by overall time
  Unit@ _ <- m.do({
    O.PutLine(stdio.out, Lines(
        Cons(Str|'Flat Profile by Overall Time',
        Cons(Str|'----------------------------',
        Cons(MetricsHeader(profile.metrics),
        /Core/List%.Map(by_overall, PrintRow(False)))))));
  });

  # Flat by self time
  Unit@ _ <- m.do({
    List@<Row@> entries = LM.ForEach(flat, Nil<Row@>,
      (LocId@ id, Count@ count, List@<Row@> es) {
        Int@ sum = /Core/List%.ForEach(count.self, Int|0, Add);
        IsZero(sum).?(true: es);

        Location@ loc = Search(profile.locations, (Location@ l) {
          /Pprof/Profile/Eq%.LocId(l.id, id);
        }).just;
        Cons(@(loc, value: count.self), es);
      });

    List@<Row@> sorted = /Core/List/Sort%.Sort(RowDescending, entries);

    Unit@ _ <- m.do(O.PutStr(stdio.out, Lines[
        Str|'Flat Profile by Self Time',
        Str|'-------------------------',
        MetricsHeader(profile.metrics)]));

    O.PutLine(stdio.out, Lines(/Core/List%.Map(sorted, PrintRow(False))));
  });

  # Call Graph
  Unit@ _ <- m.do(O.PutStr(stdio.out, Lines[
      Str|'Call Graph',
      MetricsHeader(profile.metrics)]));

  (Row@) { M@<Unit@>; }
  CallGraphRow = (Row@ row) {
    Unit@ _ <- m.do(O.PutLine(stdio.out,
        Str|'-------------------------------'));

    Unit@ _ <- m.do({
      LM.Map@<Count@> into = Counts(Nil<LocId@>, List[row.loc.id], profile);
      List@<Row@> entries = LM.ForEach(into, Nil<Row@>, FoldOverallRows);
      List@<Row@> sorted = /Core/List/Sort%.Sort(RowAscending, entries);
      O.PutStr(stdio.out, Lines(/Core/List%.Map(sorted, PrintRow(False))));
    });

    Unit@ _ <- m.do(O.PutLine(stdio.out, PrintRow(True, row)));

    {
      LM.Map@<Count@> outof = Counts(List[row.loc.id], Nil<LocId@>, profile);
      List@<Row@> entries = LM.ForEach(outof, Nil<Row@>, FoldOverallRows);
      List@<Row@> sorted = /Core/List/Sort%.Sort(RowDescending, entries);
      O.PutStr(stdio.out, Lines(/Core/List%.Map(sorted, PrintRow(False))));
    };
  };

  (List@<Row@>) { M@<Unit@>; }
  CallGraph = (List@<Row@> rows) {
    rows.?(nil: m.return(Unit));
    Unit@ _ <- m.do(CallGraphRow(rows.cons.head));
    CallGraph(rows.cons.tail);
  };

  Unit@ _ <- m.do(CallGraph(by_overall));

  # Locations
  Unit@ _ <- m.do(O.PutLine(stdio.out, Lines[
    Str|'Block Locations',
    Str|'---------------',
    Lines(/Core/List%.Map(profile.locations, (Location@ loc) {
        Strs[loc.name, Str|'[', /Pprof/Profile/Show%.LocId(loc.id), Str|'X]: ',
        loc.file, Str|':', /Core/Int/Show%.Show(loc.line), Str|':',
        /Core/Int/Show%.Show(loc.col)];
      }))]));

  m.return(True);
};

/Core/Stdio/IO%.Run(Main);
