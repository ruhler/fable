
@ Bit@ = /Core/Digits%.Bit@;
% Bit = /Core/Digits%.Bit;
% BitStr = /Core/Digits/String%.BitStr;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Cons = /Core/List%.Cons;

# LFSR@ --
#   State for a linear feedback shift register using xor as the linear
#   function.
#
# Fields:
#   c - List of N coefficients, highest order coefficient first.
#   s - List of N bits of state, with the output (most significant) bit first.
@ LFSR@ = *(List@<Bit@> c, List@<Bit@> s);

# Prod --
#   Compute the element wise product of bits in the given lists.
(List@<Bit@>, List@<Bit@>) { List@<Bit@>; }
Prod = (List@<Bit@> a, List@<Bit@> b) {
  a.?(nil: a);

  List@<Bit@> tail = Prod(a.cons.tail, b.cons.tail);
  Cons(a.cons.head.?(0: Bit.0, 1: b.cons.head), tail);
};

# Sum --
#   Compute the sum over all bits in the given list.
(List@<Bit@>) { Bit@; } Sum = (List@<Bit@> xs) {
  xs.?(nil: Bit.0);
  Bit@ head = xs.cons.head;
  Bit@ tail = Sum(xs.cons.tail);
  head.?(0: tail, 1: tail.?(0: Bit.1, 1: Bit.0));
};

# ShiftIn --
#   Shift bit i into the given state.
(List@<Bit@>, Bit@) { List@<Bit@>; }
ShiftIn = (List@<Bit@> s, Bit@ i) {
  List@<Bit@> tail = s.cons.tail;
  tail.?(nil: List[i]);
  Cons(tail.cons.head, ShiftIn(tail, i));
};

<@>@ Shifted@ = <@ T@> { *(LFSR@ lfsr, T@ output); };

# Shift --
#   Shift the LFSR.
(LFSR@) { Shifted@<Bit@>; } Shift = (LFSR@ lfsr) {
  List@<Bit@> bits = Cons(Bit.1, Prod(lfsr.c, lfsr.s));
  @(lfsr: LFSR@(lfsr.c, ShiftIn(lfsr.s, Sum(bits))),
    output: lfsr.s.cons.head);
};

# Creates an LFSR from the given coefficients and initial seed.
# Pads or truncates seed as necessary to match the length of coefficients.
(List@<Bit@>, List@<Bit@>) { LFSR@; }
LFSR = (List@<Bit@> coefficients, List@<Bit@> seed) {
  (List@<Bit@>, List@<Bit@>, List@<Bit@>) { List@<Bit@>; }
  Resize = (List@<Bit@> c, List@<Bit@> s, List@<Bit@> sd) {
    c.?(nil: {
      s.?(nil: sd); # The sizes match. We're all set.
      Resize(c, s.cons.tail, sd.cons.tail); # Drop the MSB from the seed.
    });

    s.?(nil: Resize(c.cons.tail, s, Cons(Bit.0, sd))); # Pad the seed with 0.
    Resize(c.cons.tail, s.cons.tail, sd);
  };

  LFSR@(coefficients, Resize(coefficients, seed, seed));
};


# Taps -- 
#   A collection of LFSR primitive polynomials (from wikipedia).
#
# Polynomials.n has period 2^n - 1.
% Polynomials = @(
  8: BitStr|10111000,
  16: BitStr|1101000000001000,
  24: BitStr|111000010000000000000000
);

@(LFSR@, Shifted@, LFSR, Shift, Polynomials);
