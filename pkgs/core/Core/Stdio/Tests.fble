
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Nil = /Core/List%.Nil;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<@>@ Pure@ = /Core/Stdio/Pure%.Pure@;
<@>@ PureResult@ = /Core/Stdio/Pure%.PureResult@;
@ PureState@ = /Core/Stdio/Pure%.PureState@;
% Monad = /Core/Stdio/Pure%.Monad;
% Stdio = /Core/Stdio/Pure%.Stdio;
<@>% Run = /Core/Stdio/Pure%.Run;
<@>% PureResultEq = /Core/Stdio/Pure/Eq%.PureResultEq;
<@>% PureResultShow = /Core/Stdio/Pure/Show%.PureResultShow;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Test@ = /Core/Test%.Test@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;
<@>% AssertEquals = /Core/Test%.AssertEquals;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

# Import to test compilation.
% _ = /Core/Stdio/Cat%;
% _ = /Core/Stdio/Test%;

% S = /Core/Stdio/Stdio%<Pure@>(Monad, Stdio);

(String@) { List@<Int@>; } Bytes = (String@ str) {
  /Core/List%.Map(str, /Core/Char/Ascii%.Ord);
};

List@<Int@> HelloWorld = Bytes|'Hello
World';

# Tests --
#  A test suite to test the /Stdio% module and friends.
Test@ Tests = TestSuite(Str|Stdio, List[
  Test(Str|Return, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;
    PureResult@<Bool@> wnt = PureResult@<Bool@>(
      PureState@(inputs, Nil<String@>, Nil<String@>), True);
    PureResult@<Bool@> got = Run(Monad.return(True), inputs);
    AssertEquals(
      PureResultEq(/Core/Bool/Eq%.Eq),
      PureResultShow(/Core/Bool/Show%.Show),
      wnt, got);
  }),

  Test(Str|In, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;
    PureResult@<Maybe@<String@>> wnt = PureResult@<Maybe@<String@>>(
      PureState@(Bytes(Str|World), Nil<String@>, Nil<String@>), Just(Str|'Hello
'));
    PureResult@<Maybe@<String@>> got = Run(S.In, inputs);
    AssertEquals(
      PureResultEq(/Core/Maybe/Eq%.Eq(/Core/String/Eq%.Eq)),
      PureResultShow(/Core/Maybe/Show%.Show(/Core/String/Show%.Show)),
      wnt, got);
  }),

  Test(Str|InEnd, (Unit@ _) {
    List@<Int@> inputs = Nil<Int@>;
    PureResult@<Maybe@<String@>> wnt = PureResult@<Maybe@<String@>>(
      PureState@(Nil<Int@>, Nil<String@>, Nil<String@>), Nothing<String@>);
    PureResult@<Maybe@<String@>> got = Run(S.In, inputs);
    AssertEquals(
      PureResultEq(/Core/Maybe/Eq%.Eq(/Core/String/Eq%.Eq)),
      PureResultShow(/Core/Maybe/Show%.Show(/Core/String/Show%.Show)),
      wnt, got);
  }),

  Test(Str|Out, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;
    PureResult@<Unit@> wnt = PureResult@<Unit@>(
      PureState@(inputs, List[Str|There], Nil<String@>), Unit);
    PureResult@<Unit@> got = Run(S.Out(Str|There), inputs);
    AssertEquals(
      PureResultEq(/Core/Unit/Eq%.Eq),
      PureResultShow(/Core/Unit/Show%.Show),
      wnt, got);
  }),

  Test(Str|Err, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;
    PureResult@<Unit@> wnt = PureResult@<Unit@>(
      PureState@(inputs, Nil<String@>, List[Str|There]), Unit);
    PureResult@<Unit@> got = Run(S.Err(Str|There), inputs);
    AssertEquals(
      PureResultEq(/Core/Unit/Eq%.Eq),
      PureResultShow(/Core/Unit/Show%.Show),
      wnt, got);
  }),

  Test(Str|DoOuts, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;

    PureResult@<Unit@> wnt = PureResult@<Unit@>(
      PureState@(inputs, List[Str|A, Str|B], Nil<String@>), Unit);

    PureResult@<Unit@> got = Run({
      Unit@ _ <- Monad.do(S.Out(Str|A));
      Unit@ _ <- Monad.do(S.Out(Str|B));
      Monad.return(Unit);
    }, inputs);

    AssertEquals(
      PureResultEq(/Core/Unit/Eq%.Eq),
      PureResultShow(/Core/Unit/Show%.Show),
      wnt, got);
  }),

  Test(Str|DoErrs, (Unit@ _) {
    List@<Int@> inputs = HelloWorld;

    PureResult@<Unit@> wnt = PureResult@<Unit@>(
      PureState@(inputs, Nil<String@>, List[Str|A, Str|B]), Unit);

    PureResult@<Unit@> got = Run({
      Unit@ _ <- Monad.do(S.Err(Str|A));
      Unit@ _ <- Monad.do(S.Err(Str|B));
      Monad.return(Unit);
    }, inputs);

    AssertEquals(
      PureResultEq(/Core/Unit/Eq%.Eq),
      PureResultShow(/Core/Unit/Show%.Show),
      wnt, got);
  })
]);

@(Tests);
