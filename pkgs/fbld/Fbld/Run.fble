<@>@ List@ = /Core/List%.List@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Command@ = /Fbld/Command%.Command@;

@ Text@ = /Fbld/Text%.Text@;

<@>@ Result@ = /Fbld/Result%.Result@;
<@>% Ok = /Fbld/Result%.Ok;
<@>% Err = /Fbld/Result%.Err;


<<@>@ M@>(Monad@<M@> m) {
  @ Env@ = /Fbld/Invoke%<M@>.Env@;

  % ResultM = /Fbld/ResultM%<M@>;
  <@>@ MR@ = ResultM.MR@;
  Monad@<MR@> mr = ResultM.Monad(m);

  # Interprets and executes inline structured text.
  (Env@, Text@) { MR@<Unit@>; }
  Inline = (Env@ env, Text@ text) {
    Result@<List@<Command@>> parsed = /Fbld/Parse%.Inline(text);
    parsed.?(err: m.return(Err<Unit@>(parsed.err)));

    /Core/List%.ForEach(parsed.ok, m.return(Ok(Unit)),
      (Command@ cmd, MR@<Unit@> mx) {
        Unit@ _ <- mr.do(mx);
        env.inline(env, cmd);
      });
  };

  # Interprets and executes block structured text.
  (Env@, Text@) { MR@<Unit@>; }
  Block = (Env@ env, Text@ text) {
    Result@<List@<Command@>> parsed = /Fbld/Parse%.Block(text);
    parsed.?(err: m.return(Err<Unit@>(parsed.err)));

    /Core/List%.ForEach(parsed.ok, m.return(Ok(Unit)),
      (Command@ cmd, MR@<Unit@> mx) {
        Unit@ _ <- mr.do(mx);
        env.block(env, cmd);
      });
  };

  @(Inline, Block);
};
