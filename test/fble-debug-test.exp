
# Test that we can do gdb based debugging of the fble-debug-test binary.
# Usage:
#   expect fble-debug-test.exp srcdir builddir 

#   srcdir - Path to the source directory containing the spec/ directory.
#   builddir - Path to the build directory.
set ::s [lindex $argv 0]
set ::b [lindex $argv 1]

proc require { str } {
  expect -ex \
    $str {} \
    "(gdb) " { error "got prompt instead of \"$str\"" } \
    timeout {error "timeout waiting for \"$str\"" } \
    eof {error "eof waiting for \"$str\"" }
}

eval {
  # Test that we can set a breakpoint at a particular line in a file and run to
  # that breakpoint.
  spawn gdb --quiet $::b/test/fble-debug-test
  require "(gdb) "

  set STMT_LINE 14
  send "break $::s/test/DebugTest.fble:$STMT_LINE\n"
  require "Breakpoint 1 at"
  require "$::s/test/DebugTest.fble, line $STMT_LINE."
  require "(gdb) "

  send "run\n"
  require "at $::s/test/DebugTest.fble:$STMT_LINE"
  require "(gdb) "
  close
}

eval {
  # Test that we can set a breakpoint at a particular function and run to
  # that breakpoint.
  spawn gdb --quiet $::b/test/fble-debug-test
  expect "(gdb) "

  set LINE 14
  send "break /DebugTest%.Next!\n"
  require "Breakpoint 1 at"
  require "$::s/test/DebugTest.fble, line $LINE."
  require "(gdb) "

  send "run\n"
  require "at $::s/test/DebugTest.fble:$LINE"
  require "(gdb) "
  close
}

eval {
  # Test that we can print values of local variables
  spawn gdb --quiet $::b/test/fble-debug-test
  expect "(gdb) "

  set LINE 15
  send "break $::s/test/DebugTest.fble:$LINE\n"
  require "Breakpoint 1 at"
  require "$::s/test/DebugTest.fble, line $LINE."
  require "(gdb) "

  send "run\n"
  require "at $::s/test/DebugTest.fble:$LINE"
  require "(gdb) "

  send "info locals\n"
  require "light"
  require "u"
  require "(gdb)"

  # Test that it show the type of the variable as FbleValue*.
  send "print light\n"
  require "(struct FbleValue *)"
  require "(gdb)"

  close
}

exit 0
