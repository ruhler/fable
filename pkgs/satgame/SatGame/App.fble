
% True = /Core/Bool%.True;

@ Int@ = /Core/Int%.Int@;
% Incr = /Core/Int%.Incr;
% Sub = /Core/Int%.Sub;
% Div = /Core/Int/Div%.Div;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ IStream@ = /Core/Stream%.IStream@;
<<@>@>@ OStream@ = /Core/Stream%.OStream@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

<<@>@>@ App@ = /App/App%.App@;
<<@>@>@ Main@ = /App/App%.Main@;
@ Event@ = /App/App%.Event@;
@ Effect@ = /App/App%.Effect@;

@ Color@ = /App/Drawing%.Color@;
@ Drawing@ = /App/Drawing%.Drawing@;
@ Point@ = /App/Drawing%.Point@;
% Color = /App/Drawing%.Color;
% Rect = /App/Drawing%.Rect;
% Translate = /App/Drawing%.Translate;
% Drawings = /App/Drawing%.Drawings;

@ Params@ = /SatGame/Puzzle%.Params@;
@ Puzzle@ = /SatGame/Puzzle%.Puzzle@;

@ Game@ = /SatGame/Game%.Game@;
% Toggle = /SatGame/Game%.Toggle;

@ Layout@ = /SatGame/Game/Draw%.Layout@;
@ TileIndex@ = /SatGame/Game/Draw%.TileIndex@;
% Layout = /SatGame/Game/Draw%.Layout;
% TileOf = /SatGame/Game/Draw%.TileOf;

% PureLFSR = /SatGame/Random/PureLFSR%;
% Generate = /SatGame/Generate%<PureLFSR.M@>(PureLFSR.Monad, PureLFSR.Random);

@ Args@ = *(
  Int@ num_vars,
  Int@ num_clauses,
  Int@ num_literals_per_clause,
  Int@ seed
);

Args@ DefaultArgs = @(
  num_vars: Int|12,
  num_clauses: Int|18,
  num_literals_per_clause: Int|3,
  seed: Int|1
);

(List@<String@>, Args@) { Args@; }
GetArgs = (List@<String@> args, Args@ default) {
  args.?(nil: default);
  args.cons.tail.?(nil: default);

  String@ arg = args.cons.head;
  Int@ value = /Core/Int/Show%.Read(args.cons.tail.cons.head);

  Args@ nargs = {
    /Core/String/Eq%.Eq(arg, Str|'--vars').?(true: {
      default.@(num_vars: value);
    });

    /Core/String/Eq%.Eq(arg, Str|'--clauses').?(true: {
      default.@(num_clauses: value);
    });

    /Core/String/Eq%.Eq(arg, Str|'--lits-per-clause').?(true: {
      default.@(num_literals_per_clause: value);
    });

    /Core/String/Eq%.Eq(arg, Str|'--seed').?(true: {
      default.@(seed: value);
    });

    default;
  };
  GetArgs(args.cons.tail.cons.tail, nargs);
};

@ State@ = *(
  Game@ game,
  Int@ mouse_x,
  Int@ mouse_y,
  Drawing@ board,  # Cached drawing of the game board.

  Int@ width,      # width of the window
  Int@ height,     # height of the window
  Point@ origin    # Position of game board in window
);

Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio, App@<M@> app, Int@ width, Int@ height, List@<String@> argv) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);

  # Read and update the default seed value.
  Int@ seed <- m.do({
    Maybe@<String@> home <- m.do(stdio.getenv(Str|HOME));
    home.?(nothing: m.return(Int|0));

    String@ filename = Strs[home.just, Str|'/.fble-satgame'];
    Maybe@<IStream@<M@>> istream <- m.do(stdio.read(filename));

    Int@ seed <- m.do({
      istream.?(nothing: m.return(Int|0));
      String@ text <- m.do(I.GetText(istream.just));
      m.return(/Core/Int/Show%.Read(text));
    });

    Maybe@<OStream@<M@>> ostream <- m.do(stdio.write(filename));
    Unit@ _ <- m.do({
      ostream.?(nothing: m.return(Unit));
      O.PutStr(ostream.just, /Core/Int/Show%.Show(Incr(seed)));
    });

    m.return(seed);
  });

  Args@ args = GetArgs(argv, DefaultArgs.@(seed));
  Params@ params = Params@(args.num_vars, args.num_clauses, args.num_literals_per_clause);
  Layout@ layout = Layout(params);

  # Center the game in the window.
  # TODO: We should scale it to fit too, if only affine transformations
  # allowed non-integer multiples.
  (Int@, Int@) { Point@; } Origin = (Int@ w, Int@ h) {
    Int@ x = Div(Sub(w, layout.width), Int|2).q;
    Int@ y = Div(Sub(h, layout.height), Int|2).q;
    @(x, y);
  };

  (State@) { M@<Unit@>; } Draw = (State@ state) {
    Color@ bg_color = Color@(Int|64, Int|64, Int|64);
    Drawing@ background = Rect(Int|0, Int|0, state.width, state.height, bg_color);
    Drawing@ cursor = Rect(
      Sub(state.mouse_x, Int|4),
      Sub(state.mouse_y, Int|4),
      Int|8, Int|8, Color.Yellow);
    Drawing@ screen = Drawings[
      background,
      Translate(state.origin.x, state.origin.y, state.board),
      cursor];

    app.effect(Effect@(draw: screen));
  };

  (State@) { M@<Unit@>; } EventLoop = (State@ state) {
    Event@ e <- m.do(app.event);
    e.?(
      tick: EventLoop(state),
      key_down: m.return(Unit),
      key_up: m.return(Unit),
      mouse_down: {
        Int@ x = Sub(e.mouse_down.x, state.origin.x);
        Int@ y = Sub(e.mouse_down.y, state.origin.y);
        Maybe@<TileIndex@> ti = TileOf(layout, x, y);
        ti.?(nothing: EventLoop(state));
        Game@ ngame = Toggle(ti.just.group, ti.just.tile, state.game);
        Drawing@ board = /SatGame/Game/Draw%.Draw(layout, ngame);
        State@ nstate = state.@(game: ngame, board: board);
        Unit@ _ <- m.do(Draw(nstate));
        EventLoop(nstate);
      },
      mouse_up: EventLoop(state),
      resize: {
        State@ nstate = state.@(
          width: e.resize.width,
          height: e.resize.height,
          origin: Origin(e.resize.width, e.resize.height)
        );
        Unit@ _ <- m.do(Draw(nstate));
        EventLoop(nstate);
      },
      mouse_motion: {
        State@ ns = state.@(
          mouse_x: e.mouse_motion.x,
          mouse_y: e.mouse_motion.y
        );
        Unit@ _ <- m.do(Draw(ns));
        EventLoop(ns);
      });
  };

  Unit@ _ <- m.do(O.PutLine(stdio.out, Strs[
      Str|'Generating puzzle with params: ',
      /SatGame/Puzzle/Show%.Params(params),
      Str|' (seed ', /Core/Int/Show%.Show(args.seed), Str|')'
  ]));

  Puzzle@ puzzle = PureLFSR.Run(Generate(params), args.seed);
  Unit@ _ <- m.do(O.PutLine(stdio.out, /SatGame/Puzzle/Show%.Puzzle(puzzle)));

  Game@ game = /SatGame/Game%.Game(puzzle);

  State@ state = @(
    game,
    mouse_x: Int|0,
    mouse_y: Int|0,
    board: /SatGame/Game/Draw%.Draw(layout, game),
    width,
    height,
    origin: Origin(width, height)
  );

  Unit@ _ <- m.do(Draw(state));
  Unit@ _ <- m.do(EventLoop(state));
  m.return(True);
};

Main;
