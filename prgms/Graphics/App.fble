
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
% Color = /Drawing%.Color;
% Drawings = /Drawing%.Drawings;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

% Triangle = /Graphics/2D%.Triangle;

@ Camera@ = /Graphics/3D%.Camera@;
@ Vec@ = /Graphics/3D%.Vec@;
@ Mat@ = /Graphics/3D%.Mat@;
% View = /Graphics/3D%.View;

@ Int@ = /Int/Int%.Int@;
% Neg = /Int/Int%.Neg;
% Add = /Int/Int%.Add;
% Sub = /Int/Int%.Sub;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ State@ = Vec@;

(State@) { Drawing@; } Draw = (State@ s) {
  Camera@ camera = Camera@(
    s,
    Mat@(
      Vec@(Int|1, Int|0, Int|0),
      Vec@(Int|0, Int|1, Int|0),
      Vec@(Int|0, Int|0, Int|1)),
    Int|10);

  # Triangle that happens to already be positioned on the focus plane of the
  # camera.
  Point@ a = View(camera, Vec@(Int|0, Int|0, Int|20));
  Point@ b = View(camera, Vec@(Int|0, Int|10, Int|20));
  Point@ c = View(camera, Vec@(Int|20, Int|0, Int|20));
  Point@ d = View(camera, Vec@(Int|5, Int|5, Int|10));

  Drawings[
    Triangle(a, c, b, Color.Red),
    Triangle(a, d, c, Color.Green),
    Triangle(b, c, d, Color.Blue),
    Triangle(a, b, d, Color.White)
  ];
};

(Get@<Event@>, Put@<State@>, Unit@!, State@) { Unit@!; }
Run = (Get@<Event@> in, Put@<State@> draw, Unit@! tick, State@ state) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(state);
    Run(in, draw, tick, state);
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));

    State@ new_state = e.key_down.?(
      h: State@(Sub(state.x, Int|1), state.y, state.z),
      j: State@(state.x, Sub(state.y, Int|1), state.z),
      k: State@(state.x, Add(state.y, Int|1), state.z),
      l: State@(Add(state.x, Int|1), state.y, state.z),
      left: State@(state.x, state.y, Sub(state.z, Int|1)),
      right: State@(state.x, state.y, Add(state.z, Int|1)),
      : state);
    Run(in, draw, tick, new_state);
  });
  Run(in, draw, tick, state);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  # The window should be centered at (0, 0), ranging in from
  # (-32, -24) to (32, 24).
  Point@ a = @(x: Int|10, y: Neg(Int|10));
  Point@ b = @(x: Int|320, y: Int|240);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = out(Effect@(tick: frame_period));

  (State@) { Unit@!; } draw = (State@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0, Int|0, width, height, Color.Black),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0));
  State@ state = State@(Int|0, Int|0, Int|0);
  Run(in, draw, tick, state); 
};

Main;
