
@ Char@ = /Core/Char%.Char@;
<@>% Append = /Core/List%.Append;
<@,@>@ Map@ = /Map%.Map@;
<@,@,@>% ForEach = /Map%.ForEach;
<@>@ Show@ = /Show%.Show@;
@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

# Show --
#   Implementation of Show@ for Map@<K@, V@>. See documentation of
#   /Show%.Show@.
#
#   { }
#   { a => b; }
#   { a => b; c => d; }
#
<@ K@, @ V@>(Show@<K@>, Show@<V@>) { Show@<Map@<K@, V@>>; }
Show = <@ K@, @ V@>(Show@<K@> show_key, Show@<V@> show_value)(Map@<K@, V@> m) {
  Append<Char@>(
    ForEach<K@, V@, String@>(m, Str|'{ ', (K@ k, V@ v, String@ s) {
      Strs[s, show_key(k), Str|' => ', show_value(v), Str|'; '];
    }),
    Str|'}');
};

@(Show);
