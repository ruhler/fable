
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;

<@>% Nothing = /Core/Maybe%.Nothing;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;

@ Formula@ = /Sat/Sat%.Formula@;
@ Result@ = /Sat/Sat%.Result@;
% Solve = /Sat/Sat%.Solve;

<@>@ Parse@ = /Sat/Dimacs%.Parse@;
% ParseLines = /Sat/Dimacs%.ParseLines;
% ShowResult = /Sat/Show%.ShowResult;

Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);
  % S = /Core/Stdio/Stdio%(m, stdio); 

  M@<List@<String@>> GetLines = {
    String@ next <- m.do(I.GetLine(S.Stdin));
    next.?(nil: m.return(Nil<String@>));

    List@<String@> tail <- m.do(GetLines);
    m.return(Cons<String@>(next, tail));
  };

  (List@<String@> args) {
    List@<String@> lines <- m.do(GetLines);
    Parse@<Formula@> parsed = ParseLines(lines);
    parsed.?(error: {
      Unit@ _ <- m.do(O.PutStr(S.Stderr, parsed.error));
      m.return(False);
      });

    Result@ result = Solve(parsed.parsed, Nothing<Int@>);
    Unit@ _ <- m.do(O.PutLine(S.Stdout, ShowResult(result)));
    m.return(True);
  };
};

/Core/Stdio/IO%.Run(Main);
