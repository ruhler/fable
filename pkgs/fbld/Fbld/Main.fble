
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Core@ = /Fbld/Core%.Core@;

<@>@ Fbld@ = /Fbld/Fbld%.Fbld@;
@ Env@ = /Fbld/Fbld%.Env@;
<@>% Error = /Fbld/Fbld%.Error;
% Cat = /Fbld/Fbld/Utils%.Cat;

% Report = /Fbld/Loc%.Report;
% Start = /Fbld/Loc%.Start;
% Unknown = /Fbld/Loc%.Unknown;

@ Markup@ = /Fbld/Markup%.Markup@;
% Markups = /Fbld/Markup%.Markups;

<@>@ Result@ = /Fbld/Result%.Result@;

% Run = /Fbld/Run%;

@ Text@ = /Fbld/Text%.Text@;
% Text = /Fbld/Text%.Text;
% StringOf = /Fbld/Text%.StringOf;
% LocOf = /Fbld/Text%.LocOf;


# Main --
#   Main function for fbld program.
Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio) {
  % I = /Core/Stream/IStream%(m);
  % O = /Core/Stream/OStream%(m);

  @ Backend@ = +(Unit@ html, Unit@ markdown, Unit@ man, Unit@ text);

  @ Args@ = *(
    String@ buildstamp,
    String@ version,
    Backend@ backend,
    Bool@ fble,
    Bool@ tutorial,
    Bool@ usage_help,
    Bool@ usage_man,
    Bool@ usage_lib,
    List@<String@> rfiles
  );

  (List@<String@>) { Args@; } ParseArgs = (List@<String@> args) {
    (List@<String@>, Args@) { Args@; }
    parse = (List@<String@> args, Args@ result) {
      args.?(nil: result);
      String@ arg = args.cons.head;
      /Core/String/Eq%.Eq(Str|'--buildstamp', arg).?(true: {
        String@ buildstamp = args.cons.tail.cons.head;
        List@<String@> nargs = args.cons.tail.cons.tail;
        parse(nargs, result.@(buildstamp));
      });
      /Core/String/Eq%.Eq(Str|'--version', arg).?(true: {
        String@ version = args.cons.tail.cons.head;
        List@<String@> nargs = args.cons.tail.cons.tail;
        parse(nargs, result.@(version));
      });
      /Core/String/Eq%.Eq(Str|'--html', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(html: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--markdown', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(markdown: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--man', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(man: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--text', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(backend: Backend@(text: Unit)));
      });
      /Core/String/Eq%.Eq(Str|'--fble', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(fble: True));
      });
      /Core/String/Eq%.Eq(Str|'--tutorial', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(tutorial: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.help', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_help: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.man', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_man: True));
      });
      /Core/String/Eq%.Eq(Str|'--usage.lib', arg).?(true: {
        List@<String@> nargs = args.cons.tail;
        parse(nargs, result.@(usage_lib: True));
      });
      parse(args.cons.tail, result.@(rfiles: Cons(arg, result.rfiles)));
    };
    parse(args, Args@(Str|'??', Str|'??',
        Backend@(html: Unit), False, False, False, False, False, Nil<String@>));
  };

  (List@<String@> args) {
    Args@ parsed = ParseArgs(args);

    Core@ core = parsed.backend.?(
      html: /Fbld/Html%.Core,
      markdown: /Fbld/Markdown%.Core,
      man: /Fbld/Man%.Core,
      text: /Fbld/TextBackend%.Core);

    % Build = /Fbld/Build%(core, parsed.buildstamp);
    % Version = /Fbld/Version%(core, parsed.version);

    Markup@ markup = Markups[
      /Fbld/Define%.Markup,
      /Fbld/Core%.Markup(core),
      Build.Markup,
      Version.Markup,
      parsed.fble.?(
        true: /Fbld/Fble%.Markup,
        false: /Fbld/Markup%.Empty),
      parsed.tutorial.?(
        true: /Fbld/Tutorial%.Markup,
        false: /Fbld/Markup%.Empty),
      parsed.usage_help.?(
        true: /Fbld/Usage/Help%.Markup,
        false: /Fbld/Markup%.Empty),
      parsed.usage_lib.?(
        true: /Fbld/Usage/Lib%.Markup,
        false: /Fbld/Markup%.Empty)
    ];

    Env@ env = /Fbld/Markup%.Env(markup);

    List@<Fbld@<Text@>> fblds <- m.do(/Core/List%.ForEach(parsed.rfiles,
        m.return(Nil<Fbld@<Text@>>),
        (String@ filename, M@<List@<Fbld@<Text@>>> ms) {
          List@<Fbld@<Text@>> head <- m.do(ms);

          Fbld@<Text@> fbld <- m.do({
            Maybe@<IStream@<M@>> fin <- m.do(stdio.read(filename));
            fin.?(nothing: {
              String@ msg = Strs[Str|'unable to open ', filename];
              m.return(Error<Text@>(Unknown, msg));
            });

            String@ contents <- m.do(I.GetText(fin.just));
            m.return(Run.Block(Text(contents, Start(filename))));
          });
          m.return(Cons(fbld, head));
        }));

    Fbld@<Text@> fbld = Cat(Unknown)(fblds);
    Result@<Text@> r = /Fbld/Fbld%.Run(fbld, env);
    r.?(err: {
      String@ msg = Report(LocOf(r.err), StringOf(r.err));
      Unit@ _ <- m.do(O.PutLine(stdio.err, msg));
      m.return(False);
    });

    Unit@ _ <- m.do(O.PutStr(stdio.out, StringOf(r.ok)));
    m.return(True);
  };
};

/Core/Stdio/IO%.Run(Main);
