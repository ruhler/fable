
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
<@>@ Eq@ = /Core/Eq%.Eq@;
<@>% List = /Core/List%.List;
<@>@ Maybe@ = /Maybe%.Maybe@;
<@>% Just = /Maybe%.Just;
<@>% Nothing = /Maybe%.Nothing;
<@>@ Show@ = /Core/Show%.Show@;
@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
@ Test@ = /Core/Test%.Test@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;
<@>% AssertEquals = /Core/Test%.AssertEquals;
@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

# AB@ --
#   Helper type to test eq and show of Maybe@.
@ AB@ = +(Unit@ a, Unit@ b);
AB@ A = AB@(a: Unit);
AB@ B = AB@(b: Unit);

# EqAB --
#   An instance of Eq@ for AB@.
Eq@<AB@> EqAB = (AB@ x, AB@ y) {
  x.?(a: y.?(a: True, b: False), b: y.?(a: False, b: True));
};

# ShowAB --
#   An instance of Show@ for AB@.
Show@<AB@> ShowAB = (AB@ x) {
  x.?(a: Str|a, b: Str|b);
};

Eq@<Maybe@<AB@>> Eq = /Maybe/Eq%.Eq<AB@>(EqAB);
Show@<Maybe@<AB@>> Show = /Maybe/Show%.Show<AB@>(ShowAB);

# Tests --
#  A test suite to test the /Maybe% module and friends.
Test@ Tests = TestSuite(Str|Maybe, List<Test@>[
  Test(Str|Eq_JustEq, !({
    Bool@ wnt = True;
    Bool@ got = Eq(Just<AB@>(A), Just<AB@>(A));
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|Eq_JustNe, !({
    Bool@ wnt = False;
    Bool@ got = Eq(Just<AB@>(A), Just<AB@>(B));
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|Eq_JustNothing, !({
    Bool@ wnt = False;
    Bool@ got = Eq(Just<AB@>(A), Nothing<AB@>);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|Eq_Nothing, !({
    Bool@ wnt = True;
    Bool@ got = Eq(Nothing<AB@>, Nothing<AB@>);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|Show_Just, !({
    String@ wnt = Str|'Just:b';
    String@ got = Show(Just<AB@>(B));
    AssertEquals<String@>(/Core/String/Eq%.Eq, /Core/String/Show%.Show, wnt, got);
  })),

  Test(Str|Show_Nothing, !({
    String@ wnt = Str|'Nothing';
    String@ got = Show(Nothing<AB@>);
    AssertEquals<String@>(/Core/String/Eq%.Eq, /Core/String/Show%.Show, wnt, got);
  }))
]);

@(Tests);
