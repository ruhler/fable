
@ Char@ = /Core/Char%.Char@;

@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Neg = /Core/Int%.Neg;
% Mul = /Core/Int%.Mul;
% Int = /Core/Int/Lit%.Int;

<@>% Append = /Core/List%.Append;

<@>@ Show@ = /Core/Show%.Show@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

(String@) { Int@; } Read = {
  (Char@) { Int@; } Digit = (Char@ c) {
    c.?(
      '0': Int|0, '1': Int|1, '2': Int|2, '3': Int|3, '4': Int|4,
      '5': Int|5, '6': Int|6, '7': Int|7, '8': Int|8, '9': Int|9,
      : Int|0);
  };

  (Int@, String@) { Int@; } ReadP = (Int@ x, String@ s) {
    s.?(nil: x);
    ReadP(Add(Mul(Int|10, x), Digit(s.cons.head)), s.cons.tail);
  };
  
  (String@ s) {
    s.?(nil: Int|0);
    s.cons.head.?('-': Neg(ReadP(Int|0, s.cons.tail)), : ReadP(Int|0, s));
  };
};

Show@<Int@> Show = (Int@ x) {
  x.?(
    n: Append(Str|'-', /Core/Int/IntP/Show%.Show(x.n)),
    0: Str|0,
    p: /Core/Int/IntP/Show%.Show(x.p));
};

@(Read, Show);
