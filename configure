#!/bin/sh
# configure script for fable\
exec tclsh8.6 "$0" "$@"

# usage -- output usage info to the given channel.
proc usage { channel } {
  puts $channel {
Usage: ../configure [OPTION]...

Options:
  -h, --help          display this help and exit
  --prefix=PREFIX     install files in PREFIX [/usr/local]
}
}

namespace eval "config" {
  # ::config::srcdir
  # The top level source directory.
  # Inferred relative to the location of this configure script.
  set srcdir [file dirname [info script]]

  # ::config::builddir
  # The top level build directory.
  # Inferred from the current directory.
  set builddir "."

  # ::config::prefix
  # The install prefix.
  set prefix "/usr/local"

  while {[llength $::argv] > 0} {
    set ::arg [lindex $::argv 0]
    set ::argv [lrange $::argv 1 end]
    switch -glob $::arg {
      -h -
      --help {
        usage stdout
        exit 0
      }
      --prefix {
        if {[llength $::argv] == 0} {
          puts stderr "configure: error: missing argument to --prefix"
          exit 1
        }

        set prefix [lindex $::argv 0]
        set ::argv [lrange $::argv 1 end]
      }
      --prefix=* {
        set prefix [string range $arg [string length "--prefix="] end]
      }
      default {
        puts stderr "configure: error: unrecognized option: `$arg'"
        usage stderr
        exit 1
      }
    }
  }

  # Install directories based on prefix.
  set includedir $::config::prefix/include
  set libdir $::config::prefix/lib
  set bindir $::config::prefix/bin
  set mandir $::config::prefix/man
  set datadir $::config::prefix/share
}

# Generate config.tcl and show the user the config settings.
puts "Configuration:"
set fout [open $::config::builddir/config.tcl "w"]
puts $fout "namespace eval \"config\" {"
foreach var [info var ::config::*] {
  set name [string range $var [string length "::config::"] end]
  set value [subst $$var]
  puts "  $name=$value"
  puts $fout "  set $name \{$value\}"
}
puts $fout "}"
close $fout

# Generate the build.ninja file.
exec tclsh8.6 $::config::srcdir/build.tcl $::config::builddir/build.ninja
