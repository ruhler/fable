
@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
% Color = /Drawing%.Color;
% Drawings = /Drawing%.Drawings;

@ Drawn@ = /Drawing/Test%.Drawn@;
@ Pixel@ = /Drawing/Test%.Pixel@;
% Drawn = /Drawing/Test%.Drawn;
% Draw = /Drawing/Test%.Draw;

% Triangle = /Graphics/2D%.Triangle;

% Int = /Int/Int/Lit%.Int;

<@>% List = /List%.List;

% Str = /String%.Str;

@ Test@ = /Test%.Test@;
% Test = /Test%.Test;
% TestSuite = /Test%.TestSuite;

Test@ Tests = TestSuite(Str|'Graphics.2D', List<Test@>[
  Test(Str|'Lower Left Right Triangle', !({
    Drawing@ drawing = Triangle(
      Point@(Int|0, Int|0),
      Point@(Int|0, Int|3),
      Point@(Int|3, Int|0), Color.Green);

    # Points on the diagonal are included, because of the negative slope.
    Drawn@ wnt = Drawn[
      Pixel@(Int|0, Int|0, Color.Green),
      Pixel@(Int|0, Int|1, Color.Green),
      Pixel@(Int|0, Int|2, Color.Green),
      Pixel@(Int|1, Int|0, Color.Green),
      Pixel@(Int|1, Int|1, Color.Green),
      Pixel@(Int|2, Int|0, Color.Green)
    ];
    Drawn@ got = Draw(drawing);
      
    /Drawing/Test%.AssertEquals(wnt, got);
  })),

  Test(Str|'Upper Left Right Triangle', !({
    Drawing@ drawing = Triangle(
      Point@(Int|0, Int|0),
      Point@(Int|0, Int|3),
      Point@(Int|3, Int|3), Color.Green);

    # Points on the diagonal are excluded because of the positive slope.
    Drawn@ wnt = Drawn[
      Pixel@(Int|0, Int|1, Color.Green),
      Pixel@(Int|0, Int|2, Color.Green),
      Pixel@(Int|1, Int|2, Color.Green)
    ];
    Drawn@ got = Draw(drawing);
      
    /Drawing/Test%.AssertEquals(wnt, got);
  })),

  Test(Str|'Adjacent Gap', !({
    # Regression test for an issue with rounding we had in the past where two
    # adjacent triangles could have a gap between them because we were using
    # inconsistent rounding.
    Drawing@ drawing = Drawings[
      Triangle(
        Point@(Int|0, Int|0),
        Point@(Int|0, Int|2),
        Point@(Int|1, Int|0), Color.Green),
      Triangle(
        Point@(Int|1, Int|0),
        Point@(Int|0, Int|2),
        Point@(Int|1, Int|2), Color.Red)
    ];

    # Verify there is no gap between the two triangles.
    Drawn@ wnt = Drawn[
      Pixel@(Int|0, Int|0, Color.Green),
      Pixel@(Int|0, Int|1, Color.Red)
    ];
    Drawn@ got = Draw(drawing);
      
    /Drawing/Test%.AssertEquals(wnt, got);
  }))
]);

@(Tests);
