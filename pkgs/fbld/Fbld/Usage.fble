
<<@>@>@ Monad@ = /Core/Monad%.Monad@;

% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;

@ Loc@ = /Fbld/Loc%.Loc@;
@ StringL@ = /Fbld/StringL%.StringL@;

<<@>@ M@>(Monad@<M@> m) {
  @ MRU@ = M@</Fbld/Result%.Result@<Unit@>>;

  % MarkupModule = /Fbld/Markup%(m);
  @ Markup@ = MarkupModule.Markup@;
  % Entry = MarkupModule.Entry;
  % Block = MarkupModule.Block;

  # Usage document interface.
  @ Usage@ = *(
    # @usage[INLINE name][INLINE brief][BLOCK contents]
    # Top level tag for usage documents.
    # @param name  The name of the program.
    # @param brief  A very brief description of the program.
    # @param content  More usage and core tags describing the program.
    #
    # The following top level blocks are recommended for use in content in this
    # order:
    #   @synopsis
    #   @description
    #   @options
    #   @exitstatus
    #   @examples
    #
    # Each of these are treated as separate sections of the document.
    (Loc@, MRU@, MRU@, MRU@) { MRU@; } usage,

    # @synopsis[INLINE text]
    # Synopsis for how to run a command.
    # @param text  Command synopsis.
    (Loc@, MRU@) { MRU@; } synopsis,

    # @description[BLOCK body]
    # A description of the command.
    # @param body  The text of the description.
    (Loc@, MRU@) { MRU@; } description,

    # @options[BLOCK body]
    # A section describing the command line options.
    # Use the @opt tag to describe an option.
    # @param body  A description of the command line options.
    (Loc@, MRU@) { MRU@; } options,

    # @opt[INLINE opt][INLINE desc]
    # Describes a command line option.
    # @param opt  The option flags and arguments.
    # @param desc  A description of the option.
    (Loc@, MRU@, MRU@) { MRU@; } opt,

    # @exitstatus[BLOCK body]
    # A section describing the exit status of the command.
    # @param body  A description of the exit status.
    (Loc@, MRU@) { MRU@; } exitstatus,

    # @examples[BLOCK body]
    # A section giving example uses of the command.
    # Use @ex to give individual examples.
    # @param body  Example command line uses.
    (Loc@, MRU@) { MRU@; } examples,

    # @ex[ESCAPED text][BLOCK desc]
    # Gives an example command line use.
    # @param text  The sample command line.
    # @param desc  A description of the command line.
    (Loc@, StringL@, MRU@) { MRU@; } ex
  );

  % Utils = /Fbld/Invoke/Utils%(m);

  (Usage@) { Markup@; } Markup = (Usage@ usage) {
    Block[
      Entry(Str|'usage', Utils.LIIB(usage.usage)),
      Entry(Str|'synopsis', Utils.LI(usage.synopsis)),
      Entry(Str|'description', Utils.LB(usage.description)),
      Entry(Str|'options', Utils.LB(usage.options)),
      Entry(Str|'opt', Utils.LII(usage.opt)),
      Entry(Str|'exitstatus', Utils.LB(usage.exitstatus)),
      Entry(Str|'examples', Utils.LB(usage.examples)),
      Entry(Str|'ex', Utils.LEB(usage.ex))
    ];
  };

  @(Usage@, Markup);
};
