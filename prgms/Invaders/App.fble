
@ NativeApp@ = /App%.NativeApp@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;
% RunNative = /App%.RunNative;

@ Color@ = /Drawing%.Color@;
@ Point@ = /Drawing%.Point@;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

@ Int@ = /Int/Int%.Int@;
% 0 = /Int/Int%.0;
% Neg = /Int/Int%.Neg;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

% Game = /Invaders/Game%;
@ Game@ = Game.Game@;

@ Dir@ = /Invaders/Ship%.Dir@;

@ Input@ = +(
  Unit@ tick,
  Unit@ quit,
  Dir@ dir
);

(Get@<Event@>) { Get@<Input@>; } GetInput = (Get@<Event@> in) {
  Event@ e := in;
  e.?(
      tick: !(Input@(tick: Unit)),
      key_down: e.key_down.?(
        q: !(Input@(quit: Unit)),
        left: !(Input@(dir: Dir@(left: Unit))),
        right: !(Input@(dir: Dir@(right: Unit))),
        : GetInput(in)),
      key_up: e.key_up.?(
        left: !(Input@(dir: Dir@(stopped: Unit))),
        right: !(Input@(dir: Dir@(stopped: Unit))),
        : GetInput(in))
   );
};

(Get@<Input@>, Put@<Game@>, Unit@!, Game@) { Unit@!; }
Run = (Get@<Input@> in, Put@<Game@> draw, Unit@! tick, Game@ game) {
  Input@ i := in;
  i.?(
     tick: {
       Unit@ _ := tick;
       Game@ ngame = Game.Tick(game);
       Unit@ _ := draw(ngame);
       Run(in, draw, tick, ngame);
     },
     quit: !(Unit),
     dir: Run(in, draw, tick, Game.Input(game, i.dir)));
};

NativeApp@ Main = RunNative((Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  # Map game coordinates into screen coordinate.
  Point@ a = @(x: Int|1, y: Neg(Int|1));
  Point@ b = @(x: Int|0, y: Int|480);

  # The target is 60 FPS, or 16 millseconds per frame.
  #Int@ frame_period = Int|16;
  Int@ frame_period = Int|1000;
  Unit@! tick = out(Effect@(tick: frame_period));

  (Game@) { Unit@!; } draw = (Game@ g) {
    # Note: we don't just use out(...) here because of a bug in profiling tail
    # calls of PUT values.
    Unit@ _ := out(Effect@(draw: Over(
      Rect(0, 0, width, height, Color@(black: Unit)),
      Affine(a, b, Game.Draw(g)))));
    !(Unit);
  };

  Game@ game = Game.Initial;
  Unit@ _ := out(Effect@(tick: Int|0));
  Run(GetInput(in), draw, tick, game); 
});

Main;
