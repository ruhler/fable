
@ App@ = /App%.App@;
@ AppIO@ = /App%.AppIO@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

@ Point@ = /Drawing%.Point@;
% Color = /Drawing%.Color;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

@ Int@ = /Int%.Int@;
% 0 = /Int%.0;
% Neg = /Int%.Neg;

% Int = /Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

% Game = /Invaders/Game%;
@ Game@ = Game.Game@;

@ Dir@ = /Invaders/Ship%.Dir@;

@ Input@ = +(
  Unit@ tick,
  Unit@ quit,
  Game.Input@ game
);

Input@ Left = Input@(game: Game.Input@(dir: Dir@(left: Unit)));
Input@ Right = Input@(game: Game.Input@(dir: Dir@(right: Unit)));
Input@ Stopped = Input@(game: Game.Input@(dir: Dir@(stopped: Unit)));
Input@ Fire = Input@(game: Game.Input@(fire: Unit));

(Get@<Event@>) { Get@<Input@>; } GetInput = (Get@<Event@> in) {
  Event@ e := in;
  e.?(
      tick: !(Input@(tick: Unit)),
      key_down: e.key_down.?(
        q: !(Input@(quit: Unit)),
        left: !(Left),
        right: !(Right),
        space: !(Fire),
        : GetInput(in)),
      key_up: e.key_up.?(
        left: !(Stopped),
        right: !(Stopped),
        : GetInput(in))
   );
};

(Get@<Input@>, Put@<Game@>, Unit@!, Game@) { Unit@!; }
Run = (Get@<Input@> in, Put@<Game@> draw, Unit@! tick, Game@ game) {
  Input@ i := in;
  i.?(
     tick: {
       Unit@ _ := tick;
       Game@ ngame = Game.Tick(game);
       Unit@ _ := draw(ngame);
       Run(in, draw, tick, ngame);
     },
     quit: !(Unit),
     game: Run(in, draw, tick, Game.Input(game, i.game)));
};

App@ Main = (AppIO@ app) {
  # Map game coordinates into screen coordinate.
  Point@ a = @(x: Int|2, y: Neg(Int|2));
  Point@ b = @(x: Int|0, y: Int|480);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = app.effect(Effect@(tick: frame_period));

  (Game@) { Unit@!; } draw = (Game@ g) {
    app.effect(Effect@(draw: Over(
      Rect(0, 0, app.width, app.height, Color.Black),
      Affine(a, b, Game.Draw(g)))));
  };

  Game@ game = Game.Initial;
  Unit@ _ := app.effect(Effect@(tick: Int|0));
  Run(GetInput(app.event), draw, tick, game); 
};

Main;
