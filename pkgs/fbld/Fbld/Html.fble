
@ Char@ = /Core/Char%.Char@;

<@>% Append = /Core/List%.Append;
<@>% Reverse = /Core/List%.Reverse;
<@>% Drop = /Core/List/Length%.Drop;
<@>% Length = /Core/List/Length%.Length;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% StartsWith = /Core/String/Eq%.StartsWith;

@ Core@ = /Fbld/Core%.Core@;

<@>@ Fbld@ = /Fbld/Fbld%.Fbld@;
<@>% Return = /Fbld/Fbld%.Return;

% Cat = /Fbld/Fbld/Utils%.Cat;
% NL = /Fbld/Fbld/Utils%.NL;
% S = /Fbld/Fbld/Utils%.S;

@ Loc@ = /Fbld/Loc%.Loc@;

@ Text@ = /Fbld/Text%.Text@;
% Text = /Fbld/Text%.Text;
% LocOf = /Fbld/Text%.LocOf;
% StringOf = /Fbld/Text%.StringOf;

(Loc@, Text@) { Fbld@<Text@>; }
HtmlText = (Loc@ loc, Text@ str) {
  (Text@, String@) { Fbld@<Text@>; } text = (Text@ r, String@ s) {
    s.?(nil: Return(r));
    Char@ c = s.cons.head;
    String@ nc = c.?(
      '&': Str|'&amp;',
      '<': Str|'&lt;',
      '>': Str|'&gt;',
      : Str[c]);
    text(/Fbld/Text%.Append(r, Text(nc, loc)), s.cons.tail);
  };
  text(/Fbld/Text%.Empty(loc), StringOf(str));
};

(Loc@, Text@) { Fbld@<Text@>; }
L = (Loc@ l, Text@ str) {
  Cat(l)[S(l)|'<code>', HtmlText(l, str), S(l)|'</code>'];
};

(Loc@, Text@) { Fbld@<Text@>; }
A = (Loc@ l, Text@ str) {
  Cat(l)[S(l)|'<i>', HtmlText(l, str), S(l)|'</i>'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Label = (Loc@ l, Text@ id, Fbld@<Text@> text) {
  Cat(l)[
    S(l)|'<a id="', HtmlText(l, id), S(l)|'">',
    text, S(l)|'</a>'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Ref = (Loc@ l, Text@ id, Fbld@<Text@> caption) {
  Cat(l)[
    S(l)|'<a href="#', HtmlText(l, id), S(l)|'">',
    caption, S(l)|'</a>'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Url = (Loc@ l, Text@ url, Fbld@<Text@> text) {
  Cat(l)[
    S(l)|'<a href="', HtmlText(l, url), S(l)|'">',
    text, S(l)|'</a>'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
File = (Loc@ l, Text@ file, Fbld@<Text@> text) {
  Cat(l)[
    S(l)|'<a href="', HtmlText(l, file), S(l)|'"><code>',
    text, S(l)|'</code></a>'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Fbld = (Loc@ l, Text@ file, Fbld@<Text@> text) {
  # Convert filename from *.fbld to *.html
  String@ replaced = {
    String@ reversed = Reverse(StringOf(file));
    String@ fbld = Reverse(Str|'.fbld');
    StartsWith(reversed, fbld).?(false: replaced);
    String@ tail = Drop(Length(fbld), reversed);
    Append(Reverse(Str|'.html'), tail);
  };
  Text@ fbld = Text(Reverse(replaced), LocOf(file));

  File(l, fbld, text);
};

(Loc@, Fbld@<Text@>) { Fbld@<Text@>; }
Par = (Loc@ l, Fbld@<Text@> body) {
  Cat(l)[S(l)|'<p>', body, S(l)|'</p>', NL(l)];
};

(Loc@, Fbld@<Text@>) { Fbld@<Text@>; }
Item = (Loc@ l, Fbld@<Text@> body) {
  Cat(l)[S(l)|'<ul><li>', body, S(l)|'</li></ul>', NL(l)];
};

(Loc@, Text@, Text@) { Fbld@<Text@>; }
Code = (Loc@ l, Text@ lang, Text@ text) {
  Cat(l)[S(l)|'<pre>', HtmlText(l, text), S(l)|'</pre>', NL(l)];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Def = (Loc@ l, Fbld@<Text@> name, Fbld@<Text@> value) {
  Cat(l)[
    S(l)|'<dl><dt>', name,
    S(l)|'</dt><dd>', value,
    S(l)|'</dd></dl>', NL(l)];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Section = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[S(l)|'<h2>', title, S(l)|'</h2>', NL(l), body];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
SubSection = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[S(l)|'<h3>', title, S(l)|'</h3>', NL(l), body];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
SubSubSection = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[S(l)|'<h4>', title, S(l)|'</h4>', NL(l), body];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Doc = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[
    S(l)|'
<head>
<style>
body {
color: #333333;
font-family: sans-serif;
max-width: 50em;
}
h1 { font-size: 2.0em; }
h2 { font-size: 1.6em; }
h3 { font-size: 1.4em; }
h4 { font-size: 1.2em; }
p { font-size: 1em; }
</style>
<title>', title, S(l)|'</title>', NL(l),
  S(l)|'</head><body>', NL(l),
  S(l)|'<h1>', title, S(l)|'</h1>', NL(l),
  body,
  S(l)|'</body>', NL(l)];
};

Core@ Core = @(
  text: HtmlText,
  l: L, a: A,
  label: Label, ref: Ref,
  url: Url, file: File, fbld: Fbld,
  par: Par,
  item: Item,
  code: Code,
  def: Def,
  definition: Def,
  section: Section,
  subsection: SubSection,
  subsubsection: SubSubSection,
  doc: Doc
);

@(Core);
