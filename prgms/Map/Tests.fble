
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
% And = /Core/Bool%.And;
% Or = /Core/Bool%.Or;
@ Char@ = /Char%.Char@;
<@>@ Eq@ = /Eq%.Eq@;
<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Append = /Core/List%.Append;
<@>% Nil = /Core/List%.Nil;
<@>% Cons = /Core/List%.Cons;
<@>@ Maybe@ = /Maybe%.Maybe@;
<@>% Just = /Maybe%.Just;
<@>% Nothing = /Maybe%.Nothing;
<@>@ Show@ = /Show%.Show@;
@ String@ = /String%.String@;
% Str = /String%.Str;
@ Test@ = /Core/Test%.Test@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;
<@>% AssertEquals = /Core/Test%.AssertEquals;
@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Bit1@ = +(Unit@ 0, Unit@ 1);
@ Bit2@ = *(Bit1@ h, Bit1@ l);

Bit2@ A = Bit2@(Bit1@(0: Unit), Bit1@(0: Unit));
Bit2@ B = Bit2@(Bit1@(0: Unit), Bit1@(1: Unit));
Bit2@ C = Bit2@(Bit1@(1: Unit), Bit1@(0: Unit));

Eq@<Bit1@> EqBit1 = (Bit1@ a, Bit1@ b) {
  a.?(
    0: b.?(0: True, 1: False),
    1: b.?(0: False, 1: True));
};

Eq@<Bit1@> LtBit1 = (Bit1@ a, Bit1@ b) {
  a.?(0: b.?(0: False, 1: True), 1: False);
};

Show@<Bit1@> ShowBit1 = (Bit1@ x) {
  x.?(0: Str|0, 1: Str|1);
};

Eq@<Bit2@> EqBit2 = (Bit2@ a, Bit2@ b) {
  And(EqBit1(a.h, b.h), EqBit1(a.l, b.l));
};

Eq@<Bit2@> LtBit2 = (Bit2@ a, Bit2@ b) {
  Or(LtBit1(a.h, b.h), And(EqBit1(a.h, b.h), LtBit1(a.l, b.l)));
};

Show@<Bit2@> ShowBit2 = (Bit2@ x) {
  Append<Char@>(ShowBit1(x.h), ShowBit1(x.l));
};

<@>@ Map@ = /Map%.Map@<Bit2@>;
<@>% Empty = /Map%.Empty<Bit2@>;
<@>% Lookup = /Map%.Lookup<Bit2@>(LtBit2);
<@>% Insert = /Map%.Insert<Bit2@>(LtBit2);
<@>% Delete = /Map%.Delete<Bit2@>(LtBit2);
<@,@>% ForEach = /Map%.ForEach<Bit2@>;

Test@ Tests = TestSuite(Str|Map, List<Test@>[
  Test(Str|'empty', !({
    Maybe@<Unit@> wnt = Nothing<Unit@>;
    Maybe@<Unit@> got = Lookup<Unit@>(Empty<Unit@>, B);
    AssertEquals<Maybe@<Unit@>>(
      /Maybe/Eq%.Eq<Unit@>(/Core/Unit/Eq%.Eq),
      /Maybe/Show%.Show<Unit@>(/Core/Unit/Show%.Show),
      wnt, got);
  })),

  Test(Str|'not found', !({
    Map@<Unit@> map = Insert<Unit@>(Empty<Unit@>, C, Unit);
    Maybe@<Unit@> wnt = Nothing<Unit@>;
    Maybe@<Unit@> got = Lookup<Unit@>(map, B);
    AssertEquals<Maybe@<Unit@>>(
      /Maybe/Eq%.Eq<Unit@>(/Core/Unit/Eq%.Eq),
      /Maybe/Show%.Show<Unit@>(/Core/Unit/Show%.Show),
      wnt, got);
  })),

  Test(Str|'found', !({
    Map@<Bool@> map = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(Empty<Bool@>,
      A, False),
      B, True),
      C, False);
    Maybe@<Bool@> wnt = Just<Bool@>(True);
    Maybe@<Bool@> got = Lookup<Bool@>(map, B);
    AssertEquals<Maybe@<Bool@>>(
      /Maybe/Eq%.Eq<Bool@>(/Core/Bool/Eq%.Eq),
      /Maybe/Show%.Show<Bool@>(/Core/Bool/Show%.Show),
      wnt, got);
  })),

  Test(Str|'replace', !({
    Map@<Bool@> map = Insert<Bool@>(Insert<Bool@>(Empty<Bool@>,
      B, False),
      B, True);
    Maybe@<Bool@> wnt = Just<Bool@>(True);
    Maybe@<Bool@> got = Lookup<Bool@>(map, B);
    AssertEquals<Maybe@<Bool@>>(
      /Maybe/Eq%.Eq<Bool@>(/Core/Bool/Eq%.Eq),
      /Maybe/Show%.Show<Bool@>(/Core/Bool/Show%.Show),
      wnt, got);
  })),

  Test(Str|'delete', !({
    Map@<Bool@> map = Delete<Bool@>(Insert<Bool@>(Empty<Bool@>, B, False), B);
    Maybe@<Bool@> wnt = Nothing<Bool@>;
    Maybe@<Bool@> got = Lookup<Bool@>(map, B);
    AssertEquals<Maybe@<Bool@>>(
      /Maybe/Eq%.Eq<Bool@>(/Core/Bool/Eq%.Eq),
      /Maybe/Show%.Show<Bool@>(/Core/Bool/Show%.Show),
      wnt, got);
  })),

  Test(Str|'foreach', !({
    Map@<Bool@> map = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(Empty<Bool@>,
      B, True),
      A, True),
      B, False);

    # Note: A < B, we expect the ForEach to iterate in the order A, B. Because
    # we add A to the end of the list before B, we expect the resulting list
    # to be in the order B, A.
    @ Entry@ = *(Bit2@ key, Bool@ value);
    List@<Entry@> wnt = List<Entry@>[Entry@(B, False), Entry@(A, True)];
    List@<Entry@> got = ForEach<Bool@, List@<Entry@>>(map, Nil<Entry@>,
      (Bit2@ k, Bool@ v, List@<Entry@> l) {
        Cons<Entry@>(Entry@(k, v), l);
      });
    AssertEquals<List@<Entry@>>(
      /Core/List/Eq%.Eq<Entry@>((Entry@ a, Entry@ b) {
        And(EqBit2(a.key, b.key), /Core/Bool/Eq%.Eq(a.value, b.value));
      }),
      /Core/List/Show%.Show<Entry@>((Entry@ e) {
        /String%.Strs[ShowBit2(e.key), Str|': ', /Core/Bool/Show%.Show(e.value)];
      }),
      wnt, got);
  })),

  Test(Str|'eq: equal', !({
    Map@<Bool@> a = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, False), C, False);
    Map@<Bool@> b = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, False), C, False);

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: value mismatch', !({
    Map@<Bool@> a = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, True), C, False);
    Map@<Bool@> b = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, False), C, False);

    Bool@ wnt = False;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: different number of elements', !({
    Map@<Bool@> a = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, False), C, False);
    Map@<Bool@> b = Insert<Bool@>(Insert<Bool@>(Empty<Bool@>, A, True), B, False);

    Bool@ wnt = False;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: deleted to empty', !({
    Map@<Bool@> a = Delete<Bool@>(Insert<Bool@>(Empty<Bool@>, A, True), A);
    Map@<Bool@> b = Empty<Bool@>;

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: empty from deleted', !({
    Map@<Bool@> a = Empty<Bool@>;
    Map@<Bool@> b = Delete<Bool@>(Insert<Bool@>(Empty<Bool@>, A, True), A);

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: empty from deleted pair second', !({
    Map@<Bool@> a = Insert<Bool@>(Empty<Bool@>, A, True);
    Map@<Bool@> b = Delete<Bool@>(Insert<Bool@>(a, B, True), B);

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: empty from deleted pair first', !({
    Map@<Bool@> a = Insert<Bool@>(Empty<Bool@>, A, True);
    Map@<Bool@> b = Delete<Bool@>(Insert<Bool@>(a, C, True), C);

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'eq: equal any order', !({
    Map@<Bool@> a = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, B, False), C, False), A, True);
    Map@<Bool@> b = Insert<Bool@>(Insert<Bool@>(Insert<Bool@>(
          Empty<Bool@>, A, True), B, False), C, False);

    Bool@ wnt = True;
    Bool@ got = /Map/Eq%.Eq<Bit2@, Bool@>(EqBit2, /Core/Bool/Eq%.Eq)(a, b);
    AssertEquals<Bool@>(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })),

  Test(Str|'Show empty', !({
    Map@<Bool@> map = Empty<Bool@>;
    String@ wnt = Str|'{ }';
    String@ got = /Map/Show%.Show<Bit2@, Bool@>(ShowBit2, /Core/Bool/Show%.Show)(map);
    AssertEquals<String@>(/String/Eq%.Eq, /String/Show%.Show, wnt, got);
  })),

  Test(Str|'Show single', !({
    Map@<Bool@> map = Insert<Bool@>(Empty<Bool@>, B, False);
    String@ wnt = Str|'{ 01 => False; }';
    String@ got = /Map/Show%.Show<Bit2@, Bool@>(ShowBit2, /Core/Bool/Show%.Show)(map);
    AssertEquals<String@>(/String/Eq%.Eq, /String/Show%.Show, wnt, got);
  })),

  Test(Str|'Show multi', !({
    Map@<Bool@> map = Insert<Bool@>(Insert<Bool@>(Empty<Bool@>, B, False), C, True);
    String@ wnt = Str|'{ 01 => False; 10 => True; }';
    String@ got = /Map/Show%.Show<Bit2@, Bool@>(ShowBit2, /Core/Bool/Show%.Show)(map);
    AssertEquals<String@>(/String/Eq%.Eq, /String/Show%.Show, wnt, got);
  }))
]);

@(Tests);
