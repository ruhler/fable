
@ Decimal@ = /Core/Digits%.Decimal@;

@ Drawing@ = /App/Drawing%.Drawing@;
% Blank = /App/Drawing%.Blank;
% Drawings = /App/Drawing%.Drawings;
% Translate = /App/Drawing%.Translate;

@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Sub = /Core/Int%.Sub;
% Div = /Core/Int/Div%.Div;
% Eq = /Core/Int/Eq%.Eq;
% Gt = /Core/Int/Eq%.Gt;
% LeastSignificantDigit = /Core/Int/Digits%.LeastSignificantDigit;

% Int = /Core/Int/Lit%.Int;

% Graphics = /Invaders/Graphics%;

% Aliens = /Invaders/Aliens%;
@ Aliens@ = Aliens.Aliens@;

% Bases = /Invaders/Bases%;
@ Bases@ = Bases.Bases@;

% Bombs = /Invaders/Bombs%;
@ Bombs@ = Bombs.Bombs@;

% Bullet = /Invaders/Bullet%;
@ Bullet@ = Bullet.Bullet@;

% Ship = /Invaders/Ship%;
@ Dir@ = Ship.Dir@;
@ Ship@ = Ship.Ship@;

% Ufo = /Invaders/Ufo%;
@ Ufo@ = Ufo.Ufo@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

@ String@ = /Core/String%.String@;
% Strs = /Core/String%.Strs;
% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Lives@ = +(Unit@ 0, Unit@ 1, Unit@ 2, Unit@ 3);

# GameOver@ --
#   The game over animation.
#
# Fields:
#   hidden - the tail parts of the string "GAME OVER" that aren't yet visible.
#   visible - the front parts of the string "GAME OVER" that are visible.
#   wait - how many ticks to wait before showing the next character.
@ GameOver@ = *(String@ hidden, String@ visible, Int@ wait);

# GameOverWait --
#   The number of ticks between revealing of letters in the game over
#   animation.
Int@ GameOverWait = Int|28;

# DyingWait --
#   How long the ship spends dying.
Int@ DyingWait = Int|120;

# State@ --
#   The overall game state.
@ State@ = +(
  Unit@ defending,   # Player is actively defending against the alien attack.
  Int@ dying,        # Play ship dying animation this many ticks.
  Int@ leveling,     # Wait this many ticks to start next level.
  GameOver@ over     # The game is over.
);

Int@ LevelingTime = Int|90;

State@ GameOver = State@(over: GameOver@(Str|'GAME OVER', Str|'', GameOverWait));
State@ Dying = State@(dying: DyingWait);

@ Game@ = *(
  Int@ score,
  Int@ level,
  Lives@ lives,
  State@ state,
  Ship@ ship,
  Bullet@ bullet,
  Bases@ bases,
  Aliens@ aliens,
  Bombs@ bombs,
  Ufo@ ufo
);

@ Input@ = +(Dir@ dir, Unit@ fire);

Game@ Initial = @(
  score: Int|0,
  level: Int|0,
  lives: Lives@(3: Unit),
  state: State@(defending: Unit),
  ship: Ship.Initial,
  bullet: Bullet.Initial,
  bases: Bases.Initial(Int|0),
  aliens: Aliens.Initial(Int|0),
  bombs: Bombs.Initial,
  ufo: Ufo.Initial
);

(GameOver@) { GameOver@; } TickGameOver = (GameOver@ over) {
  Gt(over.wait, Int|0).?(true: {
    GameOver@(over.hidden, over.visible, Sub(over.wait, Int|1));
  });

  over.hidden.?(nil: over);

  GameOver@(over.hidden.cons.tail,
    Strs[over.visible, Str[over.hidden.cons.head]],
    GameOverWait);
};

(Game@) { Game@; } Tick = (Game@ g) {
  g.state.?(over: {
    g.@(state: State@(over: TickGameOver(g.state.over)));
  });

  g.state.?(leveling: {
    Eq(Int|0, g.state.leveling).?(true: {
      Int@ level = Add(Int|1, g.level);
      g.@(level,
        state: State@(defending: Unit), ship: Ship.Initial,
        bullet: Bullet.Initial, bases: Bases.Initial(level),
        aliens: Aliens.Initial(level), bombs: Bombs.Initial);
    });

    g.@(state: State@(leveling: Sub(g.state.leveling, Int|1)));
  });

  g.state.?(dying: {
    Eq(Int|0, g.state.dying).?(true: {
      g.lives.?(0: g.@(state: GameOver));

      g.@(
        state: State@(defending: Unit),
        ship: Ship.Revive(g.ship),
        bullet: Bullet.Initial,
        bombs: Bombs.Initial);
    });

    g.@(
      state: State@(dying: Sub(g.state.dying, Int|1)),
      ship: Ship.Tick(g.ship));
  });

  Game@ g_ = g.@(
    ship: Ship.Tick(g.ship),
    bullet: Bullet.Tick(g.bullet),
    aliens: Aliens.Tick(g.aliens),
    bombs: Bombs.Tick(g.bombs),
    ufo: Ufo.Tick(g.ufo));
  Game@ g = g_;

  # Check for bullet colliding with an alien.
  Game@ g_ = {
    g.bullet.live.?(false: g);
    Maybe@<Aliens.Kill@> kill = Aliens.Shoot(g.bullet.x, g.bullet.y, g.aliens);
    kill.?(nothing: g);
    g.@(score: Add(kill.just.points, g.score),
        bullet: Bullet.Initial,
        aliens: kill.just.aliens);
  };
  Game@ g = g_;

  # Check for bullet colliding with ufo.
  Game@ g_ = {
    g.bullet.live.?(false: g);
    Maybe@<Ufo.Kill@> kill = Ufo.Shoot(g.bullet.x, g.bullet.y, g.ufo);
    kill.?(nothing: g);
    g.@(
      score: Add(kill.just.points, g.score),
      bullet: Bullet.Initial,
      ufo: kill.just.ufo);
  };
  Game@ g = g_;

  # Check if all the aliens are dead.
  Game@ g_ = {
    Aliens.AllDead(g.aliens).?(false: g);
    g.@(state: State@(leveling: LevelingTime));
  };
  Game@ g = g_;

  # Check for bombs colliding with bases.
  Bombs.HitBasesResult@ hit_bases = Bombs.HitBases(g.bombs, g.bases);
  Game@ g_ = g.@(
    bases: hit_bases.bases,
    bombs: hit_bases.bombs);
  Game@ g = g_;

  # Check for bullet colliding with bases.
  Game@ g_ = {
    g.bullet.live.?(false: g);
    Bases.HitResult@ hit = Bases.BulletHit(
      g.bullet.x, Add(Bullet.Height, g.bullet.y), g.bases);
    hit.hit.?(false: g);
    g.@(bullet: Bullet.Initial, bases: hit.bases);
  };
  Game@ g = g_;

  # Check for a bomb colliding with the ship.
  Bombs.HitResult@ hit = Bombs.HitShip(g.bombs, g.ship.x);
  hit.hit.?(true: {
    Lives@ lives = g.lives.?(
      0: Lives@(0: Unit),
      1: Lives@(0: Unit),
      2: Lives@(1: Unit),
      3: Lives@(2: Unit));
    g.@(lives, state: Dying, ship: Ship.Kill(g.ship), bombs: hit.bombs);
  });

  # Fire bombs
  Game@ g_ = g.@(bombs: Aliens.FireBombs(g.aliens, g.bombs));
  Game@ g = g_;

  # Check if the aliens have invaded.
  Game@ g_ = {
    Aliens.Invaded(g.aliens).?(false: g);
    g.@(state: GameOver);
  };
  Game@ g = g_;

  g;
};

(Game@, Input@) { Game@; } Input = (Game@ g, Input@ input) {
  g.state.?(defending: {
    Game@(g.score, g.level, g.lives, g.state,
      input.?(dir: Ship.Input(g.ship, input.dir), : g.ship),
      input.?(fire: Bullet.Fire(g.bullet, g.ship), : g.bullet),
      g.bases, g.aliens, g.bombs, g.ufo);
  });

  g;
};

(Int@) { Drawing@; } DrawScore = {
  Int@ 10 = Int|10;

  (Int@ score) {
    # We only print 4 digits for the score.
    Int@ s1000 = score;
    Int@ s100 = Div(s1000, 10).q;
    Int@ s10 = Div(s100, 10).q;
    Int@ s1 = Div(s10, 10).q;
    Decimal@ d0 = LeastSignificantDigit(s1000);
    Decimal@ d1 = LeastSignificantDigit(s100);
    Decimal@ d2 = LeastSignificantDigit(s10);
    Decimal@ d3 = LeastSignificantDigit(s1);
    Translate(Int|40, Int|220, Graphics.Digits[d3, d2, d1, d0]);
  };
};

(Lives@) { Drawing@; } DrawLives = {
  Drawing@ l1 = Translate(Int|262, Int|220, Graphics.Ship);
  Drawing@ l2 = Translate(Int|242, Int|220, Graphics.Ship);
  Drawing@ l3 = Translate(Int|222, Int|220, Graphics.Ship);
  (Lives@ l) {
    l.?(
      0: Blank,
      1: Drawings[l1],
      2: Drawings[l1, l2],
      3: Drawings[l1, l2, l3]);
  };
};

(Game@) { Drawing@; } Draw = (Game@ g) {
  Drawing@ over = g.state.?(
    over: Translate(Int|120, Int|184, Graphics.String(g.state.over.visible)),
    : Blank);

  Drawings[
    DrawScore(g.score),
    DrawLives(g.lives),
    Graphics.Background,
    Ship.Draw(g.ship),
    Bullet.Draw(g.bullet),
    Bases.Draw(g.bases),
    Aliens.Draw(g.aliens),
    Bombs.Draw(g.bombs),
    Ufo.Draw(g.ufo),
    over
  ];
};

@(Game@, Input@, Initial, Input, Tick, Draw);

