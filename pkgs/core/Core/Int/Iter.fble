
@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Sub = /Core/Int%.Sub;
% Le = /Core/Int/Eq%.Le;
% Lt = /Core/Int/Eq%.Lt;

% Int = /Core/Int/Lit%.Int;

<@>@ Iter@ = /Core/Iter%.Iter@;
<@>% Has = /Core/Iter%.Has;
<@>% End = /Core/Iter%.End;

@ Unit@ = /Core/Unit%.Unit@;

Int@ 0 = Int|0;
Int@ 1 = Int|1;

# Range --
#   An iterator over the range of integers [lo, hi).
(Int@, Int@) { Iter@<Int@>; } Range = (Int@ lo, Int@ hi)(Unit@ _) {
  Lt(lo, hi).?(false: End<Int@>);
  Has(lo, Range(Add(lo, 1), hi));
};

# CountUpTo --
#   An iterator over the range of integers [0, n).
(Int@) { Iter@<Int@>; } CountUpTo = (Int@ n) {
  Range(0, n);
};

# CountDownFrom --
#   An iterator over the range of integers (n, 0]
(Int@) { Iter@<Int@>; } CountDownFrom = (Int@ n)(Unit@ _) {
  Le(n, 0).?(true: End<Int@>);
  Int@ first = Sub(n, 1);
  Has(first, CountDownFrom(first));
};

@(Range, CountUpTo, CountDownFrom);

