
@ Int@ = /Int/Int%.Int@;
% Add = /Int/Int%.Add;
% Int = /Int/Int/Lit%.Int;

<@>@ List@ = /List%.List@;
<@>% List = /List%.List;

@ Program@ = /Fblf/Program%.Program@;
@ Addr@ = /Fblf/Program%.Addr@;
@ Env@ = /Fblf/Program%.Env@;
% EmptyEnv = /Fblf/Program%.EmptyEnv;
% Seq = /Fblf/Program%.Seq;
% Noop = /Fblf/Program%.Noop;

# Instance@, Module@ --
#   Module@ is a monad for tracking program environment and allocation of
#   objects on a heap. It is implemented as a state monad.
#
#   Instance@ is the part of the Module@ monad that represents an instantiated
#   Module@.
<@>@ Instance@ = <@ T@> { *(Env@ env, Addr@ heap, T@ instance); };
<@>@ Module@ = <@ T@>(Env@, Addr@) { Instance@<T@>; };

# AllocM --
#   Allocate n bits on the heap.
#
# Example use:
#   Addr@ x <- AllocM(I(D|32));
#   ...
<@ T@>(Int@)((Addr@) { Module@<T@>; }) { Module@<T@>; }
AllocM = <@ T@>(Int@ n)((Addr@) { Module@<T@>; } f) {
  (Env@ env, Addr@ free) {
    f(free)(env, Add(free, n));
  };
};

# InstM --
#   Instantiate a module for use in another module.
#
# TODO: Re-work this API or add other APIs to allow us to start using the
# program environment for programs.
#
# Example use:
#   Program@ p <- InstM(m);
#   ...
<@ A@, @ B@>(Module@<A@>)((A@) { Module@<B@>; }) { Module@<B@>; }
InstM = <@ A@, @ B@>(Module@<A@> m)((A@) { Module@<B@>; } f) {
  (Env@ env, Addr@ free) {
    Instance@<A@> mr = m(env, free);
    f(mr.instance)(mr.env, mr.heap);
  };
};

# PureM --
#   A module that does not need to allocate space on the heap or instantiate
#   other modules.
<@ T@>(T@) { Module@<T@>; } PureM = <@ T@>(T@ instance) {
  (Env@ env, Addr@ free) { Instance@<T@>(env, free, instance); };
};

# SeqM --
#   Instantite a list of modules.
#
# TODO: generalize this to arbitrary T@?
(List@<Module@<Program@>>) { Module@<Program@>; } SeqM = (List@<Module@<Program@>> ms) {
  ms.?(
    cons: {
      Program@ a <- InstM<Program@, Program@>(ms.cons.head);
      Program@ b <- InstM<Program@, Program@>(SeqM(ms.cons.tail));
      PureM<Program@>(Seq(List<Program@>[a, b]));
    },
    nil: PureM<Program@>(Noop));
};

# Instantiate a module, with the first object allocated at address 0.
<@ T@>(Module@<T@>) { Instance@<T@>; }
Instantiate = <@ T@>(Module@<T@> m) {
  m(EmptyEnv, Int|0);
};

@(Module@, Instance@, AllocM, InstM, PureM, SeqM, Instantiate);
