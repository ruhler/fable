
{ /Bool%; @(Bool@, True, False); };
{ /Int/Int%; @(Int@); };
{ /Int/Nat/Bool%;
  @(EqNat: Eq, LtNat: Lt, LeNat: Le, GtNat: Gt, GeNat: Ge);
};

(Int@) { Bool@; } IsZero = (Int@ x) {
  ?(x; 0: True, : False);
};

(Int@, Int@){ Bool@; } Eq = (Int@ a, Int@ b) {
  ?(a;
      n: ?(b; n: EqNat(a.n, b.n),  0: False, p: False),
      0: ?(b; n: False, 0: True,  p: False),
      p: ?(b; n: False, 0: False, p: EqNat(a.p, b.p)));
};

(Int@, Int@){ Bool@; } Lt = (Int@ a, Int@ b) {
  ?(a;
      n: ?(b; n: LtNat(b.n, a.n), 0: True, p: True),
      0: ?(b; n: False, 0: False, p: True),
      p: ?(b; n: False, 0: False, p: LtNat(a.p, b.p)));
};

(Int@, Int@){ Bool@; } Le = (Int@ a, Int@ b) {
  ?(a;
      n: ?(b; n: LeNat(b.n, a.n), 0: True, p: True),
      0: ?(b; n: False, 0: True, p: True),
      p: ?(b; n: False, 0: False, p: LeNat(a.p, b.p)));
};

(Int@, Int@){ Bool@; } Gt = (Int@ a, Int@ b) {
  ?(a;
      n: ?(b; n: GtNat(b.n, a.n), 0: False, p: False),
      0: ?(b; n: True, 0: False, p: False),
      p: ?(b; n: True, 0: True, p: GtNat(a.p, b.p)));
};

(Int@, Int@){ Bool@; } Ge = (Int@ a, Int@ b) {
  ?(a;
      n: ?(b; n: GeNat(b.n, a.n), 0: True, p: False),
      0: ?(b; n: True, 0: True, p: False),
      p: ?(b; n: True, 0: True, p: GeNat(a.p, b.p)));
};

@(IsZero, Eq, Lt, Le, Gt, Ge);

