
<@>@ List@ = /Core/List%.List@;
<@>% List = /Core/List%.List;
<@>% Nil = /Core/List%.Nil;
<@>% Append = /Core/List%.Append;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;

% Internal = /Core/Stdio/Internal%;
@ _@ = Internal._@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;

@ PureState@ = *(
  List@<String@> in,
  List@<String@> out,
  List@<String@> err
);

% State = /Core/Monad/State%<PureState@>;

# Pure@ -- A monad for pure Stdio computation.
<@>@ Pure@ = State.State@;
<@>@ PureResult@ = State.R@;

Monad@<Pure@> Monad = State.Monad;

Pure@<Maybe@<String@>> In = {
  PureState@ s <- Monad.do(State.Get);
  s.in.?(nil: Monad.return(Nothing<String@>));
  Unit@ _ <- Monad.do(State.Set(PureState@(s.in.cons.tail, s.out, s.err)));
  Monad.return(Just(s.in.cons.head));
};

(String@) { Pure@<Unit@>; } Out = (String@ str) {
  # TODO: Prepend instead of append string, and fix up later for improved
  # performance?
  PureState@ s <- Monad.do(State.Get);
  State.Set(PureState@(s.in, Append(s.out, List<String@>[str]), s.err));
};

(String@) { Pure@<Unit@>; } Err = (String@ str) {
  # TODO: Prepend instead of append string, and fix up later for improved
  # performance?
  PureState@ s <- Monad.do(State.Get);
  State.Set(PureState@(s.in, s.out, Append(s.err, List<String@>[str])));
};

Stdio@<Pure@> Stdio = _@(Internal.Stdio@<Pure@>(In, Out, Err));

# Run --
#   Run a pure stdio computation.
#
# Inputs:
#   m - The computation to run.
#   in - The list of input strings.
<@ A@>(Pure@<A@>, List@<String@>) { PureResult@<A@>; }
Run = <@ A@>(Pure@<A@> m, List@<String@> in) {
  State.Run(m, PureState@(in, Nil<String@>, Nil<String@>));
};

@(Pure@, PureState@, PureResult@, Monad, Stdio, Run);

