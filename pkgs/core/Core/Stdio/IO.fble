@ Bool@ = /Core/Bool%.Bool@;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<@>@ IO@ = /Core/Monad/IO%.IO@;
@ World@ = /Core/Monad/IO%.World@;
<@>@ R@ = /Core/Monad/IO%.R@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;
<<@>@>@ OStream@ = /Core/Stream%.OStream@;

<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;

@ StdioIO@ = *(
  IO@<Maybe@<Int@>> stdin,
  (Int@, World@) { R@<Unit@>; } stdout,
  (Int@, World@) { R@<Unit@>; } stderr,
  (String@, World@) { R@<Maybe@<IStream@<IO@>>>; } read,
  (String@, World@) { R@<Maybe@<OStream@<IO@>>>; } write,
  (String@, World@) { R@<Maybe@<String@>>; } getenv
);

(StdioIO@) { Stdio@<IO@>; } StdioIO = (StdioIO@ stdio) {
  @(
    in: stdio.stdin,
    out: (Int@ s)(World@ w) { stdio.stdout(s, w); },
    err: (Int@ s)(World@ w) { stdio.stderr(s, w); },
    read: (String@ s)(World@ w) { stdio.read(s, w); },
    write: (String@ s)(World@ w) { stdio.write(s, w); },
    getenv: (String@ s)(World@ w) { stdio.getenv(s, w); }
   );
};

@ Main@ = (StdioIO@, List@<String@>) { IO@<Bool@>; };

# Run --
#   A top-level IO implementation for running a Stdio Main@ program.
(/Core/Stdio%.Main@) { Main@; }
Run = (/Core/Stdio%.Main@ main)(StdioIO@ stdio, List@<String@> args) {
  main<IO@>(/Core/Monad/IO%.Monad, StdioIO(stdio))(args);
};

@(StdioIO@, StdioIO, Run);
