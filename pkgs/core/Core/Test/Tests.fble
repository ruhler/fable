
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>% AssertEquals = /Core/Eq/Assert%.AssertEquals;

@ Int@ = /Core/Int%.Int@;

<@>% List = /Core/List%.List;
<@>% Nil = /Core/List%.Nil;

<@>@ PureResult@ = /Core/Stdio/Pure%.PureResult@;
<@>@ Pure@ = /Core/Stdio/Pure%.Pure@;
% Monad = /Core/Stdio/Pure%.Monad;
% Stdio = /Core/Stdio/Pure%.Stdio;
<@>% Run = /Core/Stdio/Pure%.Run;

% Str = /Core/String%.Str;

@ Test@ = /Core/Test%.Test@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;
% Pass = /Core/Test%.Pass;
% Fail = /Core/Test%.Fail;
% ExpectedFail = /Core/Test%.ExpectedFail;

@ Unit@ = /Core/Unit%.Unit@;

% Stdout = Stdio.out;

% RunTests = /Core/Test/Run%<Pure@>(Monad, Stdout).RunTests;

Test@ pass = Test(Str|pass, (Unit@ _) { Pass; });
Test@ fail = Test(Str|fail, (Unit@ _) { Fail|Failed; });
Test@ xfail = Test(Str|xfail, (Unit@ _) { ExpectedFail; });

Test@ Tests = TestSuite(Str|'Test', List[
  Test(Str|'RunTest.pass_passes', (Unit@ _) {
    PureResult@<Bool@> result = Run(RunTests(Str|'', pass), Nil<Int@>);

    Bool@ wnt = True;
    Bool@ got = result.x;
    AssertEquals(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  }),

  Test(Str|'RunTest.fail_fails', (Unit@ _) {
    PureResult@<Bool@> result = Run(RunTests(Str|'', fail), Nil<Int@>);

    Bool@ wnt = False;
    Bool@ got = result.x;
    AssertEquals(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  }),

  Test(Str|'RunTest.xfail_passes', (Unit@ _) {
    PureResult@<Bool@> result = Run(RunTests(Str|'', xfail), Nil<Int@>);

    Bool@ wnt = True;
    Bool@ got = result.x;
    AssertEquals(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  }),

  Test(Str|'RunTest.SuitePass', (Unit@ _) {
    Test@ suite = TestSuite(Str|'Suite', List[
      Test(Str|a, (Unit@ _) { Pass; }),
      Test(Str|b, (Unit@ _) { Pass; }),
      Test(Str|c, (Unit@ _) { ExpectedFail; }),
      Test(Str|c, (Unit@ _) { Pass; })]);

    PureResult@<Bool@> result = Run(RunTests(Str|'', suite), Nil<Int@>);

    Bool@ wnt = True;
    Bool@ got = result.x;
    AssertEquals(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  }),

  Test(Str|'RunTest.SuiteFail', (Unit@ _) {
    Test@ suite = TestSuite(Str|'Suite', List[
      Test(Str|a, (Unit@ _) { Pass; }),
      Test(Str|b, (Unit@ _) { Fail|Oops; }),
      Test(Str|c, (Unit@ _) { ExpectedFail; }),
      Test(Str|c, (Unit@ _) { Pass; })]);

    PureResult@<Bool@> result = Run(RunTests(Str|'', suite), Nil<Int@>);

    Bool@ wnt = False;
    Bool@ got = result.x;
    AssertEquals(/Core/Bool/Eq%.Eq, /Core/Bool/Show%.Show, wnt, got);
  })
]);

@(Tests);
