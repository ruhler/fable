
@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Decimal@ = /Digits%.Decimal@;
% Decimal = /Digits%.Decimal;

@ Int@ = /Core/Int%.Int@;
% Int = /Core/Int/Lit%.Int;
% Div = /Core/Int/Div%.Div;

<@>% Nothing = /Maybe%.Nothing;

# Digit --
#   Converts an integer in the range [0-9] to a decimal digit.
#
# Behavior is undefined if the integer is not in the range [0-9].
(Int@) { Decimal@; } Digit = {
  (Unit@) { Decimal@; } Undef = (Unit@ _) {
    Nothing<Decimal@>.just;
  };

  (Int@ d) {
    d.?(n: Undef(Unit), 0: Decimal.0, p: d.p.?(
        1: Decimal.1,
        2p0: d.p.2p0.?( 
          1: Decimal.2,
          2p0: d.p.2p0.2p0.?(
            1: Decimal.4,
            2p0: d.p.2p0.2p0.2p0.?(
              1: Decimal.8,
              : Undef(Unit)),
            : Undef(Unit)),
          2p1: d.p.2p0.2p1.?(
            1: Decimal.6,
            : Undef(Unit))),
        2p1: d.p.2p1.?( 
          1: Decimal.3,
          2p0: d.p.2p1.2p0.?(
            1: Decimal.5,
            2p0: d.p.2p1.2p0.2p0.?(
              1: Decimal.9,
              : Undef(Unit)),
            : Undef(Unit)),
          2p1: d.p.2p1.2p1.?(
            1: Decimal.7,
            : Undef(Unit)))));
  };
};

# LeastSignificantDigit --
#   Returns the least sigificant decimal digit of the given integer.
(Int@) { Decimal@; } LeastSignificantDigit = (Int@ x) {
  Digit(Div(x, Int|10).r);
};

@(LeastSignificantDigit);
