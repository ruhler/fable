
% Str = /Core/String%.Str;

@ Core@ = /Fbld/Core%.Core@;

<@>@ Fbld@ = /Fbld/Fbld%.Fbld@;
<@>% Error = /Fbld/Fbld%.Error;

% Cat = /Fbld/Fbld/Utils%.Cat;
% NL = /Fbld/Fbld/Utils%.NL;
% S = /Fbld/Fbld/Utils%.S;
% T = /Fbld/Fbld/Utils%.T;

@ Loc@ = /Fbld/Loc%.Loc@;

@ Text@ = /Fbld/Text%.Text@;


(Loc@, Text@) { Fbld@<Text@>; }
ManText = (Loc@ _, Text@ str) {
  T(str);
};

(Loc@, Text@) { Fbld@<Text@>; }
L = (Loc@ l, Text@ str) {
  Cat(l)[S(l)|'\fB', ManText(l, str), S(l)|'\fR'];
};

(Loc@, Text@) { Fbld@<Text@>; }
A = (Loc@ l, Text@ str) {
  Cat(l)[S(l)|'\fI', ManText(l, str), S(l)|'\fR'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Label = (Loc@ l, Text@ id, Fbld@<Text@> text) {
  text;
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Ref = (Loc@ l, Text@ id, Fbld@<Text@> caption) {
  # TODO: Should we link somehow to the label?
  caption;
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Url = (Loc@ l, Text@ url, Fbld@<Text@> text) {
  # TODO: Can we do something better here?
  Cat(l)[text, S(l)|'(', ManText(l, url), S(l)|')'];
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
File = (Loc@ l, Text@ file, Fbld@<Text@> text) {
  # TODO: Can we do something better here?
  Url(l, file, text);
};

(Loc@, Text@, Fbld@<Text@>) { Fbld@<Text@>; }
Fbld = (Loc@ l, Text@ file, Fbld@<Text@> text) {
  # TODO: Can we do something better here?
  File(l, file, text);
};

(Loc@, Fbld@<Text@>) { Fbld@<Text@>; }
Par = (Loc@ l, Fbld@<Text@> body) {
  Cat(l)[S(l)|'.P', NL(l), body, NL(l)];
};

(Loc@, Fbld@<Text@>) { Fbld@<Text@>; }
Item = (Loc@ l, Fbld@<Text@> body) {
  # TODO: Do something better here.
  Cat(l)[S(l)|'.IP \[bu] 2', body];
};

(Loc@, Text@, Text@) { Fbld@<Text@>; }
Code = (Loc@ l, Text@ lang, Text@ text) {
  Cat(l)[
    S(l)|'.P', NL(l),
    S(l)|'EX', NL(l),
    ManText(l, text), NL(l),
    S(l)|'EE', NL(l)];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Def = (Loc@ l, Fbld@<Text@> name, Fbld@<Text@> value) {
  Cat(l)[
    S(l)|'.TP 4', NL(l),
    name, NL(l),
    value, NL(l)];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Definition = (Loc@ l, Fbld@<Text@> name, Fbld@<Text@> value) {
  Cat(l)[
    S(l)|'.P', NL(l),
    name, NL(l),
    S(l)|'.RS', NL(l),
    value, S(l)|'.RE', NL(l)];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Section = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[S(l)|'.SH "', title, S(l)|'"', NL(l), body];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
SubSection = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  Cat(l)[S(l)|'.SS "', title, S(l)|'"', NL(l), body];
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
SubSubSection = (Loc@ l, Fbld@<Text@> title, Fbld@<Text@> body) {
  # TODO: Do something better here?
  Definition(l, title, body);
};

(Loc@, Fbld@<Text@>, Fbld@<Text@>) { Fbld@<Text@>; }
Doc = (Loc@ loc, Fbld@<Text@> title, Fbld@<Text@> body) {
  Error<Text@>(loc, Str|'@doc not supported by man backend. Use @man instead.');
};

@ Man@ = (Loc@, Fbld@<Text@>, Fbld@<Text@>, Fbld@<Text@>, Fbld@<Text@>) {
  Fbld@<Text@>; };

# @man[INLINE section][INLINE title][INLINE source][BLOCK body]
# Top level entry point for a man page.
# @param section  The section of the man page, e.g. 1 or 3.
# @param title  The title of the document.
# @param source The source of the document.
# @param body  The contents of the document.
Man@ Man = (Loc@ l, Fbld@<Text@> section, Fbld@<Text@> title, Fbld@<Text@> source, Fbld@<Text@> body) {
  Cat(l)[
    S(l)|'.TH "', title, S(l)|'" ', section, S(l)|' "" "', source, NL(l),
    body];
};

Core@ Core = @(
  text: ManText,
  l: L, a: A,
  label: Label, ref: Ref,
  url: Url, file: File, fbld: Fbld,
  par: Par,
  item: Item,
  code: Code,
  def: Def,
  definition: Definition,
  section: Section,
  subsection: SubSection,
  subsubsection: SubSubSection,
  doc: Doc
);

@(Core, Man@, Man);
