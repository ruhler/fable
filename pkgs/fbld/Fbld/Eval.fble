
@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Length = /Core/List/Length%.Length;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

% Strs = /Core/String%.Strs;
% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ Markup@ = /Fbld/Markup%.Markup@;
@ Command@ = /Fbld/Markup%.Command@;
@ Impl@ = /Fbld/Markup%.Impl@;
% Append = /Fbld/Markup%.Append;
% Empty = /Fbld/Markup%.Empty;

@ Env@ = /Fbld/Env%.Env@<Impl@>;
% Lookup = /Fbld/Env%.Lookup<Impl@>;

<@>@ Result@ = /Fbld/Result%.Result@;
<@>% Error = /Fbld/Result%.Error;
<@>% Try = /Fbld/Result%.Try;
% r = /Fbld/Result%.Monad;

(Env@, Markup@) { Result@<Markup@>; }
Eval = (Env@ env, Markup@ markup) {
  markup.?(
    empty: r.return(markup),
    plain: r.return(markup),
    command: {
      Command@ cmd = markup.command;
      Maybe@<Impl@> impl = {
        cmd.impl.?(just: cmd.impl);
        Lookup(env, cmd.name.text);
      };
      impl.?(nothing: r.return(markup));
      impl.just(env, cmd);
    },
    sequence: {
      Markup@ a <- r.do(Try(Eval(env, markup.sequence.a), Empty));
      Markup@ b <- r.do(Try(Eval(env, markup.sequence.b), Empty));
      r.return(Append(a, b));
    });
};

(Env@, List@<Markup@>) { Result@<List@<Markup@>>; }
Evals = (Env@ env, List@<Markup@> markups) {
  markups.?(nil: r.return(markups));
  Markup@ head <- r.do(Eval(env, markups.cons.head));
  List@<Markup@> tail <- r.do(Evals(env, markups.cons.tail));
  r.return(Cons(head, tail));
};

# Helper function to assert that a command has been provided with the given
# number of arguments.
(Int@, Command@) { Result@<Unit@>; }
AssertArgCount = (Int@ expected, Command@ cmd) {
  Int@ num_args = Length(cmd.args);
  /Core/Int/Eq%.Eq(num_args, expected).?(false: {
    Error<Unit@>(cmd.name.loc, Strs[
      Str|'Expected ', /Core/Int/Show%.Show(expected),
      Str|' arguments to ', cmd.name.text,
      Str|', but found ', /Core/Int/Show%.Show(num_args)]);
  });
  r.return(Unit);
}; 


@(Eval, Evals, AssertArgCount);
