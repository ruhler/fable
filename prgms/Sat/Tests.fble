@ Bool@ = /Bool%.Bool@;
% True = /Bool%.True;
% False = /Bool%.False;
% And = /Bool%.And;

@ Char@ = /Char%.Char@;
% Chars = /Char%.Chars;

@ IntP@ = /Int/IntP%.IntP@;
% EqIntP = /Int/IntP/Eq%.Eq;
% 1 = /Int/IntP%.1;

<@>% List = /List%.List;
<@>% Append = /List%.Append;

<@>@ Maybe@ = /Maybe%.Maybe@;
<@>% Just = /Maybe%.Just;
<@>% Nothing = /Maybe%.Nothing;

@ String@ = /String%.String@;
% Str = /String%.Str;

@ Test@ = /Test%.Test@;
@ TestResult@ = /Test%.TestResult@;
@ TestSuite@ = /Test%.TestSuite@;
% TestSuite = /Test%.TestSuite;
<@>% AssertEquals = /Test%.AssertEquals;

@ Formula@ = /Sat%.Formula@;
@ Clause@ = /Sat%.Clause@;
@ Var@ = /Sat%.Var@;
@ Assignment@ = /Sat%.Assignment@;
% Solve = /Sat%.Solve;

(IntP@) { String@; } PrintId = (IntP@ x) {
  # TODO: Print variable ids properly.
  Str(x.?(1: Chars|1, : Chars|'?'));
};

(Var@) { String@; } PrintVar = (Var@ v) {
  String@ sign = Str(v.polarity.?(true: Chars|'+', false: Chars|'-'));
  Append<Char@>(sign, PrintId(v.id));
};

(Var@, Var@) { Bool@; } EqVar = (Var@ a, Var@ b) {
  And(EqIntP(a.id, b.id), /Bool/Eq%.Eq(a.polarity, b.polarity));
};

(Maybe@<Assignment@>, Maybe@<Assignment@>) { TestResult@; }
AssertEq = (Maybe@<Assignment@> a, Maybe@<Assignment@> b) {
  AssertEquals<Maybe@<Assignment@>>(
    /Maybe/Eq%.Eq<Assignment@>(/List/Eq%.Eq<Var@>(EqVar)),
    /Maybe/Show%.Show<Assignment@>(/List/Show%.Show<Var@>(PrintVar)),
    a, b);
};

TestSuite@ Tests = TestSuite(Chars|Sat, [
  Test@(Str(Chars|Sat), !({
    Formula@ f = List<Clause@>([
      List<Var@>([Var@(1, True)])
    ]);
    Maybe@<Assignment@> want = Just<Assignment@>(List<Var@>([Var@(1, True)]));
    Maybe@<Assignment@> got = Solve(f);
    AssertEq(want, got);
  })),

  Test@(Str(Chars|Unsat), !({
    Formula@ f = List<Clause@>([
      List<Var@>([Var@(1, True)]),
      List<Var@>([Var@(1, False)])
    ]);
    Maybe@<Assignment@> want = Nothing<Assignment@>;
    Maybe@<Assignment@> got = Solve(f);
    AssertEq(want, got);
  }))
]);

@(Tests);
