
{ /Int/IntP%; @(IntP@); };
@(IntP: /Int/IntP%);
{ /Maybe%; @(Maybe@); };
{ /Unit%; @(Unit@, Unit); };

@ Int@ = +(
  IntP@ n,   # Negative integers
  Unit@ 0,
  IntP@ p    # Positive integers
);

Int@ 0 = Int@(0: Unit);

(IntP@) { Int@; } N = (IntP@ x) {
  Int@(n: x);
};

(IntP@) { Int@; } P = (IntP@ x) {
  Int@(p: x);
};

# The integer (-1)
Int@ Minus1 = N(IntP.1);

Int@ 1 = P(IntP.1);
Int@ 2 = P(IntP.2);
Int@ 3 = P(IntP.3);
Int@ 4 = P(IntP.4);
Int@ 5 = P(IntP.5);
Int@ 6 = P(IntP.6);
Int@ 7 = P(IntP.7);
Int@ 8 = P(IntP.8);
Int@ 9 = P(IntP.9);
Int@ 10 = P(IntP.10);

# a + 1
(Int@){ Int@; } Inc = (Int@ a) {
  ?(a; 
      n: {
        Maybe@<IntP@> ms = IntP.Sub1(a.n);
        ?(ms; just: N(ms.just), nothing: 0);
      },
      0: P(IntP.1),
      p: P(IntP.Add1(a.p)));
};

# a - 1
(Int@){ Int@; } Dec = (Int@ a) {
  ?(a;
      n: N(IntP.Add1(a.n)),
      0: N(IntP.1),
      p: {
        Maybe@<IntP@> ms = IntP.Sub1(a.p);
        ?(ms; just: P(ms.just), nothing: 0);
      });
};

# 2 * a
(Int@) { Int@; } Mul2 = (Int@ a) {
  ?(a; n: N(IntP.2P0(a.n)), 0: a, p: P(IntP.2P0(a.p)));
};

(IntP@, IntP@) { Int@; } SubP = (IntP@ a, IntP@ b) {
  ?(a; 1: {
         Maybe@<IntP@> ms = IntP.Sub1(b);
         ?(ms; just: N(ms.just), nothing: 0);
       },
       2p0: ?(b; 1: P(IntP.Sub1(a).just),
                 2p0: Mul2(SubP(a.2p0, b.2p0)),
                 2p1: Dec(Mul2(SubP(a.2p0, b.2p1)))),
       2p1: ?(b; 1: P(IntP.Sub1(a).just),
                 2p0: Inc(Mul2(SubP(a.2p1, b.2p0))),
                 2p1: Mul2(SubP(a.2p1, b.2p1))));
};

# a + b
(Int@, Int@) { Int@; } Add = (Int@ a, Int@ b) {
  ?(a; n: ?(b; n: N(IntP.Add(a.n, b.n)),
               0: a,
               p: SubP(b.p, a.n)),
       0: b,
       p: ?(b; n: SubP(a.p, b.n),
               0: a,
               p: P(IntP.Add(a.p, b.p))));
};

# -a 
(Int@){ Int@; } Neg = (Int@ a) {
  ?(a ; n: P(a.n), 0: a, p: N(a.p));
};

# a - b 
(Int@, Int@){ Int@; } Sub = (Int@ a, Int@ b) {
  # a - b = a + (-b)
  Add(a, Neg(b));
};

# a * b
(Int@, Int@){ Int@; } Mul = (Int@ a, Int@ b) {
  ?(a; 
    n: ?(b; n: P(IntP.Mul(a.n, b.n)),
            0: 0,
            p: N(IntP.Mul(a.n, b.p))),
    0: 0,
    p: ?(b; n: N(IntP.Mul(a.p, b.n)),
            0: 0,
            p: P(IntP.Mul(a.p, b.p))));
};

Int@ 100 = Mul(10, 10);
Int@ 1000 = Mul(10, 100);

(Int@, Int@){Int@;} Lit2 = (Int@ a, Int@ b) {
  Add(Mul(10, a), b);
};

(Int@, Int@, Int@){Int@;} Lit3 = (Int@ a, Int@ b, Int@ c) {
  Add(Mul(100, a), Lit2(b, c));
};

(Int@, Int@, Int@, Int@){Int@;} Lit4 = (Int@ a, Int@ b, Int@ c, Int@ d) {
  Add(Mul(1000, a), Lit3(b, c, d));
};

@(Int@, 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10, Lit2, Lit3, Lit4,
 Inc, Dec, Add, Sub, Neg, Mul);
