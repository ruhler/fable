
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;

@ Char@ = /Core/Char%.Char@;
% IsAlNum = /Core/Char/Type%.IsAlNum;

@ Int@ = /Core/Int%.Int@;
% Inc = /Core/Int%.Inc;
% Dec = /Core/Int%.Dec;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;
<@>% List = /Core/List%.List;
<@>% Reverse = /Core/List%.Reverse;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Unit@ = /Core/Unit%.Unit@;

@ Loc@ = /Fbld/Types%.Loc@;
@ StringL@ = /Fbld/Types%.StringL@;
@ Command@ = /Fbld/Types%.Command@;

<@>@ M@ = /Fbld/Parse/M%.M@;
<@>% Return = /Fbld/Parse/M%.Return;
<@>% Error = /Fbld/Parse/M%.Error;
<@>% Do = /Fbld/Parse/M%.Do;
% Peek = /Fbld/Parse/M%.Peek;
% Loc = /Fbld/Parse/M%.Loc;
% Get = /Fbld/Parse/M%.Get;
% Skip = /Fbld/Parse/M%.Skip;

# Parses plain text before the next @... command if any.
# Returns the empty string if there is no plain text before the next @...
# command.
M@<StringL@> Plain = {
  (Loc@, String@) { M@<StringL@>; } plain = (Loc@ l, String@ rplain) {
    Maybe@<Char@> mc <- Do(Peek);
    mc.?(nothing: Return(StringL@(Reverse(rplain), l)));
    mc.just.?('\': {
      Unit@ _ <- Do(Skip);
      String@ rp = Cons(mc.just, rplain);
      Maybe@<Char@> mb <- Do(Get);
      String@ rp2 = mb.?(just: Cons(mb.just, rp), nothing: rp);
      plain(l, rp2);
    });
    mc.just.?('@': Return(StringL@(Reverse(rplain), l)));
    Unit@ _ <- Do(Skip);
    plain(l, Cons(mc.just, rplain));
  };

  Loc@ l <- Do(Loc);
  plain(l, Str|'');
};

(Char@) { Bool@; } IsNameChar = (Char@ c) {
  c.?('_': True);
  IsAlNum(c);
};

# Parses a command name from the input, not including a leading '@'.
M@<StringL@> Name = {
  (Loc@, String@) { M@<StringL@>; } name = (Loc@ l, String@ rname) {
    Maybe@<Char@> mc <- Do(Peek);
    mc.?(nothing: Return(StringL@(Reverse(rname), l)));
    IsNameChar(mc.just).?(false: Return(StringL@(Reverse(rname), l)));
    Unit@ _ <- Do(Skip);  
    name(l, Cons(mc.just, rname));
  };

  Loc@ l <- Do(Loc);
  name(l, Str|'');
};

# Parses an explicit argument '[...]'.
M@<Maybe@<StringL@>> Arg = {
  (Loc@, Int@, String@) { M@<StringL@>; } arg = (Loc@ l, Int@ nest, String@ rarg) {
    Maybe@<Char@> mc <- Do(Get);
    mc.?(nothing: Error<StringL@>(StringL@(Str|'Unterminated argument', l)));

    Char@ c = mc.just;
    c.?('\': {
      Maybe@<Char@> nc <- Do(Get);
      nc.?(nothing: Error<StringL@>(StringL@(Str|'Unterminated argument', l)));
      arg(l, nest, Cons(nc.just, Cons(c, rarg)));
    });

    c.?('[': arg(l, Inc(nest), Cons(c, rarg)));

    c.?(']': {
      /Core/Int/Eq%.IsZero(nest).?(true: {
        Return(StringL@(Reverse(rarg), l));
      });
      arg(l, Dec(nest), Cons(c, rarg));
    });

    arg(l, nest, Cons(c, rarg));
  };

  Maybe@<Char@> mc <- Do(Peek);
  mc.?(nothing: Return(Nothing<StringL@>));
  mc.just.?('[': {
    Unit@ _ <- Do(Skip);
    Loc@ l <- Do(Loc);
    StringL@ s <- Do(arg(l, Int|0, Str|''));
    Return(Just(s));
  });
  Return(Nothing<StringL@>);
};

# Parses a sequence of arguments.
M@<List@<StringL@>> Args = {
  (List@<StringL@>) { M@<List@<StringL@>>; } args = (List@<StringL@> rargs) {
    Maybe@<StringL@> arg <- Do(Arg);
    arg.?(nothing: Return(Reverse(rargs)));
    args(Cons(arg.just, rargs));
  };

  args(Nil<StringL@>);
};

# Parse a command, starting with '@'.
M@<Maybe@<Command@>> Command = {
  Maybe@<Char@> mc <- Do(Peek);
  mc.?(nothing: Return(Nothing<Command@>));
  mc.just.?('@': {
    Unit@ _ <- Do(Skip);
    StringL@ name <- Do(Name);
    List@<StringL@> args <- Do(Args);
    Return(Just(Command@(name, args)));
  });
  Return(Nothing<Command@>);
};

# Parse commands from inline structured text.
M@<List@<Command@>> Inline = {
  (List@<Command@>) { M@<List@<Command@>>; }
  inline = (List@<Command@> rcmds) {
    StringL@ plain <- Do(Plain);
    List@<Command@> cmds = {
      plain.str.?(nil: rcmds);
      Cons(Command@(StringL@(Str|'', plain.loc), List[plain]), rcmds);
    };

    Maybe@<Command@> command <- Do(Command);
    command.?(nothing: Return(Reverse(cmds)));
    inline(Cons(command.just, cmds));
  };

  inline(Nil<Command@>);
};

@(Name, Args, Inline);
