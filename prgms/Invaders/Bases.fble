
@ Bool@ = /Bool%.Bool@;
% True = /Bool%.True;
% False = /Bool%.False;

@ Drawing@ = /Drawing%.Drawing@;
% Blank = /Drawing%.Blank;
% Color = /Drawing%.Color;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;

@ Int@ = /Int/Int%.Int@;
% Add = /Int/Int%.Add;
% Sub = /Int/Int%.Sub;
% Lt = /Int/Int/Eq%.Lt;
% Ge = /Int/Int/Eq%.Ge;
% Int = /Int/Int/Lit%.Int;

<@>@ List@ = /List%.List@;
<@>% List = /List%.List;

<@>@ Maybe@ = /Maybe%.Maybe@;

# Slice@ --
#   The vertical range of a slice of base.
@ Slice@ = *(Int@ min, Int@ max);

# Damage --
#   The number of pixels of a slice of base destroyed when hit by a bomb or a
#   bullet.
Int@ Damage = Int|4;

# BaseY --
#   The bottom Y coordinate of the bases.
Int@ BaseY = Int|36;

# Bases@ --
#   Map from x coordinate to the slice of base, if any, at that x coordinate.
@ Bases@ = /Map%.Map@<Int@, Slice@>;

# Empty --
#   No bases.
Bases@ Empty = /Map%.Empty<Int@, Slice@>;

# Lookup --
#   Look up a slice by X coordinate.
(Bases@, Int@) { Maybe@<Slice@>; } Lookup = /Map%.Lookup<Int@>(Lt)<Slice@>;

# Delete --
#   Remove a slice by X coordinate.
(Bases@, Int@) { Bases@; } Delete = /Map%.Delete<Int@>(Lt)<Slice@>;

(Bases@, Int@, Slice@) { Bases@; } Insert = /Map%.Insert<Int@>(Lt)<Slice@>;
  

# BaseSpec --
#   Specification for the shape of an individual base, relative to the lower
#   left corner of an individual base, in order of X from left to right.
List@<Slice@> BaseSpec = List<Slice@>[
  Slice@(Int|0, Int|12), Slice@(Int|0, Int|13),
  Slice@(Int|0, Int|14), Slice@(Int|0, Int|15),
  Slice@(Int|0, Int|16), Slice@(Int|2, Int|16),
  Slice@(Int|3, Int|16), Slice@(Int|4, Int|16),
  Slice@(Int|4, Int|16), Slice@(Int|4, Int|16),
  Slice@(Int|4, Int|16), Slice@(Int|4, Int|16),
  Slice@(Int|4, Int|16), Slice@(Int|4, Int|16),
  Slice@(Int|4, Int|16), Slice@(Int|4, Int|16),
  Slice@(Int|4, Int|16), Slice@(Int|3, Int|16),
  Slice@(Int|2, Int|16), Slice@(Int|0, Int|16),
  Slice@(Int|0, Int|15), Slice@(Int|0, Int|14),
  Slice@(Int|0, Int|13), Slice@(Int|0, Int|12)
];

# AddSlices --
#   Add a BaseSpec collection of slices to Bases@ starting at the given X
#   coordinate.
(Int@, List@<Slice@>, Bases@) { Bases@; }
AddSlices = {
  (Int@ x, List@<Slice@> slices, Bases@ bases) {
    slices.?(nil: bases);
    Slice@ head = slices.cons.head;
    Slice@ slice = Slice@(Add(BaseY, head.min), Add(BaseY, head.max));
    Bases@ inserted = Insert(bases, x, slice);
    AddSlices(Add(x, Int|1), slices.cons.tail, inserted);
  };
};

# Initial --
#   The initial bases, given the current game level.
(Int@) { Bases@; } Initial = (Int@ level) {
  # We no longer have bases once we reach level 4.
  Ge(level, Int|4).?(true: Empty);

  AddSlices(Int|68, BaseSpec,
  AddSlices(Int|121, BaseSpec,
  AddSlices(Int|174, BaseSpec,
  AddSlices(Int|227, BaseSpec, Empty))));
};

# HitResult@ --
#   The result of hitting a base.
#
# Fields:
#   hit: - True if the base was hit, false otherwise.
#   bases - The state of the bases after being hit.
@ HitResult@ = *(Bool@ hit, Bases@ bases);

# BombHit --
#   Check for and apply collision between bomb and bases.
#
# Inputs:
#   x, y - the coordinates of the bomb.
#   bases - the bases to check against.
(Int@, Int@, Bases@) { HitResult@; }
BombHit = (Int@ x, Int@ y, Bases@ bases) {
  HitResult@ miss = HitResult@(False, bases);
  Maybe@<Slice@> slice = Lookup(bases, x);
  slice.?(nothing: miss);
  Ge(y, slice.just.max).?(true: miss);
  Int@ max = Sub(y, Damage);

  Lt(slice.just.min, max).?(true: {
    HitResult@(True, Insert(bases, x, Slice@(slice.just.min, max)));
  });

  HitResult@(True, Delete(bases, x));
};

# BulletHit --
#   Check for and apply collision between bullet and bases.
#
# Inputs:
#   x, y - the coordinates of the top of the bullet.
#   bases - the bases to check against.
(Int@, Int@, Bases@) { HitResult@; }
BulletHit = (Int@ x, Int@ y, Bases@ bases) {
  HitResult@ miss = HitResult@(False, bases);
  Maybe@<Slice@> slice = Lookup(bases, x);
  slice.?(nothing: miss);
  Ge(y, slice.just.max).?(true: miss);
  Lt(y, slice.just.min).?(true: miss);

  Int@ min = Add(y, Damage);

  Lt(min, slice.just.max).?(true: {
    HitResult@(True, Insert(bases, x, Slice@(min, slice.just.max)));
  });

  HitResult@(True, Delete(bases, x));
};

# Draw --
#   Draw the bases.
(Bases@) { Drawing@; } Draw = {
  % ForEach = /Map%.ForEach<Int@, Slice@, Drawing@>;

  (Bases@ bases) {
    ForEach(bases, Blank, (Int@ x, Slice@ s, Drawing@ d) {
      Over(d, Rect(x, s.min, Int|1, Sub(s.max, s.min), Color.Green));
    });
  };
};

@(Bases@, HitResult@, Initial, BombHit, BulletHit, Draw);
