
@ Drawing@ = /App/Drawing%.Drawing@;
@ Color@ = /App/Drawing%.Color@;
@ Point@ = /App/Drawing%.Point@;
% Color = /App/Drawing%.Color;
% Drawings = /App/Drawing%.Drawings;
% Rect = /App/Drawing%.Rect;
% Over = /App/Drawing%.Over;
% Translate = /App/Drawing%.Translate;

@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Nil = /Core/List%.Nil;
<@>% Cons = /Core/List%.Cons;
<@>% List = /Core/List%.List;

@ ImagePair@ = *(Drawing@ a, Drawing@ b);

Color@ BackgroundColor = Color@(Int|127, Int|127, Int|127);

Drawing@ TileBackground = Rect(Int|0, Int|0, Int|48, Int|48, BackgroundColor);

(Drawing@) { Drawing@; }
Tile = (Drawing@ image) {
  Drawings[TileBackground, Translate(Int|8, Int|8, image)];
};

Color@ FillColor = Color@(Int|96, Int|96, Int|156);

ImagePair@ Square = {
  Drawing@ outline = Rect(Int|2, Int|2, Int|30, Int|30, Color.Black);
  Drawing@ inline_empty = Rect(Int|4, Int|4, Int|26, Int|26, BackgroundColor);
  Drawing@ inline_fill = Rect(Int|4, Int|4, Int|26, Int|26, FillColor);
  Drawing@ a = Drawings[outline, inline_empty];
  Drawing@ b = Drawings[outline, inline_fill];
  @(a, b);
};

ImagePair@ Triangle = {
  Drawing@ outline = /App/Drawing%.Triangle(
    Point@(Int|2, Int|2),
    Point@(Int|16, Int|30),
    Point@(Int|30, Int|2),
    Color.Black
  );
  Drawing@ inline_empty = /App/Drawing%.Triangle(
    Point@(Int|5, Int|4),
    Point@(Int|16, Int|26),
    Point@(Int|27, Int|4),
    BackgroundColor
  );
  Drawing@ inline_fill = /App/Drawing%.Triangle(
    Point@(Int|5, Int|4),
    Point@(Int|16, Int|26),
    Point@(Int|27, Int|4),
    FillColor
  );

  Drawing@ a = Drawings[outline, inline_empty];
  Drawing@ b = Drawings[outline, inline_fill];
  @(a, b);
};

List@<ImagePair@> Images = List[Square, Triangle];

Drawing@ Background = Rect(Int|0, Int|0, Int|640, Int|480, Color.Black);

Drawing@ Screenshot = {
  List@<Drawing@> images = /Core/List%.ForEach(Images, Nil<Drawing@>,
    (ImagePair@ p, List@<Drawing@> l) {
      Cons(p.a, Cons(p.b, l));
    });

  @ S@ = *(Int@ x, Drawing@ d);
  /Core/List%.ForEach(images, S@(Int|32, Background),
    (Drawing@ i, S@ s) {
      S@(Add(Int|48, s.x), Over(s.d, Translate(s.x, Int|64, Tile(i))));
    }).d;
};

@(Screenshot);
