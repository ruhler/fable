
<@>@ List@ = /List%.List@;

@ Ball@ = /Pinball/Ball%.Ball@;

% BallEdgeS = /Pinball/Collide%.BallEdgeS;
% BallPaddleS = /Pinball/Collide%.BallPaddleS;
% PaddleRangeS = /Pinball/Collide%.PaddleRangeS;

@ Edge@ = /Pinball/Edge%.Edge@;

@ Event@ = /Pinball/Event%.Event@;
% Events = /Pinball/Event%.Events;

@ Paddle@ = /Pinball/Paddle%.Paddle@;

@ Num@ = /Pinball/Num%.Num@;
% Sub = /Pinball/Num%.Sub;
% Div = /Pinball/Num%.Div;
% Le = /Pinball/Num%.Le;
% Int = /Pinball/Num%.Int;

@ Vec@ = /Pinball/Vec%.Vec@;
% AddV = /Pinball/Vec%.AddV;
% MulVS = /Pinball/Vec%.MulVS;

@ World@ = *(
  Vec@ gravity,      # acceleration due to gravity
  Ball@ ball,
  List@<Edge@> edges,
  List@<Paddle@> paddles
);

# Move --
#   Move the world by the given amount of time, assuming velocity stays
#   constant.
#
# In case of collision, we break down the time period into smaller bits. The
# assumption that velocity is constant is intended to prevent the case where
# we break down the time period into smaller and smaller bits and end up never
# reaching the end of the time period as a result. This also assumes we
# don't have any super tight corners that would cause us to collide an
# excessive number of times in a single time period.
(World@, Num@) { World@; } Move = (World@ w, Num@ t) {
  Le(t, Int|0).?(true: w);

  # Figure out when the next interesting thing happens.
  Event@ event = Events[
    BallEdgeS(w.ball, w.edges, t),      # ball collides with an edge?
    BallPaddleS(w.ball, w.paddles, t),  # ball collides with a paddle?
    PaddleRangeS(w.paddles, t)          # paddle reaches its end of range?
  ];

  # Advance the state of the world to just after the next interesting event.
  Ball@ ball = {
    event.ball.?(just: event.ball.just);
    /Pinball/Ball%.Move(w.ball, event.time);
  };

  List@<Paddle@> paddles = /List%.Map<Paddle@, Paddle@>(w.paddles, (Paddle@ p) {
    /Pinball/Paddle%.Move(p, event.time);
  });

  World@ moved = World@(w.gravity, ball, w.edges, paddles);
  Move(moved, Sub(t, event.time));
};

# Tick --
#   Advance the world by one unit of time.
(World@) { World@; } Tick = (World@ w) {
  # Compute average expected ball velocity over the time period.
  Vec@ half_gravity = MulVS(w.gravity, Div(Int|1, Int|2));
  Vec@ v_avg = AddV(w.ball.vel, half_gravity);
  World@ updated = World@(w.gravity, Ball@(w.ball.pos, v_avg, w.ball.radius), w.edges, w.paddles);

  # Move the ball assuming constant velocity.
  World@ moved = Move(updated, Int|1);

  # Compute final ball velocity.
  Vec@ v = AddV(moved.ball.vel, half_gravity);
  World@(w.gravity, Ball@(moved.ball.pos, v, moved.ball.radius), moved.edges, moved.paddles);
};

@(World@, Tick);
