
% True = /Core/Bool%.True;

@ Int@ = /Core/Int%.Int@;
% Sub = /Core/Int%.Sub;
% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

<<@>@>@ App@ = /App/App%.App@;
<<@>@>@ Main@ = /App/App%.Main@;
@ Event@ = /App/App%.Event@;
@ Effect@ = /App/App%.Effect@;

@ Color@ = /App/Drawing%.Color@;
@ Drawing@ = /App/Drawing%.Drawing@;
% Color = /App/Drawing%.Color;
% Rect = /App/Drawing%.Rect;
% Drawings = /App/Drawing%.Drawings;

@ Params@ = /SatGame/Puzzle%.Params@;
@ Puzzle@ = /SatGame/Puzzle%.Puzzle@;

@ Game@ = /SatGame/Game%.Game@;
% Toggle = /SatGame/Game%.Toggle;
% Solved = /SatGame/Game%.Solved;

@ TileIndex@ = /SatGame/Game/Draw%.TileIndex@;
% TileOf = /SatGame/Game/Draw%.TileOf;

% PureLFSR = /SatGame/Random/PureLFSR%;
% Generate = /SatGame/Generate%<PureLFSR.M@>(PureLFSR.Monad, PureLFSR.Random);

@ Args@ = *(
  Int@ num_vars,
  Int@ num_clauses,
  Int@ num_literals_per_clause,
  Int@ seed
);

Args@ DefaultArgs = @(
  num_vars: Int|12,
  num_clauses: Int|18,
  num_literals_per_clause: Int|3,
  seed: Int|1
);

(List@<String@>) { Args@; }
GetArgs = (List@<String@> args) {
  args.?(nil: DefaultArgs);
  args.cons.tail.?(nil: DefaultArgs);

  Args@ tail = GetArgs(args.cons.tail.cons.tail);

  String@ arg = args.cons.head;
  Int@ value = /Core/Int/Show%.Read(args.cons.tail.cons.head);

  /Core/String/Eq%.Eq(arg, Str|'--vars').?(true: {
    tail.@(num_vars: value);
  });

  /Core/String/Eq%.Eq(arg, Str|'--clauses').?(true: {
    tail.@(num_clauses: value);
  });

  /Core/String/Eq%.Eq(arg, Str|'--lits-per-clause').?(true: {
    tail.@(num_literals_per_clause: value);
  });

  /Core/String/Eq%.Eq(arg, Str|'--seed').?(true: {
    tail.@(seed: value);
  });

  tail;
};

@ State@ = *(
  Game@ game,
  Int@ mouse_x,
  Int@ mouse_y,
  Drawing@ screen   # Cached drawing of the game without the cursor.
);

Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio, App@<M@> app, Int@ width, Int@ height, List@<String@> argv) {
  % O = /Core/Stream/OStream%(m);

  Args@ args = GetArgs(argv);
  Params@ params = Params@(args.num_vars, args.num_clauses, args.num_literals_per_clause);

  Int@ cols = Int|3;

  (Game@) { Drawing@; } DrawScreen = (Game@ game) {
    Color@ bg_color = Solved(game).?(true: Color.Green, false: Color.Black);
    Drawing@ background = Rect(Int|0, Int|0, width, height, bg_color);
    Drawings[background, /SatGame/Game/Draw%.Draw(cols, game)];
  };

  (State@) { Drawing@; } DrawCursor = (State@ state) {
    Rect(
      Sub(state.mouse_x, Int|4),
      Sub(state.mouse_y, Int|4),
      Int|8, Int|8, Color.Yellow);
  };

  (State@) { M@<Unit@>; } Draw = (State@ state) {
    Drawing@ screen = Drawings[state.screen, DrawCursor(state)];
    app.effect(Effect@(draw: screen));
  };

  (State@) { M@<Unit@>; } EventLoop = (State@ state) {
    Event@ e <- m.do(app.event);
    e.?(
      tick: EventLoop(state),
      key_down: m.return(Unit),
      key_up: m.return(Unit),
      mouse_down: {
        Int@ x = e.mouse_down.x;
        Int@ y = e.mouse_down.y;
        Maybe@<TileIndex@> ti = TileOf(params, cols, x, y);
        ti.?(nothing: EventLoop(state));
        Game@ ng = Toggle(ti.just.group, ti.just.tile, state.game);
        State@ ns = state.@(game: ng, screen: DrawScreen(ng));
        Unit@ _ <- m.do(Draw(ns));
        EventLoop(ns);
      },
      mouse_up: EventLoop(state),
      resize: EventLoop(state),
      mouse_motion: {
        State@ ns = state.@(
          mouse_x: e.mouse_motion.x,
          mouse_y: e.mouse_motion.y
          );
        Unit@ _ <- m.do(Draw(ns));
        EventLoop(ns);
      });
  };

  Unit@ _ <- m.do(O.PutLine(stdio.out, Strs[
      Str|'Generating puzzle with params: ',
      /SatGame/Puzzle/Show%.Params(params),
      Str|' (seed ', /Core/Int/Show%.Show(args.seed), Str|')'
  ]));

  Puzzle@ puzzle = PureLFSR.Run(Generate(params), args.seed);
  Unit@ _ <- m.do(O.PutLine(stdio.out, /SatGame/Puzzle/Show%.Puzzle(puzzle)));

  Game@ game = /SatGame/Game%.Game(puzzle);

  State@ state = State@(game, Int|0, Int|0, DrawScreen(game));
  Unit@ _ <- m.do(Draw(state));
  Unit@ _ <- m.do(EventLoop(state));
  m.return(True);
};

Main;
