
@ Bool@ = /Core/Bool%.Bool@;

@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;
<@>% Nil = /Core/List%.Nil;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ Main@ = /Core/Stdio%.Main@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;
<<@>@>@ OStream@ = /Core/Stream%.OStream@;

@ String@ = /Core/String%.String@;

@ Unit@ = /Core/Unit%.Unit@;

@ PureState@ = *(
  List@<Int@> in,
  List@<Int@> out,  # built up in reverse order
  List@<Int@> err   # built up in reverse order
);

% State = /Core/Monad/State%<PureState@>;

# Pure@ -- A monad for pure Stdio computation.
<@>@ Pure@ = State.State@;
<@>@ PureResult@ = State.R@;

Monad@<Pure@> Monad = State.Monad;

Pure@<Maybe@<Int@>> In = {
  PureState@ s <- Monad.do(State.Get);
  s.in.?(nil: Monad.return(Nothing<Int@>));
  Unit@ _ <- Monad.do(State.Set(PureState@(s.in.cons.tail, s.out, s.err)));
  Monad.return(Just(s.in.cons.head));
};

(Int@) { Pure@<Unit@>; } Out = (Int@ b) {
  PureState@ s <- Monad.do(State.Get);
  State.Set(PureState@(s.in, Cons(b, s.out), s.err));
};

(Int@) { Pure@<Unit@>; } Err = (Int@ b) {
  PureState@ s <- Monad.do(State.Get);
  State.Set(PureState@(s.in, s.out, Cons(b, s.err)));
};

(String@) { Pure@<Maybe@<IStream@<Pure@>>>; } Read = (String@ _filename) {
  # TODO: Add a map from file name to List@<Int@> to allow modeling of the
  # filesystem.
  Monad.return(Nothing<IStream@<Pure@>>);
};

(String@) { Pure@<Maybe@<OStream@<Pure@>>>; } Write = (String@ _filename) {
  # TODO: Add a map from file name to List@<Int@> to allow modeling of the
  # filesystem.
  Monad.return(Nothing<OStream@<Pure@>>);
};

(String@) { Pure@<Maybe@<String@>>; } GetEnv = (String@ _filename) {
  # TODO: Add a map from String@ to String@to allow modeling of environment
  # variables.
  Monad.return(Nothing<String@>);
};

Stdio@<Pure@> Stdio = Stdio@<Pure@>(In, Out, Err, Read, Write, GetEnv);

# Run --
#   Run a pure stdio computation.
#
# Inputs:
#   m - The computation to run.
#   in - List of bytes to use for stdin.
<@ A@>(Pure@<A@>, List@<Int@>) { PureResult@<A@>; }
Run = <@ A@>(Pure@<A@> m, List@<Int@> in) {
  PureResult@<A@> result = State.Run(m, PureState@(in, Nil<Int@>, Nil<Int@>));
  result.@(s: result.s.@(
      out: /Core/List%.Reverse(result.s.out),
      err: /Core/List%.Reverse(result.s.err)));
};

# @struct[MainResult@] Result of running a pure Main@ program.
#  @field[Bool@][exit] The exit status.
#  @field[List@<Int@>][in] The remaining unconsumed input bytes.
#  @field[List@<Int@>][out] Bytes output to stdout.
#  @field[List@<Int@>][err] Bytes output to stderr.
@ MainResult@ = *(
  Bool@ exit,
  List@<Int@> in,
  List@<Int@> out,
  List@<Int@> err
);

(Main@, List@<String@>, List@<Int@>) { MainResult@; }
RunMain = (Main@ main, List@<String@> args, List@<Int@> in) {
  PureResult@<Bool@> got = Run(main<Pure@>(Stdio, Monad, args), in);
  @(exit: got.x, in: got.s.in, out: got.s.out, err: got.s.err);
};

@(Pure@, PureState@, PureResult@, Monad, Stdio, Run,
  MainResult@, RunMain);

