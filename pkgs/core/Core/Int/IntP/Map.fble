
@ Bool@ = /Core/Bool%.Bool@;
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

@ IntP@ = /Core/Int/IntP%.IntP@;
% 1 = /Core/Int/IntP%.1;

<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<@>% Just = /Core/Maybe%.Just;
<@>% Nothing = /Core/Maybe%.Nothing;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

# A Map from IntP@ to V@.
<@>@ Map@ = <@ V@> {
  @ M@ = +(Unit@ empty, P@ p),
  @ P@ = *(Maybe@<V@> 1, M@ 2p0, M@ 2p1);
  M@;
};

# A non-empty map from IntP@ to V@.
<@>@ MapP@ = <@ V@> {
  @ M@ = +(Unit@ empty, P@ p),
  @ P@ = *(Maybe@<V@> 1, M@ 2p0, M@ 2p1);
  P@;
};

Map@ Empty = <@ V@> {
  Map@<V@>(empty: Unit);
};

MapP@ EmptyP = <@ V@> {
  MapP@(Nothing<V@>, Empty<V@>, Empty<V@>);
};

<@ V@>(MapP@<V@>) { Bool@; }
IsEmptyP = <@ V@>(MapP@<V@> map) {
  map.1.?(just: False);
  map.2p0.?(p: False);
  map.2p1.?(p: False);
  True;
};

<@ V@>(Map@<V@>, IntP@) { Maybe@<V@>; }
Lookup = <@ V@>(Map@<V@> map, IntP@ key) {
  map.?(empty: Nothing<V@>);

  key.?(
    1: map.p.1,
    2p0: Lookup(map.p.2p0, key.2p0),
    2p1: Lookup(map.p.2p1, key.2p1));
};

<@ V@>(Map@<V@>, IntP@, V@) { Map@<V@>; }
Insert = <@ V@> (Map@<V@> map, IntP@ key, V@ value) {
  MapP@<V@> p = map.?(empty: EmptyP<V@>, p: map.p);

  Map@(p: key.?(
    1: p.@(1: Just(value)),
    2p0: p.@(2p0: Insert(p.2p0, key.2p0, value)),
    2p1: p.@(2p1: Insert(p.2p1, key.2p1, value))));
};

<@ V@>(Map@<V@>, IntP@) { Map@<V@>; }
Delete = <@ V@>(Map@<V@> map, IntP@ key) {
  map.?(empty: map);

  MapP@<V@> p = key.?(
    1: map.p.@(1: Nothing<V@>),
    2p0: map.p.@(2p0: Delete(map.p.2p0, key.2p0)),
    2p1: map.p.@(2p1: Delete(map.p.2p1, key.2p1)));
  IsEmptyP(p).?(true: Empty<V@>, false: Map@(p: p));
};

<@ V@>(Map@<V@>)<@ B@>(B@, (IntP@, V@, B@) { B@; }) { B@; }
ForEach = <@ V@>(Map@<V@> map)<@ B@>(B@ base, (IntP@, V@, B@) { B@; } body) {
  map.?(empty: base);
  MapP@<V@> p = map.p;

  B@ b1 = base;
  B@ b2 = p.1.?(just: body(1, p.1.just, b1), nothing: base);

  B@ b3 = ForEach(p.2p0, b2, (IntP@ i, V@ v, B@ b) {
    body(IntP@(2p0: i), v, b);
  });

  ForEach(p.2p1, b3, (IntP@ i, V@ v, B@ b) {
    body(IntP@(2p1: i), v, b);
  });
};

/Core/Map%.Map@<Map@, IntP@> Map = @(
  Map@, Empty, Insert, Delete, Lookup, ForEach
);

Map;

