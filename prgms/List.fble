
<@,@>@ Lit@ = /Literal%.Lit@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

<@>@ P@ = <@ T@> { *(T@ head, List@<T@> tail); },
<@>@ List@ = <@ T@> { +(P@<T@> cons, Unit@ nil); };

<@ T@>(T@, List@<T@>){List@<T@>;} Cons = <@ T@>(T@ a, List@<T@> l) {
  List@<T@>(cons: P@<T@>(a, l));
};

# Append list b to the end of list a.
<@ T@>(List@<T@>, List@<T@>){List@<T@>;} Append = <@ T@>(List@<T@> a, List@<T@> b) {
  a.?(cons: Cons<T@>(a.cons.head, Append<T@>(a.cons.tail, b)), nil: b);
};

List@ Nil = <@ T@> {
  List@<T@>(nil: Unit);
};

<@ T@>(Lit@<T@>) { List@<T@>; } List = <@ T@>(Lit@<T@> lit) {
  lit<List@<T@>>(Cons<T@>, Nil<T@>);
};

# InitP - Return all but the last element in 'list'.
<@ T@>(P@<T@>){List@<T@>;} InitP = <@ T@>(P@<T@> list) {
  list.tail.?(cons: Cons<T@>(list.head, InitP<T@>(list.tail.cons)), nil: Nil<T@>);
};

# Init - Return all but the last element in 'list'.
<@ T@>(List@<T@>){List@<T@>;} Init = <@ T@>(List@<T@> list) {
  list.?(cons: InitP<T@>(list.cons), nil: Nil<T@>);
};

# Concat: Flatten a list of lists into a single list of all the elements.
<@ T@>(List@<List@<T@>>){List@<T@>;} Concat = <@ T@>(List@<List@<T@>> x) {
  x.?(cons: Append<T@>(x.cons.head, Concat<T@>(x.cons.tail)), nil: Nil<T@>);
};

<@ A@, @ B@>(List@<A@>, (A@){B@;}){List@<B@>;} Map = <@ A@, @ B@>(List@<A@> a, (A@){B@;} f) {
  a.?(cons: List@<B@>(cons: MapP<A@, B@>(a.cons, f)), nil: Nil<B@>);
},
<@ A@, @ B@>(P@<A@>, (A@){B@;}){P@<B@>;} MapP = <@ A@, @ B@>(P@<A@> a, (A@){B@;} f) {
  P@<B@>(f(a.head), Map<A@, B@>(a.tail, f));
};

<@ A@, @ B@>(List@<A@>, B@, (A@, B@){B@;}){ B@;}
ForEach = <@ A@, @ B@>(List@<A@> l, B@ base, (A@, B@){B@;} body) {
  l.?(cons: ForEach<A@, B@>(l.cons.tail, body(l.cons.head, base), body),
      nil: base);
};

# ProcessEach
#  A process version of ForEach.
<@ A@, @ B@>(List@<A@>, B@, (A@, B@) { B@!; }){ B@!; }
ProcessEach = <@ A@, @ B@>(List@<A@> l, B@ base, (A@, B@) { B@!; } body) {
  l.?(
    cons: {
      B@ nbase := body(l.cons.head, base);
      ProcessEach<A@, B@>(l.cons.tail, nbase, body);
    },
    nil: !(base));
};

@(List@, List,
  Cons, Append, Nil,
  Init, Concat, Map,
  ForEach, ProcessEach);
