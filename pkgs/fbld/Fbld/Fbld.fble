
@ String@ = /Core/String%.String@;

@ Command@ = /Fbld/Command%.Command@;

@ Loc@ = /Fbld/Loc%.Loc@;

<@>@ Result@ = /Fbld/Result%.Result@;
<@>% Ok = /Fbld/Result%.Ok;
<@>% Err = /Fbld/Result%.Err;

@ Text@ = /Fbld/Text%.Text@;
% Text = /Fbld/Text%.Text;

@ FbldText@ = {
  @ FT@ = (E@) { Result@<Text@>; },
  @ I@ = (Command@) { FT@; },
  @ E@ = I@;
  FT@;
};

@ Invoke@ = (Command@) { FbldText@; };
@ Env@ = Invoke@;
<@>@ Fbld@ = <@ A@>(Env@) { Result@<A@>; };

<@ A@>(A@) { Fbld@<A@>; }
Return = <@ A@>(A@ x)(Env@ e) {
  Ok(x);
};

<@ A@>(Loc@, String@) { Fbld@<A@>; }
Error = <@ A@>(Loc@ l, String@ msg)(Env@ _) {
  Err<A@>(Text(msg, l));
};

<@ A@>(Fbld@<A@>)<@ B@>((A@) { Fbld@<B@>; }) { Fbld@<B@>; }
Do = <@ A@>(Fbld@<A@> ma)<@ B@>((A@) { Fbld@<B@>; } f)(Env@ e) {
  Result@<A@> r = ma(e);
  r.?(err: Err<B@>(r.err));
  f(r.ok)(e);
};

Fbld@<Env@> GetEnv = (Env@ env) {
  Ok(env);
};

<@ A@>(Env@, Fbld@<A@>) { Fbld@<A@>; }
WithEnv = <@ A@>(Env@ env, Fbld@<A@> m)(Env@ _) { 
  m(env);
};

# Runs an Fbld@ computation.
<@ A@>(Fbld@<A@>, Env@) { Result@<A@>; }
Run = <@ A@>(Fbld@<A@> m, Env@ e) {
  m(e);
};

@(Env@, Invoke@, Fbld@,
  Return, Error, Do, GetEnv, WithEnv,
  Run);
