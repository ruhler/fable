
% True = /Core/Bool%.True;

% Chars = /Core/Char%.Chars;

@ Int@ = /Core/Int%.Int@;
% Add = /Core/Int%.Add;
% Sub = /Core/Int%.Sub;
% Mul = /Core/Int%.Mul;
% Int = /Core/Int/Lit%.Int;
% Eq = /Core/Int/Eq%.Eq;
% Gt = /Core/Int/Eq%.Gt;

<@>@ List@ = /Core/List%.List@;

@ Stdio@ = /Core/IO/Stdio%.Stdio@;
@ IO@ = /Core/IO/Stdio%.IO@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

Int@ MinDepth = Int|4;

Int@ 0 = Int|0;
Int@ 1 = Int|1;
Int@ 2 = Int|2;

@ Node@ = *(Tree@ left, Tree@ right),
@ Tree@ = +(Unit@ leaf, Node@ node);

(Int@) { Tree@; } Tree = (Int@ depth) {
  Eq(0, depth).?(true: {
    Tree@(leaf: Unit);
  });

  Int@ sdepth = Sub(depth, 1);
  Tree@(node: Node@(Tree(sdepth), Tree(sdepth)));
};

(Tree@) { Int@; } Check = (Tree@ tree) {
  tree.?(
    leaf: 1,
    node: Add(Add(1, Check(tree.node.left)), Check(tree.node.right)));
};

(Int@) { Int@; } Exp2 = (Int@ x) {
  Eq(0, x).?(true: 1);
  Mul(2, Exp2(Sub(x, 1)));
};

Stdio@ Main = (IO@ io, List@<String@> args) {
  Int@ n = args.?(cons: /Core/Int/Show%.Read(args.cons.head), nil: 0);
  Int@ max_depth = Gt(Add(2, MinDepth), n).?(
    true: Add(2, MinDepth),
    false: n);
  Int@ stretch_depth = Add(1, max_depth);

  Int@ check = Check(Tree(stretch_depth));
  Unit@ _ := io.stdout(Strs[
    Str|'stretch tree of depth ', /Core/Int/Show%.Show(stretch_depth),
    Str|'	 check: ', /Core/Int/Show%.Show(check), Str[Chars.nl]]);

  Tree@ long_lived = Tree(max_depth);

  (Int@) { Unit@!; } ForDepth = (Int@ depth) {
    Gt(depth, max_depth).?(true: !(Unit));

    Int@ iterations = Exp2(Add(Sub(max_depth, depth), MinDepth));

    (Int@, Int@) { Int@; } ForIter = (Int@ check, Int@ n) {
      Gt(n, iterations).?(true: check);
      ForIter(Add(check, Check(Tree(depth))), Add(1, n));
    };

    Int@ check = ForIter(0, 1);
    Unit@ _ := io.stdout(Strs[
      /Core/Int/Show%.Show(iterations),
      Str|'	 trees of depth ', /Core/Int/Show%.Show(depth),
      Str|'	 check: ', /Core/Int/Show%.Show(check), Str[Chars.nl]]);
    ForDepth(Add(2, depth));
  };
  Unit@ _ := ForDepth(MinDepth);
  Unit@ _ := io.stdout(Strs[
    Str|'long lived tree of depth ',
    /Core/Int/Show%.Show(max_depth),
    Str|'	 check: ', /Core/Int/Show%.Show(Check(long_lived)), Str[Chars.nl]]);
  !(True);
};

@(Main);
