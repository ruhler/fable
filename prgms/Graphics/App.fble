
@ App@ = /App%.App@;
@ Event@ = /App%.Event@;
@ Effect@ = /App%.Effect@;

% True = /Bool%.True;
% False = /Bool%.False;

@ Drawing@ = /Drawing%.Drawing@;
@ Point@ = /Drawing%.Point@;
% Color = /Drawing%.Color;
% Drawings = /Drawing%.Drawings;
% Over = /Drawing%.Over;
% Rect = /Drawing%.Rect;
% Affine = /Drawing%.Affine;

@ Vec@ = /Graphics/3D%.Vec@;

@ Camera@ = /Graphics/Camera%.Camera@;
% Camera = /Graphics/Camera%.Camera;
% Translate = /Graphics/Camera%.Translate;
% RotateY = /Graphics/Camera%.RotateY;
% Focus = /Graphics/Camera%.Focus;
% Triangle = /Graphics/Camera%.Triangle;
% Vertex = /Graphics/Camera%.Vertex;

@ Int@ = /Int/Int%.Int@;
% Neg = /Int/Int%.Neg;

% Int = /Int/Int/Lit%.Int;

<@>@ Get@ = /Process%.Get@;
<@>@ Put@ = /Process%.Put@;

@ Unit@ = /Unit%.Unit@;
% Unit = /Unit%.Unit;

@ State@ = Camera@;

State@ Initial = Camera(Int|10);

(State@) { Drawing@; } Draw = (State@ camera) {
  Vec@ a = Vertex(camera, Vec@(Neg(Int|5), Neg(Int|10), Int|20));
  Vec@ b = Vertex(camera, Vec@(Int|0, Neg(Int|10), Int|30));
  Vec@ c = Vertex(camera, Vec@(Int|5, Neg(Int|10), Int|20));

  Vec@ d = Vertex(camera, Vec@(Neg(Int|5), Int|10, Int|20));
  Vec@ e = Vertex(camera, Vec@(Int|0, Int|10, Int|30));
  Vec@ f = Vertex(camera, Vec@(Int|5, Int|10, Int|20));

  Drawings[
    Triangle(camera, a, b, c, Color.Cyan),
    Triangle(camera, d, f, e, Color.Green)
  ];
};

(Get@<Event@>, Put@<State@>, Unit@!, State@) { Unit@!; }
Run = (Get@<Event@> in, Put@<State@> draw, Unit@! tick, State@ state) {
  Event@ e := in;

  e.?(tick: {
    Unit@ _ := tick;
    Unit@ _ := draw(state);
    Run(in, draw, tick, state);
  });

  e.?(key_down: {
    e.key_down.?(q: !(Unit));

    State@ new_state = e.key_down.?(
      a: Translate(Neg(Int|1), Int|0, Int|0, state),
      d: Translate(Int|1, Int|0, Int|0, state),
      j: Focus(Int|1, state),
      k: Focus(Neg(Int|1), state),
      s: Translate(Int|0, Int|0, Neg(Int|1), state),
      w: Translate(Int|0, Int|0, Int|1, state),

      left: RotateY(False, state),
      right: RotateY(True, state),
      : state);
    Run(in, draw, tick, new_state);
  });
  Run(in, draw, tick, state);
};

App@ Main = (Int@ width, Int@ height, Get@<Event@> in, Put@<Effect@> out) {
  # The window should be centered at (0, 0), ranging in from
  # (-32, -24) to (32, 24).
  Point@ a = @(x: Int|10, y: Neg(Int|10));
  Point@ b = @(x: Int|320, y: Int|240);

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  Unit@! tick = out(Effect@(tick: frame_period));

  (State@) { Unit@!; } draw = (State@ s) {
    out(Effect@(draw: Over(
      Rect(Int|0, Int|0, width, height, Color.Black),
      Affine(a, b, Draw(s)))));
  };

  Unit@ _ := out(Effect@(tick: Int|0));
  Run(in, draw, tick, Initial); 
};

Main;
