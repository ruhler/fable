
% True = /Core/Bool%.True;
% False = /Core/Bool%.False;

<@>@ Eq@ = /Core/Eq%.Eq@;

@ Profile@ = /Pprof/Profile%.Profile@;
@ LocId@ = /Pprof/Profile%.LocId@;
@ Location@ = /Pprof/Profile%.Location@;
@ Sequence@ = /Pprof/Profile%.Sequence@;
@ Sample@ = /Pprof/Profile%.Sample@;

Eq@<LocId@> LocId = /Core/String/Eq%.Eq;

Eq@<Sequence@> Sequence = /Core/List/Eq%.Eq(LocId);

Eq@<Sample@> Sample = (Sample@ a, Sample@ b) {
  /Core/List/Eq%.Eq(/Core/Int/Eq%.Eq, a.metrics, b.metrics).?(false: False);
  Sequence(a.path, b.path).?(false: False);
  True;
};

Eq@<Location@> Location = (Location@ a, Location@ b) {
  LocId(a.id, b.id).?(false: False);
  /Core/String/Eq%.Eq(a.name, b.name).?(false: False);
  /Core/String/Eq%.Eq(a.file, b.file).?(false: False);
  /Core/Int/Eq%.Eq(a.line, b.line).?(false: False);
  /Core/Int/Eq%.Eq(a.col, b.col).?(false: False);
  True;
};

Eq@<Profile@> Profile = (Profile@ a, Profile@ b) {
  /Core/List/Eq%.Eq(/Core/String/Eq%.Eq, a.metrics, b.metrics).?(false: False);
  /Core/List/Eq%.Eq(Sample, a.samples, b.samples).?(false: False);
  /Core/List/Eq%.Eq(Location, a.locations, b.locations).?(false: False);
  True;
};

@(Profile, Sample, Sequence, Location, LocId);
