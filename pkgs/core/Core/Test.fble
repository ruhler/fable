
<@>@ List@ = /Core/List%.List@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

@ TestResult@ = +(Unit@ pass, String@ fail, Unit@ xfail);
@ TestCase@ = *(String@ name, (Unit@) { TestResult@; } test);
@ TestSuite@ = *(String@ name, List@<Test@> tests),
@ Test@ = +(TestCase@ case, TestSuite@ suite);

# A passing test result.
TestResult@ Pass = TestResult@(pass: Unit);

# A failing test result.
(String@) { TestResult@; } Fail = (String@ msg) {
  TestResult@(fail: msg);
};

# An expected failing test result.
TestResult@ ExpectedFail = TestResult@(xfail: Unit);

# Test --
#   Creates an individual test.
#
# Inputs:
#   name - the name of the test
#   test - a function that returns the result of the test
(String@, (Unit@) { TestResult@; }) { Test@; }
Test = (String@ name, (Unit@) { TestResult@; } test) {
  Test@(case: TestCase@(name, test));
};

# TestSuite --
#   Creates a suite of tests.
#
# Inputs:
#   name - the name of the test suite.
#   tests - the list of tests in the test suite.
(String@, List@<Test@>) { Test@; }
TestSuite = (String@ name, List@<Test@> tests) {
  Test@(suite: TestSuite@(name, tests));
};

# ExpectFails --
#   Change the test result to expected failure if the given test result is
#   failing and msg is contained somewhere in the failure message.
(TestResult@, String@) { TestResult@; }
ExpectFails = (TestResult@ result, String@ msg) {
  result.?(
    pass: Fail|'Expected failure, but test passed',
    fail: /Core/String/Eq%.Contains(result.fail, msg).?(
      true: ExpectedFail,
      false: Fail(Strs[
        Str|'Expected failure containing "', msg,
        Str|'", but failed with "', result.fail, Str|'"'])),
    xfail: Fail|'Expected failure, but was xfail');
};

@(TestResult@, Test@, TestSuite@, TestCase@,
  Pass, Fail, ExpectedFail,
  ExpectFails, Test, TestSuite);
