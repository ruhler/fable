% True = /Core/Bool%.True;
% False = /Core/Bool%.False;
% And = /Core/Bool%.And;

<@>@ Eq@ = /Core/Eq%.Eq@;

% Int = /Core/Int/Lit%.Int;

<@>% List = /Core/List%.List;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;

@ Test@ = /Core/Test%.Test@;
@ TestResult@ = /Core/Test%.TestResult@;
% Test = /Core/Test%.Test;
% TestSuite = /Core/Test%.TestSuite;
<@>% AssertEquals = /Core/Test%.AssertEquals;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

<<@>@>% NoDebug = /Sat/Debug%.NoDebug;

@ Clause@ = /Sat/Solver%.Clause@;
@ VarId@ = /Sat/Solver%.VarId@;
@ Var@ = /Sat/Solver%.Var@;
@ Assignment@ = /Sat/Solver%.Assignment@;
@ Result@ = /Sat/Solver%.Result@;
% AddClauses = /Sat/Solver%.AddClauses;
% NewSolver = /Sat/Solver%.NewSolver;
% EqId = /Sat/Solver%.EqId;

@ Formula@ = /Sat/Dimacs%.Formula@;

% ShowResult = /Sat/Show%.ShowResult;
% ShowParsedFormula = /Sat/Show%.ShowParsedFormula;

<@>@ Parse@ = /Sat/Dimacs%.Parse@;
% Parse = /Sat/Dimacs%.Parse;



Eq@<Var@> EqVar = (Var@ a, Var@ b) {
  And(EqId(a.id, b.id), /Core/Bool/Eq%.Eq(a.pos, b.pos));
};

Eq@<Clause@> EqClause = /Core/List/Eq%.Eq<Var@>(EqVar);

Eq@<Formula@> EqFormula = /Core/List/Eq%.Eq<Clause@>(EqClause);


Eq@<Parse@<Formula@>>
EqParsedFormula = (Parse@<Formula@> a, Parse@<Formula@> b) {
  a.?(error: b.?(error: /Core/String/Eq%.Eq(a.error, b.error), : False),
      parsed: b.?(parsed: EqFormula(a.parsed, b.parsed), : False));
};

Eq@<Assignment@> EqAssignment = /Core/List/Eq%.Eq<Var@>(EqVar);

Eq@<Result@> EqResult = (Result@ a, Result@ b) {
  a.?(
    sat: b.?(sat: EqAssignment(a.sat, b.sat), : False),
    unsat: b.?(unsat: True, : False));
};

(Result@, Result@) { TestResult@; }
AssertEq = (Result@ a, Result@ b) {
  AssertEquals<Result@>(EqResult, ShowResult, a, b);
};

VarId@ X = /Core/Int%.1;
VarId@ Y = /Core/Int%.2;
VarId@ Z = /Core/Int%.3;

(Formula@) { Result@; } Solve = {
  <@>@ M@ = /Core/Monad/Pure%.Pure@;
  Monad@<M@> m = /Core/Monad/Pure%.Monad;
  % solve = /Sat/Solver%.Solve<M@>(m, NoDebug<M@>(m));
  (Formula@ formula) {
    solve(AddClauses(NewSolver, formula));
  };
};

Test@ Tests = TestSuite(Str|Sat, List[
  Test(Str|Sat, (Unit@ _) {
    Formula@ f = List[
      List[Var@(X, True)]
    ];
    Result@ want = Result@(sat: List[Var@(X, True)]);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|Unsat, (Unit@ _) {
    Formula@ f = List[
      List[Var@(X, True)],
      List[Var@(X, False)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|EmptyClause, (Unit@ _) {
    # Test that we properly detect an empty clause as one of the initial
    # clauses.
    Formula@ f = List[
      List[Var@(X, True)],
      List<Var@>[],
      List[Var@(Y, True)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|AttemptedConflict, (Unit@ _) {
    # Test that attempts to cover the case where an attempted variable
    # assignment leads to a conflict.
    # (x' + y)(x' + y')(x + y)(x + y')
    Formula@ f = List[
      List[Var@(X, False), Var@(Y, True)],
      List[Var@(X, False), Var@(Y, False)],
      List[Var@(X, True), Var@(Y, True)],
      List[Var@(X, True), Var@(Y, False)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|AttemptedConflict2, (Unit@ _) {
    # Another test that attempts to cover the case where an attempted variable
    # assignment leads to a conflict.
    # (x' + x')(x + x)
    Formula@ f = List[
      List[Var@(X, False), Var@(X, False)],
      List[Var@(X, True), Var@(X, True)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|Unsat3, (Unit@ _) {
    # Regression test for a bug where we weren't properly applying learned
    # clauses, which led to an infinite loop.
    Formula@ f = List[
      List[Var@(X, True), Var@(Y, True), Var@(Z, True)],
      List[Var@(X, True), Var@(Y, True), Var@(Z, False)],
      List[Var@(X, True), Var@(Y, False), Var@(Z, True)],
      List[Var@(X, True), Var@(Y, False), Var@(Z, False)],
      List[Var@(X, False), Var@(Y, True), Var@(Z, True)],
      List[Var@(X, False), Var@(Y, True), Var@(Z, False)],
      List[Var@(X, False), Var@(Y, False), Var@(Z, True)],
      List[Var@(X, False), Var@(Y, False), Var@(Z, False)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|EmptyConflict, (Unit@ _) {
    # Regression test for a bug where we saw an empty conflict clause.
    VarId@ a = Int|1;
    VarId@ b = Int|2;
    VarId@ c = Int|3;
    VarId@ d = Int|4;
    VarId@ e = Int|5;
    Formula@ f = List[
      List[Var@(a, False), Var@(b, True)],
      List[Var@(a, True), Var@(b, False)],
      List[Var@(a, False), Var@(b, False)],
      List[Var@(a, True), Var@(b, True)],
      List[Var@(e, True), Var@(c, True)],
      List[Var@(c, True), Var@(e, False), Var@(d, True)],
      List[Var@(d, False), Var@(c, False)]
    ];
    Result@ want = Result@(unsat: Unit);
    Result@ got = Solve(f);
    AssertEq(want, got);
  }),

  Test(Str|Dimacs, (Unit@ _) {
    String@ dimacs = Str|'c
c start with comments
c
c
p cnf 5 3
1 -5 4 0
-1 5 3 4 0
-3 -4 0
';
    Formula@ formula = List[
      List[Var@(Int|1, True), Var@(Int|5, False), Var@(Int|4, True)],
      List[Var@(Int|1, False), Var@(Int|5, True), Var@(Int|3, True), Var@(Int|4, True)],
      List[Var@(Int|3, False), Var@(Int|4, False)]];

    Parse@<Formula@> want = Parse@(parsed: formula);
    Parse@<Formula@> got = Parse(dimacs);
    AssertEquals(EqParsedFormula, ShowParsedFormula, want, got);
  })

]);

/Core/Stdio/IO%.Run(/Core/Test/Stdio%.Run(Tests));
