
@ Int@ = /Core/Int%.Int@;

<@>@ List@ = /Core/List%.List@;
<@>% Cons = /Core/List%.Cons;

<<@>@>@ Monad@ = /Core/Monad%.Monad@;

<<@>@>@ IStream@ = /Core/Stream%.IStream@;

@ String@ = /Core/String%.String@;

% Map = /Core/Int/Map%.Map;

@ Block@ = /Pprof/Profile%.Block@;
@ Blocks@ = /Pprof/Profile%.Blocks@;
@ Sample@ = /Pprof/Profile%.Sample@;
@ Profile@ = /Pprof/Profile%.Profile@;

@ Function@ = /Pprof/Parser/Profile%.Function@;
@ Location@ = /Pprof/Parser/Profile%.Location@;
@ LocationId@ = /Pprof/Parser/Profile%.LocationId@;
@ StringId@ = /Pprof/Parser/Profile%.StringId@;
@ ProtoProfile@ = /Pprof/Parser/Profile%.Profile@;
<<@>@>% ProfileParser = /Pprof/Parser/Profile%.Profile;

<@>% Run = /Pprof/Parser/Proto%.Run;

<@>@ Result@ = /Pprof/Result%.Result@;

# @func[Metrics] Extracts list of metrics names from parsed profile
#  @arg[Map@<String@>][strings] The string table
#  @arg[List@<StringId@>][sample_types]
#   List of parsed sample types to extract from.
#  @arg[List@<String@>][metrics] List of already extracted metrics.
#  @returns[List@<String@>] The complete list of extracted metrics.
(Map.Map@<String@>, List@<StringId@>, List@<String@>) { List@<String@>; }
Metrics = (
  Map.Map@<String@> strings,
  List@<StringId@> sample_types,
  List@<String@> metrics) {
  sample_types.?(nil: metrics);

  StringId@ sample_type = sample_types.cons.head;
  String@ metric = Map.Lookup(strings, sample_type).just;
  Metrics(strings, sample_types.cons.tail, Cons(metric, metrics));
};

# Extracts info about a block from the profile.
(ProtoProfile@, Location@) { Block@; }
Block = (ProtoProfile@ parsed, Location@ location) {
  Function@ function = Map.Lookup(parsed.functions, location.func_id).just;

  String@ name = Map.Lookup(parsed.strings, function.name).just;
  String@ file = Map.Lookup(parsed.strings, function.file).just;
  Int@ line = location.line;
  Int@ column = location.col;
  @(name, file, line, column);
};

(ProtoProfile@) { Blocks@; }
Blocks = (ProtoProfile@ parsed) {
  Map.ForEach(parsed.locations, Map.Empty<Block@>,
    (LocationId@ id, Location@ location, Blocks@ blocks) {
       Map.Insert(blocks, id, Block(parsed, location));
    });
};

(ProtoProfile@) { Profile@; }
ProfileFromParsed = (ProtoProfile@ parsed) {
  List@<String@> metrics = Metrics(parsed.strings, parsed.sample_types)[];
  Blocks@ blocks = Blocks(parsed); 
  List@<Sample@> samples = /Core/List%.Reverse(parsed.samples);

  @(metrics, blocks, samples);
};

<<@>@ M@>(IStream@<M@>, Monad@<M@>) { M@<Result@<Profile@>>; }
Parse = <<@>@ M@>(IStream@<M@> in, Monad@<M@> m) {
  Result@<ProtoProfile@> parsed <- m.do(Run(ProfileParser, in, m));
  m.return(/Pprof/Result%.Apply(ProfileFromParsed, parsed));
};

@(Parse);
