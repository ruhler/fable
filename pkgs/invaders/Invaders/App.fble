
% True = /Core/Bool%.True;

@ Int@ = /Core/Int%.Int@;
% 0 = /Core/Int%.0;
% Neg = /Core/Int%.Neg;
% Max = /Core/Int/Eq%.Max;

% Int = /Core/Int/Lit%.Int;

<@>@ List@ = /Core/List%.List@;
<@>% Contains = /Core/List/Eq%.Contains;
<@>@ Maybe@ = /Core/Maybe%.Maybe@;
<<@>@>@ Monad@ = /Core/Monad%.Monad@;
<<@>@>@ Stdio@ = /Core/Stdio%.Stdio@;
<<@>@>@ IStream@ = /Core/Stream%.IStream@;
<<@>@>@ OStream@ = /Core/Stream%.OStream@;

@ String@ = /Core/String%.String@;
% Str = /Core/String%.Str;
% Strs = /Core/String%.Strs;

@ Unit@ = /Core/Unit%.Unit@;
% Unit = /Core/Unit%.Unit;

<<@>@>@ App@ = /App/App%.App@;
<<@>@>@ Main@ = /App/App%.Main@;
@ Event@ = /App/App%.Event@;
@ Effect@ = /App/App%.Effect@;

@ Point@ = /App/Drawing%.Point@;
% Color = /App/Drawing%.Color;
% Over = /App/Drawing%.Over;
% Rect = /App/Drawing%.Rect;
% Affine = /App/Drawing%.Affine;

% Game = /Invaders/Game%;
@ Game@ = Game.Game@;

@ Dir@ = /Invaders/Ship%.Dir@;

@ Input@ = +(
  Unit@ tick,
  Unit@ quit,
  Game.Input@ game
);

Input@ Left = Input@(game: Game.Input@(dir: Dir@(left: Unit)));
Input@ Right = Input@(game: Game.Input@(dir: Dir@(right: Unit)));
Input@ Stopped = Input@(game: Game.Input@(dir: Dir@(stopped: Unit)));
Input@ Fire = Input@(game: Game.Input@(fire: Unit));

Main@ Main = <<@>@ M@>(Monad@<M@> m, Stdio@<M@> stdio, App@<M@> app, Int@ width, Int@ height, List@<String@> args) {
  % O = /Core/Stream/OStream%(m);

  Contains(/Core/String/Eq%.Eq, args, Str|'--help').?(true: {
    Unit@ _ <- m.do(O.PutStr(stdio.out, /Invaders/Usage%));
    m.return(True);
  });

  M@<Input@> GetInput = {
    Event@ e <- m.do(app.event);
    e.?(
        tick: m.return(Input@(tick: Unit)),
        key_down: e.key_down.?(
          q: m.return(Input@(quit: Unit)),
          left: m.return(Left),
          right: m.return(Right),
          space: m.return(Fire),
          : GetInput),
        key_up: e.key_up.?(
          left: m.return(Stopped),
          right: m.return(Stopped),
          : GetInput),
        : GetInput
     );
  };

  # Map game coordinates into screen coordinate.
  Point@ a = @(x: Int|2, y: Neg(Int|2));
  Point@ b = @(x: Int|0, y: Int|480);

  (Game@) { M@<Unit@>; } Draw = (Game@ g) {
    app.effect(Effect@(draw: Over(
      Rect(0, 0, width, height, Color.Black),
      Affine(a, b, Game.Draw(g)))));
  };

  # Returns the high score after playing the game.
  (M@<Unit@>, Game@) { M@<Int@>; }
  Run = (M@<Unit@> tick, Game@ game) {
    Input@ i <- m.do(GetInput);
    i.?(
       tick: {
         Unit@ _ <- m.do(tick);
         Game@ ngame = Game.Tick(game);
         Unit@ _ <- m.do(Draw(ngame));
         Run(tick, ngame);
       },
       quit: m.return(Max(game.score, game.high_score)),
       game: Run(tick, Game.Input(game, i.game)));
  };

  # The target is 60 FPS, or 16 millseconds per frame.
  Int@ frame_period = Int|16;
  M@<Unit@> tick = app.effect(Effect@(tick: frame_period));

  # Read High Score
  % I = /Core/Stream/IStream%(m);
  Int@ high_score <- m.do({
    Maybe@<String@> home <- m.do(stdio.getenv(Str|HOME));
    home.?(nothing: m.return(Int|0));

    String@ filename = Strs[home.just, Str|'/.fble-invaders'];
    Maybe@<IStream@<M@>> istream <- m.do(stdio.read(filename));
    istream.?(nothing: m.return(Int|0));

    String@ text <- m.do(I.GetText(istream.just));
    m.return(/Core/Int/Show%.Read(text));
  });

  # Set up the game.
  Game@ game = Game.Initial(high_score);
  Unit@ _ <- m.do(app.effect(Effect@(tick: Int|0)));

  # Check for --test mode, where we draw something to the screen and exit
  # right away.
  Contains(/Core/String/Eq%.Eq, args, Str|'--test').?(true: {
    Unit@ _ <- m.do(Draw(game));
    m.return(True); 
  });

  # Play the game
  Int@ new_high_score <- m.do(Run(tick, game));

  # Save high score.
  Unit@ _ <- m.do({
    Maybe@<String@> home <- m.do(stdio.getenv(Str|HOME));
    home.?(nothing: m.return(Unit));

    String@ filename = Strs[home.just, Str|'/.fble-invaders'];
    Maybe@<OStream@<M@>> ostream <- m.do(stdio.write(filename));
    ostream.?(nothing: m.return(Unit));

    Unit@ _ <- m.do(O.PutStr(ostream.just, /Core/Int/Show%.Show(new_high_score)));
    O.Close(ostream.just);
  });

  m.return(True);
};

Main;
