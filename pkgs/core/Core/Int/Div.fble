
@ Int@ = /Core/Int%.Int@;
% Decr = /Core/Int%.Decr;
% Neg = /Core/Int%.Neg;
% Sub = /Core/Int%.Sub;
% 0 = /Core/Int%.0;

% P = /Core/Int/IntP/Div%;

# QR@ --
#   Quotient and remainder from integer division.
@ QR@ = *(Int@ q, Int@ r);

% DivideByZero = /Core/Undef%.Undef<QR@>;

# Div --
#   a = q*b + r, |r| < |b|, where q = floor(a/b)
(Int@, Int@) { QR@; } Div = (Int@ a, Int@ b) {
  b.?(
    n: {
      QR@ qr = Div(Neg(a), Neg(b));
      @(q: qr.q, r: Neg(qr.r));
    },
    0: DivideByZero.undefined,
    p: {
      a.?(
        n: {
          P.QR@ qr = P.Div(a.n, b.p);
          Int@ q = Neg(qr.q);
          qr.r.?(0: @(q: q, r: 0));
          @(q: Decr(q), r: Sub(b, qr.r));
        },
        0: @(q: 0, r: 0),
        p: P.Div(a.p, b.p));
      });
};

@(QR@, Div);
