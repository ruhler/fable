
@ Bool@ = /Bool%.Bool@;
% True = /Bool%.True;
% False = /Bool%.False;
% Not = /Bool%.Not;
% And = /Bool%.And;
% Or = /Bool%.Or;
% Eq = /Bool/Eq%.Eq;

% Chars = /Char%.Chars;

<@>@ CharLit@ = /String%.CharLit@;

@ Test@ = /Test%.Test@;
@ TestSuite@ = /Test%.TestSuite@;
% Test = /Test%.Test;
<@>% AssertEquals = /Test%.AssertEquals;
% TestSuite = /Test%.TestSuite;

# T --
#  Tests that a boolean expression evaluates to True.
#
# Inputs:
#  name - the name of the test.
#  p - a process that evaluates and returns the result of the boolean
#      expression to test.
#
# Results:
#  A test case that tests the result of evaluating the boolean expression is
#  True.
#
# Example Use:
#  T(Chars|Test_True_Is_True, !(True));
(CharLit@, Bool@!) { Test@; } T = (CharLit@ name, Bool@! p) {
  Test(name, {
    Bool@ x := p;
    !(AssertEquals<Bool@>(Eq, /Bool/Show%.Show, True, x));
  });
};

# F --
#  Tests that a boolean expression evaluates to False.
#
# Inputs:
#  name - the name of the test.
#  p - a process that evaluates and returns the result of the boolean
#      expression to test.
#
# Results:
#  A test case that tests the result of evaluating the boolean expression is
#  False.
#
# Example Use:
#  F(Chars|Test_False_Is_False, !(False));
(CharLit@, Bool@!) { Test@; } F = (CharLit@ name, Bool@! p) {
  Test(name, {
    Bool@ x := p;
    !(AssertEquals<Bool@>(Eq, /Bool/Show%.Show, False, x));
  });
};

# Tests --
#  A test suite to test the /Bool% module.
TestSuite@ Tests = TestSuite(Chars|Bool, [
  T(Chars|True, !(True)),
  F(Chars|False, !(False)),

  F(Chars|Not_True, !(Not(True))),
  T(Chars|Not_False, !(Not(False))),

  T(Chars|And_True_True, !(And(True, True))),
  F(Chars|And_True_False, !(And(True, False))),
  F(Chars|And_False_True, !(And(False, True))),
  F(Chars|And_False_False, !(And(False, False))),

  T(Chars|Or_True_True, !(Or(True, True))),
  T(Chars|Or_True_False, !(Or(True, False))),
  T(Chars|Or_False_True, !(Or(False, True))),
  F(Chars|Or_False_False, !(Or(False, False))),

  T(Chars|Eq_True_True, !(Eq(True, True))),
  F(Chars|Eq_True_False, !(Eq(True, False))),
  F(Chars|Eq_False_True, !(Eq(False, True))),
  T(Chars|Eq_False_False, !(Eq(False, False)))
]);

@(Tests);
