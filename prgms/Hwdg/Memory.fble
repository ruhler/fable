
@ Coord@ = /Hwdg/Coord%.Coord@;
@ Data@ = /Hwdg/Data%.Data@;

@ Int@ = /Core/Int%.Int@;

<@>@ Maybe@ = /Maybe%.Maybe@;
<@>@ List@ = /Core/List%.List@;

# Memory@ --
#   An unbounded 2D array of memory.
#
# Each location in the memory is either empty or contains data 0 or 1.
@ Memory@ = /Map%.Map@<Coord@, Data@>;

# Empty -- 
#   A completely empty memory.
Memory@ Empty = /Map%.Empty<Coord@, Data@>;

# Get --
#   Get the value of memory at a particular location.
(Memory@, Coord@) { Maybe@<Data@>; } Get = {
  /Map%.Lookup<Coord@>(/Hwdg/Coord%.Lt)<Data@>;
};

# Put -- 
#   Set the data value at a particular location.
(Memory@, Coord@, Data@) { Memory@; } Set = {
  /Map%.Insert<Coord@>(/Hwdg/Coord%.Lt)<Data@>;
};

# Clear --
#   Clear the data value of a particular location, leaving the location empty.
(Memory@, Coord@) { Memory@; } Clear = {
  /Map%.Delete<Coord@>(/Hwdg/Coord%.Lt)<Data@>;
};

# Eq --
#   Test whether two memories contains the same contents.
/Eq%.Eq@<Memory@> Eq = {
  /Map/Eq%.Eq<Coord@, Data@>(/Hwdg/Coord%.Eq, /Hwdg/Data%.Eq);
};

/Show%.Show@<Memory@> Show = {
  /Map/Show%.Show<Coord@, Data@>(/Hwdg/Coord%.Show, /Hwdg/Data%.Show);
};

# ForEach --
#   Iterate over non-empty values in the memory.
#
# Type Parameters:
#   B@ - The type of value updated and returned by the foreach loop.
#
# Inputs:
#   memory - the memory to iterate over.
#   base - the initial value to update and return from the loop.
#   body - the body of the loop.
#
# Returns:
#   The result of applying the body consecutively to the base value, once for
#   each entry in the map.
#
# Conceptually:
#   B@ b = base
#   foreach (coord, data) in memory:
#     b = body(k, coord, data)
#   return b
<@ B@>(Memory@, B@, (Coord@, Data@, B@) { B@; }) { B@; }
ForEach = <@ B@> { /Map%.ForEach<Coord@, Data@, B@>; };

# Cell@ --
#   Describes the data value at a particular location in memory.
@ Cell@ = *(Int@ x, Int@ y, Data@ data);

# Memory --
#   Construct an initial memory based on a list of Cell@s.
(List@<Cell@>) { Memory@; } Memory = (List@<Cell@> cells) {
  /Core/List%.ForEach<Cell@, Memory@>(cells, Empty, (Cell@ cell, Memory@ memory) {
    Set(memory, Coord@(cell.x, cell.y), cell.data);
  });
};

@(Memory@, Cell@, Memory, Empty, Get, Set, Clear, Eq, Show, ForEach);
